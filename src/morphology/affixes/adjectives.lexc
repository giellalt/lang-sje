! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or divvun@samediggi.no



! ========== !
!! !!!Adjectives !
! ========== !

!! Här är lexikon för adjektivsböjning.
!! Först jämnstaviga, så uddastaviga, och sen nån gång kontrakta.
!! Efteråt kommer morfologin, som också spiller över i nouns.lexc.

LEXICON A_EVEN_A !!= @CODE@ är likastaviga med a-attr
+A+Attr:%^WG%>a # ;
A_EVEN ;
+A:%^WG COMPSUP ;

LEXICON A_EVEN_AA !!= @CODE@ är likastaviga ocskå med a-attr ???!!
+A+Attr:%>a # ;
A_EVEN ;
+A:%^WG COMPSUP ; !ändra vg

LEXICON A_EVEN_E !!= @CODE@ är likastaviga med e-attr
+A+Attr:%^WG%>e # ;
A_EVEN ;
+A:%^WG COMPSUP ;

LEXICON A_EVEN_IES !!= @CODE@ 
+A+Attr:%^WG%>es # ;
A_EVEN ;
+A:%^WG COMPSUP ;

LEXICON A_EVEN_B !!= @CODE@ 
+A+Attr:%^WG%>s # ;
A_EVEN ;
+A:%^WG COMPSUP ;

LEXICON A_EVEN_0 !!= @CODE@ 
+A+Attr: # ;
A_EVEN ;
+A:%^WG COMPSUP ;

LEXICON A_EVEN_NOCG_0 !!= @CODE@ 
+A+Attr: # ;
A_EVEN_NOCG ;
+A: COMPSUP ;

LEXICON A_EVEN_NOCG_S !!= @CODE@ 
+A+Attr:%>s # ; 
A_EVEN_NOCG ;
+A: COMPSUP_A ;

LEXICON A_EVEN !!= @CODE@ 
+A: EVENCASE ;  ! Nom, Ess, Ill
+A:%^WG N_EVEN_W ; ! CG: Acc, Gen, etc.
+A:%^WG COMPSUP ;

LEXICON A_EVEN_NOCG !!= @CODE@ 
+A: EVENCASE ;
+A: N_EVEN_W ; ! No CG here
+A:%^WG COMPSUP ;


LEXICON COMPSUP !!= @CODE@ 
+Comp+Sg+Nom:%^WG%>p # ;
+Comp+Attr:%^WG%>p # ;
+Comp:%^WG%>b ODD_OBLIQUE_U_COMMON ;

+Superl+Sg+Nom:%^WG%>mus # ;
+Superl+Attr:%^WG%>mus # ;
+Superl:%^WG%>mussa EVENCASE ;
+Superl:%^WG%>mussa%^WG  N_EVEN_W ; ! CG here

LEXICON COMPSUP_A !!= @CODE@ 
+Comp+Sg+Nom:%>p # ;
+Comp+Attr:%>p # ;
+Comp:%>b ODD_OBLIQUE_U_COMMON ;

+Superl+Sg+Nom:%^WG%>mus # ;
+Superl+Attr:%^WG%>mus # ;
+Superl:%^WG%>mussa EVENCASE ;
+Superl:%^WG%>mussa%^WG  N_EVEN_W ; ! CG here



LEXICON A_ODD !!= @CODE@ är uddastaviga
+A+Attr:is # ;
+A+Sg+Nom: PX ;
+A: ODD_OBLIQUE_COMMON ;
+A: ODD_COMPSUP ;

LEXICON A_ODD_GIS !!= @CODE@ är  d
+A+Attr:gis # ; 
+A+Sg+Nom:k PX ;
+A:g ODD_OBLIQUE_COMMON ;
+A:g ODD_COMPSUP ;


LEXICON A_ODD_B
+A+Attr:mes # ;
+A+Sg+Nom:mes PX ;
+A:bmás ODD_OBLIQUE_COMMON ;
+A:bmás ODD_COMPSUP ;


LEXICON A_ODD_R
+A+Attr:as # ;
+A+Sg+Nom:as PX ;
+A:ras ODD_OBLIQUE_COMMON ;
+A:ras ODD_COMPSUP ;


LEXICON A_ODD_AA
+A+Attr:es # ;
+A+Sg+Nom:es PX ;
+A:ás ODD_OBLIQUE_COMMON ;
+A:ás ODD_COMPSUP ;

LEXICON A_ODD_C
+A+Attr:des # ;
+A+Sg+Nom:des PX ;
+A:vdás ODD_OBLIQUE_COMMON ;
+A:vdás ODD_COMPSUP ;

LEXICON A_ODD_D
+A+Attr:kes # ;
+A+Sg+Nom:kes PX ;
+A:skás ODD_OBLIQUE_COMMON ;
+A:skás ODD_COMPSUP ;

LEXICON A_ODD_E
+A+Attr:es # ;
+A+Sg+Nom:es PX ;
+A:pás ODD_OBLIQUE_COMMON ;
+A:pás ODD_COMPSUP ;

LEXICON A_ODD_F
+A+Attr:es # ;
+A+Sg+Nom:es PX ;
+A:rás ODD_OBLIQUE_COMMON ;
+A:rás ODD_COMPSUP ;

LEXICON A_ODD_K
+A+Attr:k # ; 
+A+Sg+Nom:k PX ;
+A:g ODD_OBLIQUE_COMMON ;
+A: ODD_COMPSUP ;



LEXICON A_ODD_T
+A+Attr:ähppis # ; 
+A+Sg+Nom:áhppat PX ;
+A:áhppad ODD_OBLIQUE_COMMON ;
+A:áhppad ODD_COMPSUP ;


LEXICON A_ODD_SIS
+A+Attr:sis # ; 
+A+Sg+Nom:s PX ;
+A:s ODD_OBLIQUE_COMMON ;
+A: ODD_COMPSUP ;

LEXICON A_ODD_DIS
+A+Attr:dis # ; 
+A+Sg+Nom:t PX ;
+A:d ODD_OBLIQUE_COMMON ;
+A:d ODD_COMPSUP ;



LEXICON A_ODD_k_K
+A+Attr:k # ; 
+A+Sg+Nom:k PX ;
+A:g ODD_OBLIQUE_COMMON ;
+A:g ODD_COMPSUP ;


LEXICON ODD_COMPSUP
+Comp+Sg+Nom:%>up # ;
+Comp:%>ubbo N_EVEN_W ; !ändra till ej stv, jämn.st.o-stam
+Comp:%>ubbo               N_EVEN_S ;
+Comp:%>ubboj     N_EVEN_J ;

+Superl+Nom:%>umos # ;
+Superl:%>ubmus ODD_OBLIQUE_COMMON ; 

	

