! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or feedback@divvun.no




! ========== !
! Inflection !
! ========== !

!juollkie+N+Abe             morf =======%> upper
!juollkie^WG%>htahk   trans
!                                     
!juollkie^WG%>Abehtahk   mphon
!juol0kie00htahk         trans ======%> lower

! Noun 
! ===============


! even-syllabic
! -------------

LEXICON N_EVEN ! giella, guolle
+N: EVENCASE ;  ! Sg Nom, Ess
+N: N_EVEN_ILL ;
+N:^WG N_EVEN_WK ; ! CG here

LEXICON N_EVEN_O ! rássjo-variant rässjuj
+N: EVENCASE ;  ! Sg Nom, Ess
+N: N_EVEN_ILL ; 
+N: N_EVEN_ILL_O ; ! allows rássjo-variant: rässjuj
+N:^WG N_EVEN_WK ; ! CG here

LEXICON N_EVEN_NOCG ! without consonant gradation, e.g. bägga<->biegga
+N:^G3 EVENCASE ;
+N:^G3 N_EVEN_ILL ;
+N:^WG N_EVEN_WK ; ! ^WG necessary for words like vuodda<->vuaddag-

LEXICON EVENCASE
+Sg+Nom:^UAUML    K ;
N_EVEN_ESS ; ! Essive

LEXICON N_EVEN_ILL
+Sg+Ill:%>j^V2E2AA K ; !V2 e->á in ill.sg, dim

LEXICON N_EVEN_ILL_O
+Sg+Ill+Use/NG:%>j^V2O2U K ; !V2 o->u in ill.sg, dim

LEXICON N_EVEN_ESS
+Ess:^UAUML%>n K ;

LEXICON N_EVEN_WK
+Der/Dim:%>tj^V2E2AA N_ODD ;
+Sg+Gen:%>0  K ;
+Sg+Acc:%>v  K ;
+Sg+Ine:%>n K ;
+Sg+Ela+Err/Orth:%>s K ;
+Sg+Ela:%>st K ;
+Abe:%>dak K ; ! Norm
!+Abe:%>htaka K ; ! Informants S and Bj 
!+Abe:%>htak K ; ! Informants S and Bj 
+Pl+Nom:%>0  K ;
!:^WG^IJ%>j N_EVEN_J ;
:^IJ%>j N_EVEN_J ;

LEXICON N_EVEN_J
+Pl+Gen:    K ;
+Pl+Acc:d   K ;
+Pl+Ine:n   K ;
+Pl+Ela+Err/Orth:s  K ;
+Pl+Ela:st  K ; !norm
+Pl+Ill:da  K ; 
+Pl+Com:    K ;
+Sg+Com:n K ;         !norm
+Sg+Com+Use/NG:na K ; !variant

! LEXICON N_EVEN_B
! +Sg+Nom:^UAUML%>e    K ;
! +Sg+Gen:^WG%>e  K ;
! +Sg+Acc:^WG%>ev  K ;
! +Sg+Ine:^WG%>en K ;
! +Sg+Ela+Err/Orth:^WG%>es K ;
! +Sg+Ela:^WG%>est K ;
! +Sg+Ill:%>áj K ;
! +Abe:^WG%>edak K ; ! Norm
! +Pl+Nom:^WG%>e  K ;
! +Sg+Com:^WG%>ijn   K ; !norm
! +Sg+Com:^WG%>ijna  K ; !variant
! +Pl+Gen:^WG%>ij    K ;
! +Pl+Acc:^WG%>ijd   K ;
! +Pl+Ine:^WG%>ijn   K ;
! +Pl+Ela+Err/Orth:^WG%>ijs  K ;
! +Pl+Ela:^WG%>ijst  K ; !norm
! +Pl+Ill:^WG%>ijda  K ; 
! +Pl+Com:^WG%>ij    K ;
! +Ess:%>en K ;



!LEXICON N_EVEN_4 ! gonagassa, 
!+N: EVENCASE_4 ;
!+N:a N_EVEN_ILL ;
!+N:a^WG N_EVEN_WK ; ! CG here
!
LEXICON EVENCASE_4 !mentioned in Adj-Affixes!
+Sg+Nom:^CDEL K ;
:a N_EVEN_ESS ; !essive



! odd-syllabic
! ------------

LEXICON N_ODD !standard odd-syllable noun, eg. guohtom<->guohtom-
+N+Sg+Nom: K ;
+N: N_ODD_X ;

LEXICON N_ODD_WG !standard odd-syllable noun with CGrad, eg. vanas<->vadnas-
+N+Sg+Nom:^WG K ;
+N: N_ODD_X ;

LEXICON N_ODD_OPEN !open-syllable, eg. biena<->biednaga
+N+Sg+Nom:^WG^CDEL K ;
+N: N_ODD_X ;

LEXICON N_ODD_VH !odd-syllable with VH, eg. ålol->ullul- !!What about sarves->sarrvá-
+N+Sg+Nom:^WG K ;
+N:^V2O2U N_ODD_X ;

LEXICON N_ODD_X !N_ODD suffixes (X as in suffiX)
+Sg+Acc:a%>v K ;
+Sg+Gen:a K ;
+Sg+Ill:^VHBARi%>j K ;
+Sg+Ine:^VHBARi%>n K ;
+Sg+Ela+Err/Orth:^VHBARi%>s K ;
+Sg+Ela:^VHBARi%>st K ;
+Sg+Com:^VHBARi%>jn K ;
+Ess:^VHBARi%>n K ;
+Abe:a%>htá K ;
+Pl+Nom:a K ;
+Pl+Acc:^VHBARi%>jd K ;
+Pl+Gen:^VHBARi%>j K ;
+Pl+Ill:^VHBARi%>jda K ;
+Pl+Ine:^VHBARi%>jn K ;
+Pl+Ela+Err/Orth:^VHBARi%>js K ;
+Pl+Ela:^VHBARi%>jst K ;
+Pl+Com:^VHBARi%>j K ;





! contracted
! ----------

LEXICON N_CONTR_OJU !båtsoj<->buhtsu
+N+Sg+Nom:oj^WG K ;
N_CONTR_O ;

LEXICON N_CONTR_OU !suolo<->sullu
+N+Sg+Nom:o^WG K ;
N_CONTR_O ;

LEXICON N_CONTR_O !båtsoj<->buhtsu, suolo<->sullu (except NOM.SG)
+N+Sg+Acc:u%>v K ;
+N+Sg+Gen:u K ;
+N+Sg+Ill:u%>j K ;
+N+Sg+Ine:u%>n K ;
+N+Sg+Ela+Err/Orth:u%>s K ;
+N+Sg+Ela:u%>st K ;
+N+Sg+Com:u%>jn K ;
+N+Ess:u%>n K ;
+N+Ess:u%>n K ;
+N+Abe:u%>dak K ; ! XXX
+N+Pl+Nom:u K ;
+N+Pl+Acc:u%>jd K ;
+N+Pl+Gen:u%>j K ;
+N+Pl+Ill:u%>jda K ;
+N+Pl+Ine:u%>jn K ;
+N+Pl+Ela+Err/Orth:u%>js K ;
+N+Pl+Ela:u%>jst K ;
+N+Pl+Com:u%>j K ;

LEXICON N_CONTR_AJA !ålmaj<->ålma
+N+Sg+Nom:a^WGj K ;
+N+Sg+Acc:av K ;
+N+Sg+Gen:a K ;
+N+Sg+Ill:aj K ;
+N+Sg+Ine:an K ;
+N+Sg+Ela+Err/Orth:as K ;
+N+Sg+Ela:ast K ;
+N+Sg+Com:ajn K ;
+N+Ess:an K ; !check ^WG - is it required by ESS? is it implemented by twolc?
+N+Ess:a^WGn K ; !check ^WG - is it required by ESS? is it implemented by twolc?
+N+Abe:adak K ; ! XXX
+N+Pl+Nom:a K ;
+N+Pl+Acc:ajd K ;
+N+Pl+Gen:aj K ;
+N+Pl+Ill:ajda K ;
+N+Pl+Ine:ajn K ;
+N+Pl+Ela+Err/Orth:ajs K ;
+N+Pl+Ela:ajst K ;
+N+Pl+Com:aj K ;

LEXICON N_CONTR_ESA !sarves<->sarvvá
+N+Sg+Nom:e^WGs K ;
+N+Sg+Acc:áv K ;
+N+Sg+Gen:á K ;
+N+Sg+Ill:áj K ;
+N+Sg+Ine:án K ;
+N+Sg+Ela+Err/Orth:ás K ;
+N+Sg+Ela:ást K ;
+N+Sg+Com:ájn K ;
+N+Ess:án K ; !check ^WG - is it required by ESS? is it implemented by twolc?
+N+Ess:á^WGn K ; !check ^WG - is it required by ESS? is it implemented by twolc?
+N+Abe:ádak K ; ! XXX
+N+Pl+Nom:á K ;
+N+Pl+Acc:ájd K ;
+N+Pl+Gen:áj K ;
+N+Pl+Ill:ájda K ;
+N+Pl+Ine:ájn K ;
+N+Pl+Ela+Err/Orth:ájs K ;
+N+Pl+Ela:ájst K ;
+N+Pl+Com:áj K ;




LEXICON PX ! possessive suffixes (to be added later, if at all (not productive))
# ;


! NOTE: 
! NOTE: below here: lexica that are referred to in adjectives (or at least outside noun-stems)
! NOTE: 

LEXICON N_EVEN_C ! mentioned in adjectives!
+Sg+Gen:%>o  K ; 
+Sg+Acc:%>ov  K ;
+Sg+Ine:%>on K ;
+Sg+Ela+Err/Orth:%>os K ;
+Sg+Ela:%>ost K ;
+Abe:%>odak K ; ! Norm
+Pl+Nom:%>o  K ;
+Sg+Com:%>ojn   K ; !norm
+Sg+Com+Err/Orth:%>ojna  K ; !variant
+Pl+Gen:%>oj    K ;
+Pl+Acc:%>ojd   K ;
+Pl+Ine:%>ojn   K ;
+Pl+Ela+Err/Orth:%>ojs  K ;
+Pl+Ela:%>ojst  K ; !norm
+Pl+Ill:%>ojda  K ; 
+Pl+Com:%>oj    K ;
+Ess:%>on K ;



!used by adjectives - don't delete:
LEXICON ODD_OBLIQUE_COMMON !used by adjectives - don't delete
+Sg+Acc:a%>v K ;
+Sg+Gen:a K ;
+Sg+Ill:i%>j K ;
+Sg+Ine:i%>n K ;
+Sg+Ela+Err/Orth:i%>s K ; 
+Sg+Ela:i%>st K ; 
+Sg+Com:i%>jn K ;
+Ess:i%>n K ;
+Pl+Nom:a K ;
+Pl+Acc:i%>jd K ;
+Pl+Gen:i%>j K ;
+Pl+Ill:i%>jda K ;
+Pl+Ine:i%>jn K ;
+Pl+Ela+Err/Orth:i%>js K ;
+Pl+Ela:i%>jst K ;
+Pl+Com:i%>j K ;


!used by adjectives - don't delete:
LEXICON ODD_OBLIQUE_U_COMMON !used by adjectives - don't delete:
+Sg+Acc:uv K ;
+Sg+Gen:u K ;
+Sg+Ill:uj K ;
+Sg+Ine:un K ;
+Sg+Ela+Err/Orth:us K ; 
+Sg+Ela:ust K ; 
+Sg+Com:ujn K ;
+Ess:un K ;
+Pl+Nom:u K ;
+Pl+Acc:ujd K ;
+Pl+Gen:uj K ;
+Pl+Ill:ujda K ;
+Pl+Ine:ujn K ;
+Pl+Ela+Err/Orth:ujs K ;
+Pl+Ela:ujst K ;
+Pl+Com:uj K ;

