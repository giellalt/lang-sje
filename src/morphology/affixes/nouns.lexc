! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or feedback@divvun.no




! ========== !
! Inflection !
! ========== !

!juollkie+N+Abe             morf =======%> upper
!juollkie^WG%>htahk   trans
!                                     
!juollkie^WG%>Abehtahk   mphon
!juol0kie00htahk         trans ======%> lower

! Noun 
! ===============


! even-syllabic
! -------------

LEXICON N_EVEN ! giella, guolle
+N: EVENCASE ;  ! Sg Nom, Ess
+N: N_EVEN_ILL ;
+N:^WG N_EVEN_WK ; ! CG here

LEXICON N_EVEN_O ! rássjo-variant rässjuj
+N: EVENCASE ;  ! Sg Nom, Ess
+N: N_EVEN_ILL ; 
+N: N_EVEN_ILL_O ; ! allows rássjo-variant: rässjuj
+N:^WG N_EVEN_WK ; ! CG here

LEXICON N_EVEN_NOCG ! without consonant gradation, e.g. bägga<->biegga
+N:^G3 EVENCASE ;
+N:^G3 N_EVEN_ILL ;
+N:^WG N_EVEN_WK ; ! ^WG necessary for words like vuodda<->vuaddag-

LEXICON EVENCASE
+Sg+Nom:^UAUML    K ;
N_EVEN_ESS ; ! Essive

LEXICON N_EVEN_ILL
+Sg+Ill:%>j^V2E2AA K ; !V2 e->á in ill.sg, dim

LEXICON N_EVEN_ILL_O
+Sg+Ill+Use/NG:%>j^V2O2U K ; !V2 o->u in ill.sg, dim

LEXICON N_EVEN_ESS
+Ess:^UAUML%>n K ;

LEXICON N_EVEN_WK
+Der/Dim:%>tj^V2E2AA N_ODD ;
+Sg+Gen:%>0  K ;
+Sg+Acc:%>v  K ;
+Sg+Ine:%>n K ;
+Sg+Ela+Err/Orth:%>s K ;
+Sg+Ela:%>st K ;
+Abe:%>dak K ; ! Norm
!+Abe:%>htaka K ; ! Informants S and Bj 
!+Abe:%>htak K ; ! Informants S and Bj 
+Pl+Nom:%>0  K ;
:^IJ%>j N_EVEN_J ;

LEXICON N_EVEN_J
+Pl+Gen:    K ;
+Pl+Acc:d   K ;
+Pl+Ine:n   K ;
+Pl+Ela+Err/Orth:s  K ;
+Pl+Ela:st  K ; !norm
+Pl+Ill:da  K ; 
+Pl+Com:    K ;
+Sg+Com:n K ;         !norm
+Sg+Com+Use/NG:na K ; !variant


LEXICON N_EVEN4 !mánnodak<->mánnodag-
+N+Sg+Nom: K ;
+N+Sg+Acc:a%>v K ;
+N+Sg+Gen:a%>0 K ;
+N+Sg+Ill:a%>j K ;
+N+Sg+Ine:a%>n K ;
+N+Sg+Ela:a%>st K ;
+N+Sg+Ela+Err/Orth:a%>s K ;
+N+Sg+Com:a%>jn K ;
+N+Ess:a%>n K ;
!+N+Abe:a%>dak K ; ! XXX
+N+Pl+Nom:a%>0 K ;
+N+Pl+Acc:a%>jd K ;
+N+Pl+Gen:a%>j K ;
+N+Pl+Ill:a%>jda K ;
+N+Pl+Ine:a%>jn K ;
+N+Pl+Ela:a%>jst K ;
+N+Pl+Ela+Err/Orth:a%>js K ;
+N+Pl+Com:a%>j K ;


LEXICON N_EVEN4_ISA !guoksagis<->guoksagas-
+N+Sg+Nom:i2s K ;
+N+Sg+Acc:asa%>v K ;
+N+Sg+Gen:asa%>0 K ;
+N+Sg+Ill:asa%>j K ;
+N+Sg+Ine:asa%>n K ;
+N+Sg+Ela+Err/Orth:asa%>s K ;
+N+Sg+Ela:asa%>st K ;
+N+Sg+Com:asa%>jn K ;
+N+Ess:asa%>n K ; 
!+N+Abe:asa%>dak K ; ! XXX
+N+Pl+Nom:asa%>0 K ;
+N+Pl+Acc:asa%>jd K ;
+N+Pl+Gen:asa%>j K ;
+N+Pl+Ill:asa%>jda K ;
+N+Pl+Ine:asa%>jn K ;
+N+Pl+Ela+Err/Orth:asa%>js K ;
+N+Pl+Ela:asa%>jst K ;
+N+Pl+Com:asa%>j K ;





! odd-syllabic
! ------------

LEXICON N_ODD !standard odd-syllable noun, eg. guohtom<->guohtom-
+N+Sg+Nom:%>0 K ; 
+N: N_ODD_X ;
+N: N_ODD_DIM ; !link to DIM

LEXICON N_ODD_WG !standard odd-syllable noun with CGrad, eg. vanas<->vadnas-
+N+Sg+Nom:^WG K ;
+N: N_ODD_X ;
+N: N_ODD_DIM ; !link to DIM

LEXICON N_ODD_OPEN !open-syllable, eg. biena<->biednaga
+N+Sg+Nom:^WG^CDEL K ;
+N: N_ODD_X ;
+N: N_ODD_DIM ; !link to DIM

LEXICON N_ODD_VH !odd-syllable with VH, eg. ålol->ullul- !!What about sarves->sarrvá-
+N+Sg+Nom:^WG K ;
+N:^V2O2U N_ODD_X ;
+N:^V2O2U N_ODD_DIM ; !link to DIM

LEXICON N_ODD_X !N_ODD suffixes (X as in suffiX)
+Sg+Acc:a%>v K ;
+Sg+Gen:a%>0 K ;
+Sg+Ill:i2%>j K ;
+Sg+Ine:i2%>n K ;
+Sg+Ela+Err/Orth:i2%>s K ;
+Sg+Ela:i2%>st K ;
+Sg+Com:i2%>jn K ;
+Ess:i2%>n K ;
+Abe:a%>htá K ;
+Pl+Nom:a%>0 K ;
+Pl+Acc:i2%>jd K ;
+Pl+Gen:i2%>j K ;
+Pl+Ill:i2%>jda K ;
+Pl+Ine:i2%>jn K ;
+Pl+Ela+Err/Orth:i2%>js K ;
+Pl+Ela:i2%>jst K ;
+Pl+Com:i2%>j K ;

LEXICON N_ODD_DIM !N_ODD suffixes (X as in suffiX)
+Der/Dim:a%>tj N_ODD ;


! contracted
! ----------

LEXICON N_CONTR_OJU !båtsoj<->buhtsu
+N+Sg+Nom:oj^WG K ;
N_CONTR_O ;

LEXICON N_CONTR_OU !suolo<->sullu
+N+Sg+Nom:o^WG K ;
N_CONTR_O ;

LEXICON N_CONTR_O !båtsoj<->buhtsu, suolo<->sullu (except NOM.SG)
+N+Sg+Acc:u%>v K ;
+N+Sg+Gen:u%>0 K ;
+N+Sg+Ill:u%>j K ;
+N+Sg+Ine:u%>n K ;
+N+Sg+Ela+Err/Orth:u%>s K ;
+N+Sg+Ela:u%>st K ;
+N+Sg+Com:u%>jn K ;
+N+Ess:u%>n K ;
+N+Ess:u%>n K ;
+N+Abe:u%>dak K ; ! XXX
+N+Pl+Nom:u%>0 K ;
+N+Pl+Acc:u%>jd K ;
+N+Pl+Gen:u%>j K ;
+N+Pl+Ill:u%>jda K ;
+N+Pl+Ine:u%>jn K ;
+N+Pl+Ela+Err/Orth:u%>js K ;
+N+Pl+Ela:u%>jst K ;
+N+Pl+Com:u%>j K ;

LEXICON N_CONTR_AJA !ålmaj<->ålma
+N+Sg+Nom:a^WGj K ;
+N+Sg+Acc:a%>v K ;
+N+Sg+Gen:a%>0 K ;
+N+Sg+Ill:a%>j K ;
+N+Sg+Ine:a%>n K ;
+N+Sg+Ela+Err/Orth:a%>s K ;
+N+Sg+Ela:a%>st K ;
+N+Sg+Com:a%>jn K ;
+N+Ess:a%>n K ; !check ^WG - is it required by ESS? is it implemented by twolc?
!+N+Ess:a^WGn K ; !check ^WG - is it required by ESS? is it implemented by twolc?
+N+Abe:a%>dak K ; ! XXX
+N+Pl+Nom:a%>0 K ;
+N+Pl+Acc:a%>jd K ;
+N+Pl+Gen:a%>j K ;
+N+Pl+Ill:a%>jda K ;
+N+Pl+Ine:a%>jn K ;
+N+Pl+Ela+Err/Orth:a%>js K ;
+N+Pl+Ela:a%>jst K ;
+N+Pl+Com:a%>j K ;

LEXICON N_CONTR_ESA !sarves<->sarvvá
+N+Sg+Nom:e^WGs K ;
+N+Sg+Acc:á%>v K ;
+N+Sg+Gen:á%>0 K ;
+N+Sg+Ill:á%>j K ;
+N+Sg+Ine:á%>n K ;
+N+Sg+Ela+Err/Orth:á%>s K ;
+N+Sg+Ela:á%>st K ;
+N+Sg+Com:á%>jn K ;
+N+Ess:á%>n K ; !check ^WG - is it required by ESS? is it implemented by twolc?
!+N+Ess:á^WGn K ; !check ^WG - is it required by ESS? is it implemented by twolc?
+N+Abe:á%>dak K ; ! XXX
+N+Pl+Nom:á%>0 K ;
+N+Pl+Acc:á%>jd K ;
+N+Pl+Gen:á%>j K ;
+N+Pl+Ill:á%>jda K ;
+N+Pl+Ine:á%>jn K ;
+N+Pl+Ela+Err/Orth:á%>js K ;
+N+Pl+Ela:á%>jst K ;
+N+Pl+Com:á%>j K ;




LEXICON PX ! possessive suffixes (to be added later, if at all (not productive))
# ;




! NOTE: 
! NOTE: below here: lexica that are referred to in adjectives (or at least outside noun-stems)
! NOTE: 

!used by adjectives - don't delete:
LEXICON EVENCASE_4 !used by adjectives - don't delete
+Sg+Nom:^CDEL K ;
:a N_EVEN_ESS ; !essive

!used by adjectives - don't delete:
LEXICON N_EVEN_C !used by adjectives - don't delete
+Sg+Gen:%>o  K ; 
+Sg+Acc:%>ov  K ;
+Sg+Ine:%>on K ;
+Sg+Ela+Err/Orth:%>os K ;
+Sg+Ela:%>ost K ;
+Abe:%>odak K ; ! Norm
+Pl+Nom:%>o  K ;
+Sg+Com:%>ojn   K ; !norm
+Sg+Com+Err/Orth:%>ojna  K ; !variant
+Pl+Gen:%>oj    K ;
+Pl+Acc:%>ojd   K ;
+Pl+Ine:%>ojn   K ;
+Pl+Ela+Err/Orth:%>ojs  K ;
+Pl+Ela:%>ojst  K ; !norm
+Pl+Ill:%>ojda  K ; 
+Pl+Com:%>oj    K ;
+Ess:%>on K ;

!used by adjectives - don't delete:
LEXICON ODD_OBLIQUE_COMMON !used by adjectives - don't delete
+Sg+Acc:a%>v K ;
+Sg+Gen:a K ;
+Sg+Ill:i%>j K ;
+Sg+Ine:i%>n K ;
+Sg+Ela+Err/Orth:i%>s K ; 
+Sg+Ela:i%>st K ; 
+Sg+Com:i%>jn K ;
+Ess:i%>n K ;
+Pl+Nom:a K ;
+Pl+Acc:i%>jd K ;
+Pl+Gen:i%>j K ;
+Pl+Ill:i%>jda K ;
+Pl+Ine:i%>jn K ;
+Pl+Ela+Err/Orth:i%>js K ;
+Pl+Ela:i%>jst K ;
+Pl+Com:i%>j K ;

!used by adjectives - don't delete:
LEXICON ODD_OBLIQUE_U_COMMON !used by adjectives - don't delete:
+Sg+Acc:uv K ;
+Sg+Gen:u K ;
+Sg+Ill:uj K ;
+Sg+Ine:un K ;
+Sg+Ela+Err/Orth:us K ; 
+Sg+Ela:ust K ; 
+Sg+Com:ujn K ;
+Ess:un K ;
+Pl+Nom:u K ;
+Pl+Acc:ujd K ;
+Pl+Gen:uj K ;
+Pl+Ill:ujda K ;
+Pl+Ine:ujn K ;
+Pl+Ela+Err/Orth:ujs K ;
+Pl+Ela:ujst K ;
+Pl+Com:uj K ;

