! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or divvun@samediggi.no




! ========== !
! Inflection !
! ========== !

!juollkie+N+Abe             morf =======> upper
!juollkie^WG>htahk   trans
!                                     
!juollkie^WG>Abehtahk   mphon
!juol0kie00htahk         trans ======> lower

! Noun 
! ===============


! even-syllabic
! -------------

LEXICON N_EVEN_NOCG
+N: EVENCASE ;
+N: N_EVEN_W ; ! No CG here, ingen stadieväxling

LEXICON N_EVEN
+N: EVENCASE ;
+N:%^WG N_EVEN_W ; ! CG here

LEXICON EVENCASE
+Sg+Nom:    K ;
EVENOBLIQUE ;

LEXICON EVENOBLIQUE 
               N_EVEN_S ;
:%^jDEL%>j     N_EVEN_J ;
N_EVEN_ILL ;

LEXICON N_EVEN_ILL
+Sg+Ill:%>j%^MONB4J K ;

LEXICON N_EVEN_S
+Ess:%>n K ;

LEXICON N_EVEN_W
+Sg+Gen:  K ;
+Sg+Acc:%>v  K ;
+Sg+Ine:%>n K ;
+Sg+Ela+Use/Sub:%>s K ;
+Sg+Ela:%>st K ;
+Abe:%>dak K ; ! Norm
!+Abe:%>htaka K ; ! Informants S and Bj 
!+Abe:%>htak K ; ! Informants S and Bj 
+Pl+Nom:%>0  K ;

LEXICON N_EVEN_J
+Sg+Com:%^MONB4Jn   K ; !norm
+Sg+Com:%^MONB4Jna  K ; !variant
+Pl+Gen:    K ;
+Pl+Acc:t   K ;
+Pl+Ine:n   K ;
+Pl+Ela+Use/Sub:s  K ;
+Pl+Ela:st  K ; !norm
+Pl+Ill:da  K ; 
+Pl+Com:    K ;


! odd-syllabic
! ------------

LEXICON N_ODD_VV
+N+Sg+Nom:%^MONB4J PX ;
+N: ODD_OBLIQUE ;

LEXICON N_ODD_CG
+N+Sg+Nom:%^WG PX ;
+N: ODD_OBLIQUE ;

LEXICON N_ODD_CG_VV
+N+Sg+Nom:%^WG%^MONB4J PX ;
+N: ODD_OBLIQUE ;

LEXICON N_ODD
+N+Sg+Nom: PX ;
+N: ODD_OBLIQUE ;


LEXICON ODD_OBLIQUE
+Abe:ahtá PX ;
ODD_OBLIQUE_COMMON ;

LEXICON ODD_OBLIQUE_COMMON
+Sg+Acc:av PX ;
+Sg+Gen:a PX ;
+Sg+Ill:ij PX ;
+Sg+Ine:in PX ;
+Sg+Ela+Use/Sub:is PX ; 
+Sg+Ela:ist PX ; 
+Sg+Com:ijn PX ;
+Ess:in PX ;
+Pl+Nom:a PX ;
+Pl+Acc:ijt PX ;
+Pl+Gen:ij PX ;
+Pl+Ill:ijda PX ;
+Pl+Ine:ijn PX ;
+Pl+Ela+Use/Sub:ijs PX ;
+Pl+Ela:ijst PX ;
+Pl+Com:ij PX ;


LEXICON ODD_OBLIQUE_U
+Abe:uhtá PX ;
ODD_OBLIQUE_U_COMMON ;

LEXICON ODD_OBLIQUE_U_COMMON
+Sg+Acc:uv PX ;
+Sg+Gen:u PX ;
+Sg+Ill:uj PX ;
+Sg+Ine:un PX ;
+Sg+Ela+Use/Sub:us PX ; 
+Sg+Ela:ust PX ; 
+Sg+Com:ujn PX ;
+Ess:un PX ;
+Pl+Nom:u PX ;
+Pl+Acc:ujt PX ;
+Pl+Gen:uj PX ;
+Pl+Ill:ujda PX ;
+Pl+Ine:ujn PX ;
+Pl+Ela+Use/Sub:ujs PX ;
+Pl+Ela:ujst PX ;
+Pl+Com:uj PX ;


! contracted
! ----------

! initial contlex

LEXICON SARVES
 +N:%^WG SARVES-NE ;
 +N:%^MON CONTR-OTH ;

LEXICON SUOLOJ
 +N:%^WG SUOLOJ-NE ;
 +N:%^MON CONTR-OTH ;


! ne contlex

LEXICON SARVES-NE
 +Sg+Nom:%>s K ;
 +Ess:%>n K ;

LEXICON SUOLOJ-NE
 +Sg+Nom:%>j K ;
 +Ess:%>n K ;


! other contlex

LEXICON CONTR-OTH
 N_EVEN_ILL ;
 N_EVEN_W ;
 

! under construction

LEXICON N_CONTR_C
ÅLMAAJ ; ! skall ordnas till

LEXICON ÅLMAAJ
+N+Sg+Nom:%^WGáj PX ;
+N+Sg+Acc:áv PX ;
+N+Sg+Gen:á PX ;
+N+Sg+Ill:áj PX ;
+N+Sg+Ine:án PX ;
+N+Sg+Ela+Use/Sub:ás PX ;
+N+Sg+Ela:ást PX ;
+N+Sg+Com:ájn PX ;
+N+Ess:án PX ;
+N+Ess:%^WGán PX ;
+N+Abe:ádak PX ; ! XXX
+N+Pl+Nom:á PX ;
+N+Pl+Acc:ájt PX ;
+N+Pl+Gen:áj PX ;
+N+Pl+Ill:ájda PX ;
+N+Pl+Ine:ájn PX ;
+N+Pl+Ela+Use/Sub:ájs PX ;
+N+Pl+Ela:ájst PX ;
+N+Pl+Com:áj PX ;

LEXICON N_ODD_NOCG
+N+Sg+Nom: PX ;
+N: ODD_OBLIQUE ;

LEXICON BIENA
+N:%^WG%^CDEL BIENASHORT ;
+N: BIENALONG ;

LEXICON BIENALONG
+Sg+Acc:av PX ;
+Sg+Gen:a PX ;
+Sg+Ill:ij PX ;
+Sg+Ine:in PX ;
+Sg+Ela+Use/Sub:is PX ;
+Sg+Ela:ist PX ;
+Sg+Com:ijn PX ;
+Ess:in PX ;
+Abe:ahtá PX ;
+Pl+Nom:a PX ;
+Pl+Acc:ijt PX ;
+Pl+Gen:ij PX ;
+Pl+Ill:ijda PX ;
+Pl+Ine:ijn PX ;
+Pl+Ela+Use/Sub:ijs PX ;
+Pl+Ela:ijst PX ;
+Pl+Com:ij PX ;


LEXICON BIENASHORT
+Sg+Nom: PX ;



LEXICON N_CONTR
+N: CONTRNOMSG ;
+N: CONTROBL ;

LEXICON CONTRNOMSG
+Sg+Nom:%^MONB4Joj K ;
 
LEXICON CONTROBL 
+Sg+Acc:uv PX ;
+Sg+Gen:u PX ;
+Sg+Ill:uj PX ;
+Sg+Ine:un PX ;
+Sg+Ela+Use/Sub:us PX ;
+Sg+Ela:ust PX ;
+Sg+Com:ujn PX ;
+Abe:udak PX ;
+Pl+Nom:u PX ;
+Pl+Acc:ujt PX ;
+Pl+Gen:uj PX ;
+Pl+Ill:ujda PX ;
+Pl+Ine:ujn PX ;
+Pl+Ela+Use/Sub:ujs PX ;
+Pl+Ela:ujst PX ;
+Pl+Com:uj PX ;



LEXICON PX
# ;


