! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or feedback@divvun.no






! ===== !
! Verbs !
! ===== !


!! !!!Even syllabled verbs

LEXICON V_EVEN_E !båhtet
+V:     V_EVEN_E_STR ;
+V:e^WG V_EVEN_E_WK ;

LEXICON V_EVEN_E_STR
+Ind+Prs+Sg3:á%> #;
!+Ind+Prs+Sg3+Err/Orth:a%> #; !just as an example, for possible later implementation
+Ind+Prs+Du1:i%>n #; 
+Ind+Prs+Du2:e%>behtin #;
+Ind+Prs+Du2:e%>bähtten #;
+Ind+Prs+Du3:e%> 3DUPRS ;
+Ind+Prs+Pl1:e%>p #;
+Ind+Prs+Pl2:e%>behtit #;
+Ind+Prs+Pl2:e%>bähttet #;
+Ind+Prs+Pl3:i%> #; 
+Ind+Prt+Sg1:i%>v #; 
+Ind+Prt+Sg2:i%> #; 
+Ind+Prt+Pl3:i%>n #; 

+Imprt+Du2:e%>n # ; 
+Imprt+Pl2:i%>t #;

+Inf:e%>t # ; 
+GerII:e%>min # ; !aka: +Actio+Ess ?
+PrfPrc:á%>m # ;
+PrsPrc:e%>j # ; 
+Vsubst:e%>m #; 

LEXICON V_EVEN_E_WK
+Ind+Prs+Sg1:%>v^V2E2AA #;
+Ind+Prs+Sg2:%>^V2E2AA #;
:^IJ%>j V_EVEN_E_WK_J ; !suffixes with -j-element that trigger e->i in V2

+Imprt+Sg2:%> #; 

+GerI:%>din # ;  
+ConNeg:%> # ; 
+Vgen:%> #;  
!+Sup:%>tjit # ;
+Pot:%>tj V_EVEN_POT ; 

LEXICON V_EVEN_E_WK_J
+Ind+Prt+Sg3: #;
+Ind+Prt+Du1:men #;
+Ind+Prt+Du2:den #;
+Ind+Prt+Du3: 3DUPRT ;
+Ind+Prt+Pl1:me #;
+Ind+Prt+Pl2:de #;



LEXICON V_EVEN_A !dahkat
+V:     V_EVEN_A_STR ;
+V:a^WG V_EVEN_A_WK ;

LEXICON V_EVEN_A_STR
+Ind+Prs+Sg3:á%> #;
+Ind+Prs+Du1:i%>n #; 
+Ind+Prs+Du2:a%>behtin #;
!+Ind+Prs+Du2:a%>bähtten #;
+Ind+Prs+Du3:a%> 3DUPRS ;
+Ind+Prs+Pl1:a%>p #;
+Ind+Prs+Pl2:a%>behtit #;
!+Ind+Prs+Pl2:a%>bähttet #;
+Ind+Prs+Pl3:i%> #; 
+Ind+Prt+Sg1:i%>v #; 
+Ind+Prt+Sg2:i%> #; 
+Ind+Prt+Pl3:i%>n #; 

+Imprt+Du2:e%>n # ; 
+Imprt+Pl2:i%>t #;

+Inf:a%>t # ; 
+GerII:a%>min # ;
+PrfPrc:a%>m # ;
+PrsPrc:e%>j # ; 
!+Vsubst:a%>m #; 

LEXICON V_EVEN_A_WK
+Ind+Prs+Sg1:%>v #;
+Ind+Prs+Sg2:%> #;
+Ind+Prt+Sg3:%>j #;
+Ind+Prt+Du1:%>jmen #;
+Ind+Prt+Du2:%>jden #;
+Ind+Prt+Du3:%>j 3DUPRT ;
+Ind+Prt+Pl1:%>jme #;
+Ind+Prt+Pl2:%>jde #;

+Imprt+Sg2:%> #; 

+GerI:%>din # ;  
!+Sup:%>tjit # ;
+ConNeg:%> # ; 
+Vgen:%> #;  
+Pot:%>tj V_EVEN_POT ;


LEXICON V_GILGIJ !special case for deviant gilgij-forms of gallgat
+V:	V_GILGIJ_FORMS ;

LEXICON V_GILGIJ_FORMS !see above
+Ind+Prt+Sg3:%>j #;
+Ind+Prt+Du1:%>jmen #;
+Ind+Prt+Du2:%>jden #;
+Ind+Prt+Du3:%>j 3DUPRT ;
+Ind+Prt+Pl1:%>jme #;
+Ind+Prt+Pl2:%>jde #;


LEXICON V_EVEN_O !viessot, årrot
+V:     V_EVEN_O_STR ;
+V:o^WG V_EVEN_O_WK ;

LEXICON V_EVEN_O_STR
+Ind+Prs+Sg3:u%> #; !should this be -u-?
+Ind+Prs+Du1:u%>n #; 
+Ind+Prs+Du2:o%>behtin #;
+Ind+Prs+Du2:o%>bähtten #;
+Ind+Prs+Du3:o%> 3DUPRS ;
+Ind+Prs+Pl1:o%>p #;
+Ind+Prs+Pl2:o%>behtit #;
+Ind+Prs+Pl2:o%>bähttet #;
+Ind+Prs+Pl3:u%> #; 
+Ind+Prt+Sg1:u%>v #; 
+Ind+Prt+Sg2:u%> #; 
+Ind+Prt+Pl3:u%>n #; 

+Imprt+Du2:o%>n # ; 
+Imprt+Pl2:u%>t #;

+Inf:o%>t # ; 
+GerII:o%>min # ;
+PrfPrc:u%>m # ;
+PrsPrc:o%>j # ; 
+Vsubst:o%>m #; 

LEXICON V_EVEN_O_WK
+Ind+Prs+Sg1:%>v #;
+Ind+Prs+Sg2:%> #;
+Ind+Prt+Sg3:%>j #;
+Ind+Prt+Du1:%>jmen #;
+Ind+Prt+Du2:%>jden #;
+Ind+Prt+Du3:%>j 3DUPRT ;
+Ind+Prt+Pl1:%>jme #;
+Ind+Prt+Pl2:%>jde #;

+Imprt+Sg2:%> #; 

+GerI:%>din # ;  
!+Sup:%>tjit # ;
+ConNeg:%> # ; 
+Vgen:%> #;  
+Pot:%>tj V_EVEN_POT ; 


LEXICON V_EVEN_Å !hållåt, bårråt
+V:     V_EVEN_Å_STR ;
+V:å^WG V_EVEN_Å_WK ;

LEXICON V_EVEN_Å_STR
+Ind+Prs+Sg3:á%> #;
+Ind+Prs+Du1:i%>n #; 
+Ind+Prs+Du2:å%>behtin #;
!+Ind+Prs+Du2:å%>bähtten #;
+Ind+Prs+Du3:å%> 3DUPRS ;
+Ind+Prs+Pl1:å%>p #;
+Ind+Prs+Pl2:å%>behtit #;
!+Ind+Prs+Pl2:å%>bähttet #;
+Ind+Prs+Pl3:i%> #; 
+Ind+Prt+Sg1:i%>v #; 
+Ind+Prt+Sg2:i%> #; 
+Ind+Prt+Pl3:i%>n #; 

+Imprt+Du2:e%>n # ; 
+Imprt+Pl2:i%>t #;

+Inf:å%>t # ; 
+GerII:å%>min # ;
+PrfPrc:å%>m # ;
+PrsPrc:e%>j # ; !-åt cognate with -at verbs in saL/saN, which also get -ej
!+Vsubst:å%>m #; 

LEXICON V_EVEN_Å_WK
+Ind+Prs+Sg1:%>v #;
+Ind+Prs+Sg2:%> #;
+Ind+Prt+Sg3:%>j #;
+Ind+Prt+Du1:%>jmen #;
+Ind+Prt+Du2:%>jden #;
+Ind+Prt+Du3:%>j 3DUPRT ;
+Ind+Prt+Pl1:%>jme #;
+Ind+Prt+Pl2:%>jde #;

+Imprt+Sg2:%> #; 

+GerI:%>din # ;  
!+Sup:%>tjit # ;
+ConNeg:%> # ; 
+Vgen:%> #;  
+Pot:%>tj V_EVEN_POT ; 



!POT forms for V_EVEN_
LEXICON V_EVEN_POT !
+Sg1:a%>v #;
+Sg2:a%> #;
+Sg3:a%> #;
+Sg3+Use/NG:%> #;
+Du1:i%>n #; 
+Du2:ä%>hppen #;
+Du3:e%>ba #;
+Pl1:i%>p #;
+Pl2:i%>hppit #;
+Pl3:i%> #; 



!! !!!Odd syllabled verbs

LEXICON V_ODD !ságastit
+V+Ind+Prs+Sg1:a%>v #;
+V+Ind+Prs+Sg2:a%> #;
+V+Ind+Prs+Sg3:a%> #;
+V+Ind+Prs+Sg3+Use/NG:%> #;
+V+Ind+Prs+Du1:i%>n #; 
+V+Ind+Prs+Du2:ä%>hppen #;
+V+Ind+Prs+Du3:e%>ba #;
+V+Ind+Prs+Pl1:i%>p #;
+V+Ind+Prs+Pl2:i%>hppit #;
+V+Ind+Prs+Pl3:i%> #; 

+V+Ind+Prt+Sg1:i%>v #; 
+V+Ind+Prt+Sg2:i%> #; 
+V+Ind+Prt+Sg3:i%>j #;
+V+Ind+Prt+Du1:i%>jmen #;
+V+Ind+Prt+Du2:i%>jden #;
+V+Ind+Prt+Du3:i%>jga #;
+V+Ind+Prt+Pl1:i%>jme #;
+V+Ind+Prt+Pl2:i%>jde #;
+V+Ind+Prt+Pl3:i%>n #; 

+V+Imprt+Sg2:e%> #; 
+V+Imprt+Du2:e%>hten # ; 
+V+Imprt+Pl2:i%>httit #;

+V+Inf:i%>t # ; 
+V+GerII:e%>min # ;
+V+PrfPrc:a%>m # ;
+V+ConNeg:e%> # ; 
+V+PrsPrc:ä%>ddje # ; 
!+V+Vsubst:a%>m #; 
!+V+GerI:a%>din # ;  
!+V+Sup:a%>tjit # ;
!+V+Vgen:e%> #;  

!V_ODD POT forms -- check!
+V+Pot:a%>tj V_ODD_POT ;

!POT forms for V_ODD
LEXICON V_ODD_POT !
!+Sg1:a%>v #;
!+Sg2:a%> #;
+Sg3:a%> #;
+Sg3+Use/NG:%> #;
!+Du1:i%>n #; 
!+Du2:ä%>hppen #;
!+Du3:e%>ba #;
!+Pl1:i%>p #;
!+Pl2:i%>hppit #;
!+Pl3:i%> #; 





!! !!!contracted verbs

LEXICON V_CONTR !välldut, tjerrut
+V+Ind+Prs+Sg1:%>v #;
+V+Ind+Prs+Sg2:%> #;
+V+Ind+Prs+Sg3:%>ja #;
+V+Ind+Prs+Du1:%>jin #; 
+V+Ind+Prs+Du2:%>bähttin #;
+V+Ind+Prs+Du3:%>ba #;
+V+Ind+Prs+Pl1:%>p #;
+V+Ind+Prs+Pl2:%>bihttit #;
+V+Ind+Prs+Pl3:%>ji #; 

+V+Ind+Prt+Sg1:%>jiv #; 
+V+Ind+Prt+Sg2:%>ji #; 
+V+Ind+Prt+Sg3:%>j #;
+V+Ind+Prt+Du1:%>jmin #;
+V+Ind+Prt+Du2:%>jdin #;
+V+Ind+Prt+Du3:%>j 3DUPRT ;
+V+Ind+Prt+Pl1:%>jme #;
+V+Ind+Prt+Pl2:%>jde #;
+V+Ind+Prt+Pl3:%>jin #; 

+V+Imprt+Sg2:%> #; 
!+V+Imprt+Du2:%>hten # ; 
!+V+Imprt+Pl2:%>httit #;

+V+Inf:%>t # ; 
+V+GerII:%>min # ;
+V+PrfPrc:%>m # ;
+V+ConNeg:%> # ; 
+V+PrsPrc:%>jäddje # ; 
!+V+Vsubst:%>m #; 
!+V+GerI:%>din # ;  
!+V+Sup:%>tjit # ;
!+V+Vgen:%> #;  

!V_CONTR POT forms -- check!
+V+Pot:%>tj V_CONTR_POT ;

!POT forms for V_CONTR
LEXICON V_CONTR_POT !
+Sg1:a%>v #;
+Sg2:a%> #;
+Sg3:a%> #;
+Sg3+Use/NG:%> #;
+Du1:i%>n #; 
+Du2:ä%>hppen #;
+Du3:e%>ba #;
+Pl1:i%>p #;
+Pl2:i%>hppit #;
+Pl3:i%> #; 






!! !!!Auxiliaries

LEXICON LE !!= * @CODE@ 
lä+V+Ind+Prs: LEPRS ; 
lä+V+Ind+Prt: LEPRT ; 
lä+V+Ind+Pot: LEPOT ;
lä+V+PrfPrc:lam # ; ! lä (conneg) + *am (prfprc allomorph of *lä*)
lä+V+Inf:årrot # ; !infinitive
lä+V+ConNeg:lä # ; !connegative

LEXICON LEPRS !!= * @CODE@ 
+Sg1:läv # ; 
+Sg1+Use/NG:lev # ; 
+Sg2:lä # ;  
+Sg2+Use/NG:le # ;  
+Sg2:l # ; !enclitic (only attaches to vowel-final preceding word)
+Sg3:lä # ; 
+Sg3:l # ; !enclitic (only attaches to vowel-final preceding word)
!+Sg3:le # ; 
+Du1:lin # ; 
!+Du1:len # ; 
+Du2:lihpen # ; 
+Du2:lähppen # ;  
+Du3:läbá # ; 
+Pl1:läp # ; 
+Pl1:lip # ; 
!+Pl1:lep # ; 
+Pl2:lihpit # ; 
+Pl2:lehpit # ; 
+Pl3:lä # ;
+Pl3:l # ; !enclitic (only attaches to vowel-final preceding word)

LEXICON LEPRT !!= * @CODE@ 
+Sg1:lijjiv    # ; 
+Sg1:lidjiv    # ;  
+Sg2:lijji    # ; 
+Sg2:lidji    # ;  
+Sg3:lij    # ; 
+Du1:lijmen # ; 
+Du1:liejme # ; 
+Du2:lijden # ;
+Du2:liejde # ; 
+Du3:lij 3DUPRT ;
+Du3:liej 3DUPRT ; 
+Pl1:lijme # ; 
+Pl1:liejme # ; 
+Pl2:lijde # ;
+Pl2:liejde # ; 
+Pl3:lidjin    # ;  
+Pl3:lijjin    # ; 
+Pl3:lin    # ; 

LEXICON LEPOT
+Sg1:lidtjav # ; 
+Sg2:lidtja # ; 
+Sg3:lidtj # ; 
+Du1:lidtjin # ; 
+Du2:lidtjabehtin # ; 
+Du3:lidtjaba # ; 
+Du3:lidtjaban # ; 
+Pl1:lidtjap # ;  
+Pl2:lidtjabihtit # ; 
+Pl3:lidtji # ; 


!LEXICON ATJA !!= * @CODE@ !!! originally in ACSj:  ATJA "skola" ;  !!= * @CODE@  ???
!atja+V+Ind+Prs: ATJAPRS ; 
!atja+V+Ind+Prt: ATJAPRT ; 
!
!
!LEXICON ATJAPRS !!= * @CODE@ 
!+Sg1:atjav      # ;  !!= * @CODE@ 
!+Sg2:atjah      # ; 
!+Sg3:atja       # ; 
!+Du1:itjin      # ; 
!+Du2:atjabihten # ; 
!+Du3:atjaba     # ; 
!+Pl1:atjap      # ; 
!+Pl2:atjabihtit # ; 
!+Pl3:itjih      # ; 
!
!LEXICON ATJAPRT !!= * @CODE@ 
!+Sg1:itjiv    # ;  !!= * @CODE@ 
!+Sg2:itjih    # ; 
!+Sg3:atjaj    # ; 
!+Du1:atjajmen # ; 
!+Du2:atjajden # ; 
!+Du3:atjajgan # ; 
!+Pl1:atjajmeh # ; 
!+Pl2:atjajdeh # ; 
!+Pl3:itjin    # ; 

LEXICON IJ !!= * @CODE@ 
!ij+V+Ind+Prs: IJPRS ;
!ij+V+Ind+Prt: IJPRT ;
!ij+V+Imprt: IJIMP ;
ij+V+Neg+Prs: IJPRS ;
ij+V+Neg+Prt: IJPRT ;
ij+V+Neg+Imprt: IJIMP ;

!cf. lä+V+PrfPrc:lam # ; ! lä (conneg) + *am (prfprc allomorph of *lä*)
!check tags!
ij+V+Neg+Prs+Pl3+ConNeg+PrfPrc:iellam # ; ! = e+lam

LEXICON IJPRS !!= * @CODE@ 
+Sg1:iv    # ;  !!= * @CODE@ 
+Sg2:i    # ; 
+Sg3:ij    # ; 
+Du1:en # ; 
+Du2:ehpen # ;   
+Du3:iebá # ; 
+Pl1:iehpe # ; 
+Pl1:ep # ; 
+Pl2:ehpit # ;  
+Pl3:ie   # ;
+Pl3:e   # ;

LEXICON IJPRT !!= * @CODE@ 
+Sg1:idtjiv    # ;  !!= * @CODE@ 
+Sg2:idtji    # ; 
+Sg3:idtjij    # ; 
+Du1:iejme # ; 
+Du1:idtjijmen # ; 
+Du2:iejde  # ; 
+Du2:iejden  # ; 
+Du2:idtjijden # ; 
+Du3:iej 3DUPRT ; 
+Du3:idtjij 3DUPRT ; 
+Pl1:iejme # ; 
+Pl1:idtjijme # ; 
+Pl2:iejde # ; 
+Pl2:idtjijde # ; 
+Pl3:idtjin    # ;


LEXICON IJIMP !!= * @CODE@ 
+Sg2:iele  # ; !!= * @CODE@ 
+Du2:iellen # ; 
+Du3:ellit  # ;


LEXICON 3DUPRS !variation in 3rd-DUAL-Presence suffixes for EVEN stems
:ba # ;
+Use/NG:ban # ;

LEXICON 3DUPRT !variation in 3rd-DUAL-Preterite suffixes for EVEN and CONTRACTED stems
:ga # ;
+Use/NG:gan # ;


