! Pite Sámi TWOLC file
!
! $Id$
!
!

Alphabet
  a b c d e f g h i ï j k l m n o p q r s t u v w x y z æ ä ø ö å
  á é í 
  Å Ä Ö Ø Æ Á
  Á É Ó Ú Í À È Ò Ù Ì Ë Ü Ï Â Ê Ô Û Î Ã Ý þ Ñ Ð
  á é ó ú í à è ò ù ì ë ü ï â ê ô û î ã ý þ ñ ð ß ª ß 
  %>:0
  
  %^WG:0 %^jDEL:0 %^MONB4J:0 
;

Sets
  Letter = a b c d e f g h i ï j k l m n o p q r s t u v w x y z æ ä ø ö å á é í ó ú ;
  Cns    =     b c d   f g h     j k l m n   p q r s t   v w x   z           ;
  Vow    = a      e       i ï           o           u       y   æ ä ø ö å  á é í ó ú ;
  WeG    =  %^WG %^jDEL ;
  
!Definitions

!  Dummy     =   

Rules
 
 
! Consonant gradation rules


"Consonant Gradation for xx:x" 
Cx:0 <=> Vow Cx _  Vow:+ WeG:0 ;
     where Cx in ( m n s r f v ) ;
         
"Consonant Gradation for xyy:xy" 
Cy:0 <=> Cx _ Cy Vow:+ WeG:0 ;
     where Cx in ( j  )
           Cy in ( d  )  
          matched ;
         

"Consonant Gradation for xxy:xy" 
Cx:0 <=> Vow: Cx _ Cy Vow:+ WeG:0 ;
     where Cx in (j j j b d g k l l l l r r r s d )
           Cy in (p t k j j j s p t k g p t k j n )  
          matched ;
         
"Consonant Gradation for hxxy:hxy" 
Cx:0 <=> h Cx _ Cy Vow:+ WeG:0 ;
     where Cx in ( d t )
           Cy in ( j j )  
          matched ;
         

!"Consonant Gradation for hx:x"
"Delete h"
h:0 <=> Vow+ _ Cx: WeG:0 ;
    where Cx in (p t k) ;
    
"change unvoiced to voiced"
Cx:Cy <=> h: _ WeG:0 ;
    where Cx in (p t k)
          Cy in (b d g)
          matched ;

"Consonant Gradation for xN:N" 
Cx:0 <=> Vow _ Cy Vow:+ WeG:0 ;
    where Cx in (p t k)
          Cy in (m n n)
          matched ;
         

!€ juollkie^WG
!€ juol0kie0

!€ juollkie^jDELj
!€ juol0ki00j


"Vowel Deletion in front of j"
Vx:0 <=> Vy _ %^jDEL:0 ;
     where Vx in (e o)
           Vy in (i u)
           matched ;


"aa in front of j 1"
i:a <=> _ e: %^MONB4J:0 ;


"aa in front of j 2"
e:a <=> i: _ %^MONB4J:0 ;

"o deletion in front of j"
o:0 <=> u _ %^MONB4J:0 ;

!€ riejjtuo%^MONB4Jj
!€ ruejjtu00j
