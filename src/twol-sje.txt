! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or divvun@samediggi.no

! Pite Sámi TWOLC file
!
! $Id$
!
!

Alphabet
  a b c d e f g h i ï j k l m n o p q r s t u v w x y z æ ä ø ö å
  á é í 
  Å Ä Ö Ø Æ Á
  Á É Ó Ú Í À È Ò Ù Ì Ë Ü Ï Â Ê Ô Û Î Ã Ý þ Ñ Ð
  á é ó ú í à è ò ù ì ë ü ï â ê ô û î ã ý þ ñ ð ß ª ß %-
  d2:d t2:t j2:j h2:h g2:g
  ':0
  
  %^WG:0 %^jDEL:0 %^MONB4J:0 %^CDEL:0

! Morpheme boundaries:
 «  ! Derivational prefix
 »  ! Derivational suffix
 %< ! Inflectional prefx
 %> ! Inflectional suffix
 #  ! Word boundary for both lexicalised and dynamic compounds
 %^ ! (exceptional) soft hyphenation point

! End of alphabet definitions
;

Sets
  Letter = a b c d e f g h i ï j k l m n o p q r s t u v w x y z æ ä ø ö å á é í ó ú ;
  Cns    =   b c d   f g h     j k l m n   p q r s t   v w x   z                     ;
  Vow    = a       e       i ï           o           u       y   æ ä ø ö å á é í ó ú ;
  WeG    =  %^WG %^jDEL ;
  Dummy  =  %^WG %^jDEL %^MONB4J %^CDEL ;
  
!Definitions

!  Dummy     =   

Rules
 
 
! Consonant gradation rules

"Consonant Gradation for htt(j):ht(j)" 
t:0 <=> Vow: h t _ ( j ) (Vow:+) (Cns:+) ( Dummy: ) WeG: ;

"Consonant Gradation for hxx:hx" 
Cx:0 <=> Vow: h Cx _ (Vow:+) (Cns:+) ( Dummy: ) WeG: ;
     where Cx in ( p t k ) ;

"Consonant Gradation for xx:x" 
Cx:0 <=> Vow: Cx _  Vow:+ (Cns:+) ( Dummy: ) WeG: ;
     where Cx in ( p t k m n s r l f j v ) ; ! OBS fjerna j pga av ijja, och in igen för ijja

"Consonant Gradation for xxy:xy" 
Cx:0 <=> Vow: Cx _ Cy (Vow:+) (Cns:+) ( Dummy: ) WeG: ;
 where Cx in ( b b b b b b b d d d d f f f g g g g j j j j j j j j j j k k k l l l l l l l l l l m m m m n n n p p r r r r r r r r r r s s s s s s t t t t ŋ ŋ ŋ v v v v v )
       Cy in ( d j l m n r t j k n v d n t d l ŋ v b d g p k l r s t v s t v j b d f g k p s t v b g p s d j t s t b d f g j k p s t v j k n p t v j k m s g k n d l r g s )  
          matched ;
         
"Consonant Gradation for xy:y" 
Cx:0 <=> Vow: _ Cy (Vow:+) (Cns:+) ( Dummy: ) WeG: ;
     where Cx in ( d d )
           Cy in ( j n )  
          matched ;
         
"Consonant Gradation for hxxy:hxy" 
Cx:0 <=> h Cx _ Cy (Vow:+) (Cns:+) ( Dummy: ) WeG: ;
     where Cx in ( d t )
           Cy in ( j j )  
          matched ;

!"Consonant Gradation for hx:x"
"Delete h"
h:0 <=> Vow+ _ Cx: (Vow:+) (Cns:+) ( Dummy: ) WeG: ;
    where Cx in (p t k) ;

"Delete h before ts"
h:0 <=> Vow+ _ t s Vow:+ (Cns:+) ( Dummy: ) WeG: ;
    
"change unvoiced to voiced"
Cx:Cy <=> h: _ (Vow:+) (Cns:+) ( Dummy: ) WeG: ;
    where Cx in (p t k)
          Cy in (b d g)
          matched ;

"Consonant Gradation for xN:N" 
Cx:0 <=> Vow ([l|r|v|j]) _ Cy (Vow:+) (Cns:+) ( Dummy: ) WeG: ;
    where Cx in (b t k)
          Cy in (m n n)
          matched ;
          

! Other consonant rules 
! =====================

"Final Consonant Deletion"          
Cx:0 <=> _ (Dummy:) %^CDEL: ;
	where Cx in ( b d g ) ; 


"Auslautherdung"
Cx:Cy <=> _ .#. ;
    where Cx in ( d2 h2 g2 )
          Cy in ( t  j  k  )
          matched ;

"Word Final Consonant Cluster Simplification"
t2:0 <=> s _ .#. ;

"Fricative Auslautherdung"
s:t <=> _ j2: .#. ;

          
! ===========          
! Vowel rules 
! ===========          
        
"Vowel Shortening in Front of j-initial suffixes"
 e:i <=> Cns _ Dummy:* %>: j ;
 
 !€ riehpe>jt
 !€ r0e0bi0jt


"Vowel Deletion in front of j"
Vx:0 <=> Vy _ %^jDEL:0 ;
     where Vx in (e o)
           Vy in (i u)
           matched ;

"o to u in front of j"
o:u <=> _  ([Dummy: - %^jDEL:0]) %>: (j) %^MONB4J:0 ;
! This rule works both for Ill (without the jDEL) and for Com Sg (with it)



"aa in front of j 1"
i:a <=> _ e: %^MONB4J:0 ;


"aa in front of j 2"
e:a <=> i: _ %^MONB4J:0 ;


"o deletion in front of j"
o:0 <=> u _ %^MONB4J:0 ;

"i deletion in front of j"
i:0 <=> _ e Cns:* Vow:* %>: %^MONB4J:0 ;

"uä to uo in weak grade"
ä:o <=> u _ Cns:+ Vow:+ ( Dummy: ) WeG: ;

!€ juällge^WG
!€ juol0ge0

!€ juällkie^jDELj
!€ juol0ki00j

"u to å in odd stems"
u:å <=> _ Cns:* Vow: Cns (Dummy:)  %^MONB4J:0 ;

!€ ullul^WG^MONB4J
!€ ål0ol


!€ riejjtuo%^MONB4Jj
!€ ruejjtu00j

