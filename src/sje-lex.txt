! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or divvun@samediggi.no

    Multichar_Symbols

! POS
+N +V +A 
+Adv +CC +CS +Interj +Pron +Num

! Verbal MSP
+Prs +Fut +Prt +Prf 
+Ind +Imp +Cond +Opt
+Sg1 +Sg2 +Sg3 
+Du1 +Du2 +Du3
+Pl1 +Pl2 +Pl3
+Inf
+Pos +Neg +ConNeg
+GerI +GerII +PrfPrc +VAbe +Sup +VAdv

! Nominal MSP
+Sg +Pl
+Nom +Acc +Gen +Ill +Ine +Ela +Com +Ess +Abe
+Ord +Card
+Qst
+Prop +Ani +Mal +Fem +Sur +Plc +Org +Obj ! Semantic properties of names

+PxSg1 +PxSg2 +PxSg3 +PxPl1 +PxPl2 +PxPl3

%^WG    ! weak grade
%^jDEL  ! vowel deletion in front of j
%^MONB4J ! ie to aa (before j), uo to u before j

! Key lexicon

LEXICON Root

Noun ;
Propernoun ;
Verb ;
Adjective ;
Adverb ;
Conjunction ;
Subjunction ;
Interjection ;
Numeral ;
Pronoun ;
Postposition ;
Punctuation ;
Particle ;





! ========== !
! Inflection !
! ========== !

!juollkie+N+Abe	     morf =======> upper
!juollkie^WG>htahk   trans
!				     
!juollkie^WG>Abehtahk   mphon
!juol0kie00htahk	 trans ======> lower

! Noun 
! ===============

LEXICON N_EVEN
+N: EVENCASE ;

LEXICON EVENCASE
               N_EVEN_S ;
:%^WG          N_EVEN_W ;
:%^jDEL%>j     N_EVEN_J ;
+Sg+Ill:%^MONB4Jj K ;

LEXICON N_EVEN_S
+Sg+Nom:    K ;
+Ess:%>n K ;

LEXICON N_EVEN_W
+Sg+Gen:%>0  K ;
+Sg+Acc:%>v  K ;
+Sg+Ine:%>n K ;
+Sg+Ela:%>st K ;
+Abe:%>htahk K ; ! Normally
+Abe:%>htaka K ; ! Informants S and Bj 
+Abe:%>htak K ; ! Informants S and Bj 
+Pl+Nom:%>h  K ;

LEXICON N_EVEN_J
+Sg+Com:n   K ;
+Sg+Com:na  K ; !variant
+Pl+Gen:    K ;
+Pl+Acc:t   K ;
+Pl+Ine:n   K ;
+Pl+Ela:st  K ;
+Pl+Ill:ta  K ;
+Pl+Com:    K ;

LEXICON KISTAH
#;

LEXICON ÅÅPPPAA
#;

LEXICON ALMATJ
+N+Sg+Nom:%^WG PX ;
+N+Sg+Acc:av PX ;
+N+Sg+Gen:a PX ;
+N+Sg+Ill:ij PX ;
+N+Sg+Ine:in PX ;
+N+Sg+Ela:ist PX ;
+N+Sg+Com:ijn PX ;
+N+Sg+Ess:in PX ;
+N+Abe:ahtá PX ;
+N+Pl+Nom:ah PX ;
+N+Pl+Acc:ijt PX ;
+N+Pl+Gen:ij PX ;
+N+Pl+Ill:ijta PX ;
+N+Pl+Ine:ijn PX ;
+N+Pl+Ela:ist PX ;
+N+Pl+Com:ij PX ;



LEXICON ÅLMAAJ
+N+Sg+Nom:%^WGáj PX ;
+N+Sg+Acc:áv PX ;
+N+Sg+Gen:á PX ;
+N+Sg+Ill:áj PX ;
+N+Sg+Ine:án PX ;
+N+Sg+Ela:ást PX ;
+N+Sg+Com:ájn PX ;
+N+Ess:án PX ;
+N+Ess:%^WGán PX ;
+N+Abe:áhtahk PX ;
+N+Pl+Nom:áh PX ;
+N+Pl+Acc:ájt PX ;
+N+Pl+Gen:áj PX ;
+N+Pl+Ill:ájta PX ;
+N+Pl+Ine:ájn PX ;
+N+Pl+Ela:ást PX ;
+N+Pl+Com:áj PX ;


LEXICON BIENA ! is of course not even...
+N:%^WG N_EVEN_S ;
+N:ga N_EVEN_W ; 


LEXICON PX
# ;


! ========== !
! Adjectives !
! ========== !


LEXICON A_EVEN
+A+Attr: # ;
+A: EVENCASE ;








! ===== !
! Verbs !
! ===== !

LEXICON SLADDJIT
BÅHTET ;

LEXICON ÅRROT
BÅHTET ;

LEXICON VESSOT
BÅHTET ;

LEXICON VUODDJAT
+V+Inf:%>at # ; 
+V:     VUODDJAT_S ;
+V:%^WG VUODDJAT_W ;

LEXICON REKKNUT
BÅHTET ;

LEXICON BRUVKKUT
BÅHTET ;

LEXICON VADTSAAT
+V:     VADTSAAT_S ;
+V:%^WG VADTSAAT_W ;

LEXICON VADTSAAT_S
VUODDJAT_S ;

LEXICON VADTSAAT_W
VUODDJAT_W ;


LEXICON LÅHKÅT
BÅHTET ;

LEXICON BÅHTET
+V:     BÅHTET_S ;
+V:%^WG BÅHTET_W ;


LEXICON BÅHTET_S
+Inf:%>et # ; 
+Inf:%>iet # ;  ! JL
+GerII:%>iemin # ;
+PrfPrc:%>ám # ;

+Ind+Prs+Sg3:%>a #;
+Ind+Prs+Sg3:%>á #;
+Ind+Prs+Du1:%>in #;
+Ind+Prs+Du3:%>iepan #;
+Ind+Prs+Du3:%>iepah #;
+Ind+Prs+Pl1:%>iep #;
+Ind+Prs+Pl2:%>iepihtit #;
+Ind+Prs+Pl3:%>ih #;
+Ind+Prt+Sg1:%>iv #;
+Ind+Prt+Sg2:%>ih #;
+Ind+Prt+Pl3:%>in #; 

LEXICON BÅHTET_W
+GerI:%>ietin # ; 
+VAdv:ie # ;
+Sup:%>átjit # ;
+Vabe:%>iehk # ;

+Ind+Prs+Sg1:%>av #;
+Ind+Prs+Sg2:%>ah #;
+Ind+Prs+Du2:%>iepehtien #;
+Ind+Prt+Sg3:%>ij #;
+Ind+Prt+Du1:%>ijmen #;
+Ind+Prt+Du2:%>ijten #;
+Ind+Prt+Du3:%>ijkah #;
+Ind+Prt+Du3:%>ijkan #;
+Ind+Prt+Pl1:%>ijmeh #;
+Ind+Prt+Pl2:%>ijteh #;



LEXICON VUODDJAT_S
+Inf:%>at # ; 
+GerII:%>amin # ;
+PrfPrc:%>am # ;
+Ind+Prs+Sg3:%>á #;
+Ind+Prs+Du1:%>in #;
+Ind+Prs+Du2:%>apehtien #;
+Ind+Prs+Du3:%>apan #;
+Ind+Prs+Du3:%>apah #;
+Ind+Prs+Pl1:%>ap #;
+Ind+Prs+Pl2:%>apihtit #;
+Ind+Prs+Pl3:%>ih #;
+Ind+Prt+Sg1:%>iv #;
+Ind+Prt+Sg2:%>ih #;
+Ind+Prt+Pl3:%>in #; 

LEXICON VUODDJAT_W
+GerI:%>atin # ; 
+VAdv:%>a # ;
+Sup:%>atjit # ;
+Sup:%>atjit # ;
+Vabe:%>ahk # ;

+Ind+Prs+Sg1:%>av #;
+Ind+Prs+Sg2:%>ah #;
+Ind+Prt+Sg3:%>aj #;
+Ind+Prt+Du1:%>ajmen #;
+Ind+Prt+Du2:%>ajten #;
+Ind+Prt+Du3:%>ajkah #;
+Ind+Prt+Du3:%>ajkan #;
+Ind+Prt+Pl1:%>ajmeh #;
+Ind+Prt+Pl2:%>ajteh #;




LEXICON NAMMADIT
+V+Inf:%>it # ;
+V+Ind+Prs: V_ODD_IND_PRS ;
+V+Ind+Prt: V_ODD_IND_PRT ;

LEXICON V_ODD_IND_PRS
+Sg1:%>av # ; 
+Sg2:%>ah # ; 
+Sg3: # ; 
+Sg3:%>a # ; 
+Du1:%>in # ; 
+Du2:%>iehpien # ; 
+Du3:%>iehpaa # ; 
+Pl1:%>ip # ; 
+Pl2:%>ihpit # ; 
+Pl3:%>ih # ; 

LEXICON V_ODD_IND_PRT
+Sg1:%>iv # ; 
+Sg2:%>ih # ; 
+Sg3:%>ij # ; 
+Du1:%>ijmien # ; 
+Du2:%>iejtien # ; 
+Du3:%>iejkaa # ; 
+Pl1:%>iejmieh # ; 
+Pl2:%>iejtieh # ; 
+Pl3:%>im # ; 
 
 











LEXICON K
# ;
! +Qst:go # ; ! Dummy clitic only!


