! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or divvun@samediggi.no

    Multichar_Symbols

! POS
+N +V +A 
+Adv +CC +CS +Interj +Pron +Num

! Verbal MSP
+Prs +Fut +Prt +Prf 
+Ind +Imp +Cond +Opt
+Sg1 +Sg2 +Sg3 
+Du1 +Du2 +Du3
+Pl1 +Pl2 +Pl3
+Inf
+Pos +Neg +ConNeg
+GerI +GerII +PrfPrc +VAbe +Sup +VAdv
+Cmp

! Nominal MSP
+Sg +Pl
+Nom +Acc +Gen +Ill +Ine +Ela +Com +Ess +Abe
+Ord +Card
+Qst
+Prop +Ani +Mal +Fem +Sur +Plc +Org +Obj ! Semantic properties of names

+PxSg1 +PxSg2 +PxSg3 +PxPl1 +PxPl2 +PxPl3

%^WG    ! weak grade
%^jDEL  ! vowel deletion in front of j
%^MONB4J ! ie to aa (before j), uo to u before j

+Use/Sub ! Not part of standard
+Use/NG  ! Part of standard, but not generated
+Use/Circ
+Hyph
+RCmpnd
+Use/-Spell
+CmpN/First

d2 t2 j2 h2 g2 ! Variable consonants, undergo Auslautherdung
               ! NB! This is the opposite of the sme pattern

! Symbols that need to be escaped on the lower side (towards twolc):
 »7     ! »
 «7     ! «
 %[%>%] ! >
 %[%<%] ! <


! Flag diacritics
 @P.NeedNoun.ON@  @D.NeedNoun.ON@  @C.NeedNoun@ ! (Dis)allowing compounds
 @U.Cap.Obl@  @U.Cap.Opt@   ! Allowing downcasing of
                  ! derived names: deatnulasj.
               ! Actually, downcasing should be obligatory


! Key lexicon

LEXICON Root

Noun ;
!ProperNoun ;
Verb ;
!Adjective ;
!Adverb ;
Conjunction ;
Subjunction ;
Interjection ;
!Numeral ;
Pronoun ;
Postposition ;
!Abbreviation ;
!Punctuation ;
Particle ;





! Prefixing and compounding
! =========================


! This is for regular compounding. All entries NOT requiring a hyphen should point to this:
LEXICON R
                     +Use/Circ+Cmp#:# Noun ;                       ! This is the normal case.           
      @P.NeedNoun.ON@+Use/Circ+Cmp#:@P.NeedNoun.ON@# Verb ;        ! Flag diacr declares that the compound   
!      @P.NeedNoun.ON@+Use/Circ+Cmp#:@P.NeedNoun.ON@# Adjective ;   ! needs a N tag later in the derivation  
                +Hyph+Use/Circ+Cmp#:%- Noun ;                      ! These 3 cases add a hyphen, so     
 @P.NeedNoun.ON@+Hyph+Use/Circ+Cmp#:@P.NeedNoun.ON@%-  Verb ;      ! Sem-julggaštus and maana-gåetie       
! @P.NeedNoun.ON@+Hyph+Use/Circ+Cmp#:@P.NeedNoun.ON@%-  Adjective ; ! are allowed.                 

! +Use/Circ+Cmp%-#:%-# Acronym ;    ! maana-tv, "lomme-cd-spelar" ! No acronyms for sje yet.
! +Use/Circ+Cmp%-#:%-# Numeral ;    ! maana-tv, "lomme-cd-spelar"
! +Use/Circ+Cmp%-#:%-# ProperNoun ; ! Proper nouns as the 2nd part of compounds for non-hyph.
                                   ! words. viessu-London goes through here.

         +RCmpnd:%- ENDLEX ;     ! This to take care of Oahpo- ja dutkandept - want this in speller


! This is for compounding with hyphen. All entries REQUIRING a hyphen should point to this:
LEXICON RHyph ! Recursive lexicon from all classes REQUIRING a hyphen to follow.
              ! NBNBNB!!! The hyphen should be added here! NOT in the other lexicons!

                +Use/Circ+Cmp%-#:%-# Noun ;                                     ! This is the normal case.
 @P.NeedNoun.ON@+Use/Circ+Cmp%-#:@P.NeedNoun.ON@%-# Verb ;       ! Flag diacr declares that the compound
! @P.NeedNoun.ON@+Use/Circ+Cmp%-#:@P.NeedNoun.ON@%-# Adjective ; ! needs a N tag later in the derivation

! +Use/Circ+Cmp%-#:%-# Acronym ;    ! maana-tv, "lomme-cd-spelar"
! +Use/Circ+Cmp%-#:%-# ProperNoun ; ! Proper nouns as the 2nd part of compounds for hyph-words.
                               ! London-Hull is covered here, whereas Hull-viessu
                               ! is covered by RHyph + Noun.

         +RCmpnd:%- ENDLEX ;      ! This to take care of Oahpo- ja dutkandept - want this in speller

! Proper noun compounds without hyphens are NOT presently covered - if they were
! to be added, they need to be marked +Use/Sub. Examples: LondonHull & viessuLondon.



! ========== !
! Inflection !
! ========== !

!juollkie+N+Abe             morf =======> upper
!juollkie^WG>htahk   trans
!                                     
!juollkie^WG>Abehtahk   mphon
!juol0kie00htahk         trans ======> lower

! Noun 
! ===============



LEXICON N_EVEN_NOCG
+N: EVENCASE ;
+N: N_EVEN_W ; ! No CG here

LEXICON N_EVEN
+N: EVENCASE ;
+N:^WG N_EVEN_W ; ! CG here

LEXICON EVENCASE
               N_EVEN_S ;
:%^jDEL%>j     N_EVEN_J ;
+Sg+Ill:%>%^MONB4Jj K ;

LEXICON N_EVEN_S
+Sg+Nom:    K ;
+Ess:%>n K ;

LEXICON N_EVEN_W
+Sg+Gen:%>0  K ;
+Sg+Acc:%>v  K ;
+Sg+Ine:%>n K ;
+Sg+Ela:%>st K ;
+Abe:%>htahk K ; ! Lehtiranta
+Abe:%>dak K ; ! Norm
+Abe:%>htaka K ; ! Informants S and Bj 
+Abe:%>htak K ; ! Informants S and Bj 
+Pl+Nom:%>0  K ;

LEXICON N_EVEN_J
+Sg+Com:%^MONB4Jn   K ;
+Sg+Com:%^MONB4Jna  K ; !variant
+Pl+Gen:    K ;
+Pl+Acc:t   K ;
+Pl+Ine:n   K ;
+Pl+Ela:st  K ;
+Pl+Ill:ta  K ; !Lehtiranta
+Pl+Ill:da  K ; !norm
+Pl+Com:    K ;

LEXICON ÅÅPPPAA
+N: ÅBBÁCASE ;

LEXICON ÅBBÁCASE
+Sg+Nom:    K ;
+Ess:%>n K ;
+Sg+Gen:%>0  K ;
+Sg+Acc:%>v  K ;
+Sg+Ine:%>n K ;
+Sg+Ela:%>st K ;
+Abe:%>htahk K ; ! Lehtiranta
+Abe:%>dak K ; ! Norm
+Abe:%>htaka K ; ! Informants S and Bj 
+Abe:%>htak K ; ! Informants S and Bj 
+Pl+Nom:%>0  K ;
+Sg+Ill:%^MONB4Jj K ;
+Sg+Com:%>jn   K ;
+Sg+Com:%>jna  K ; !variant
+Pl+Gen:%>j    K ;
+Pl+Acc:%>jt   K ;
+Pl+Ine:%>jn   K ;
+Pl+Ela:%>jst  K ;
+Pl+Ill:%>jta  K ; !Lehtiranta
+Pl+Ill:%>jda  K ; !norm
+Pl+Com:%>j    K ;


LEXICON N_ODD
+N+Sg+Nom:%^WG PX ;
+N+Sg+Acc:av PX ;
+N+Sg+Gen:a PX ;
+N+Sg+Ill:ij PX ;
+N+Sg+Ine:in PX ;
+N+Sg+Ela:ist PX ;
+N+Sg+Com:ijn PX ;
+N+Sg+Ess:in PX ;
+N+Abe:ahtá PX ;
+N+Pl+Nom:a PX ;
+N+Pl+Acc:ijt PX ;
+N+Pl+Gen:ij PX ;
+N+Pl+Ill:ijda PX ;
+N+Pl+Ine:ijn PX ;
+N+Pl+Ela:ist PX ;
+N+Pl+Com:ij PX ;

LEXICON N_CONTR_C
ÅLMAAJ ; ! skall ordnas till

LEXICON ÅLMAAJ
+N+Sg+Nom:%^WGáj PX ;
+N+Sg+Acc:áv PX ;
+N+Sg+Gen:á PX ;
+N+Sg+Ill:áj PX ;
+N+Sg+Ine:án PX ;
+N+Sg+Ela:ást PX ;
+N+Sg+Com:ájn PX ;
+N+Ess:án PX ;
+N+Ess:%^WGán PX ;
+N+Abe:áhtahk PX ;
+N+Pl+Nom:áh PX ;
+N+Pl+Acc:ájt PX ;
+N+Pl+Gen:áj PX ;
+N+Pl+Ill:ájta PX ;
+N+Pl+Ine:ájn PX ;
+N+Pl+Ela:ást PX ;
+N+Pl+Com:áj PX ;

LEXICON GAHPER
N_ODD ;

LEXICON BIENA
+N:%^WG BIENASHORT ;
+N: BIENALONG ;

LEXICON BIENALONG
+N+Sg+Acc:av PX ;
+N+Sg+Gen:a PX ;
+N+Sg+Ill:aj PX ;
+N+Sg+Ine:an PX ;
+N+Sg+Ela:ast PX ;
+N+Sg+Com:ajn PX ;
+N+Abe:ahtahk PX ;
+N+Pl+Nom:a PX ;
+N+Pl+Acc:ajt PX ;
+N+Pl+Gen:aj PX ;
+N+Pl+Ill:adta PX ;
+N+Pl+Ine:ajn PX ;
+N+Pl+Ela:ast PX ;
+N+Pl+Com:aj PX ;


LEXICON BIENASHORT
+Sg+Nom: PX ;
+Ess:%>n PX ;

LEXICON PX
# ;


! ========== !
! Adjectives !
! ========== !


LEXICON A_EVEN
+A+Attr: # ;
+A: EVENCASE ;








! ===== !
! Verbs !
! ===== !

LEXICON SLADDJIT
BÅHTET ;

LEXICON ÅRROT
+V:     ÅRROT_S ;
+V:%^WG ÅRROT_W ;

LEXICON ÅRROT_S
+Inf:%>ot # ; 
+GerII:%>umin # ;
+PrfPrc:%>um # ;

+Ind+Prs+Sg3:%>u #;
+Ind+Prs+Du1:%>un #;
+Ind+Prs+Du2:%>obehtin #;
+Ind+Prs+Du3:%>oba #;
+Ind+Prs+Pl1:%>op #;
+Ind+Prs+Pl2:%>obihtit #;
+Ind+Prs+Pl3:%>u #;
+Ind+Prt+Sg1:%>uv #;
+Ind+Prt+Sg2:%>u #;
+Ind+Prt+Pl3:%>un #; 

LEXICON ÅRROT_W
+GerI:%>udin # ; 
+VAdv:u # ;
+Sup:%>utjit # ;
+Vabe:%>uhk # ;

+Ind+Prs+Sg1:%>ov #;
+Ind+Prs+Sg2:%>o #;
+Ind+Prt+Sg3:%>oj #;
+Ind+Prt+Du1:%>ojmen #;
+Ind+Prt+Du2:%>ojden #;
+Ind+Prt+Du3:%>ojga #;
+Ind+Prt+Du3:%>ojgan #;
+Ind+Prt+Pl1:%>ojme #;
+Ind+Prt+Pl2:%>ojde #;

LEXICON VESSOT
ÅRROT ;

LEXICON BALLAT
+V:     BALLAT_S ;
+V:%^WG BALLAT_W ;

LEXICON VADTSAAT
+V:     VADTSAAT_S ;
+V:%^WG VADTSAAT_W ;

LEXICON VADTSAAT_S
BALLAT_S ;

LEXICON VADTSAAT_W
BALLAT_W ;


LEXICON LÅHKÅT
+V:     LÅHKÅT_S ;
+V:%^WG LÅHKÅT_W ;

LEXICON LÅHKÅT_S
+Inf:%>åt # ; 
+GerII:%>åmin # ; ! ???
+PrfPrc:%>åm # ;

+Ind+Prs+Sg3:%>a #;
+Ind+Prs+Sg3:%>á #;
+Ind+Prs+Du1:%>in #;
+Ind+Prs+Du2:%>åbehtin #;
+Ind+Prs+Du3:%>åba #;
+Ind+Prs+Pl1:%>åp #;
+Ind+Prs+Pl2:%>åbihtit #;
+Ind+Prs+Pl3:%>i #;
+Ind+Prt+Sg1:%>iv #;
+Ind+Prt+Sg2:%>i #;
+Ind+Prt+Pl3:%>in #; 

LEXICON LÅHKÅT_W
+GerI:%>ietin # ; 
+VAdv:ie # ;
+Sup:%>átjit # ;
+Vabe:%>iehk # ;

+Ind+Prs+Sg1:%>åv #;
+Ind+Prs+Sg2:%>å #;
+Ind+Prt+Sg3:%>åj #;
+Ind+Prt+Du1:%>åjmen #;
+Ind+Prt+Du2:%>åjden #;
+Ind+Prt+Du3:%>åjga #;
+Ind+Prt+Pl1:%>åjme #;
+Ind+Prt+Pl2:%>åjde #;


LEXICON BÅHTET
+V:     BÅHTET_S ;
+V:%^WG BÅHTET_W ;


LEXICON BÅHTET_S
+Inf:%>et # ; 
+GerII:%>iemin # ;
+PrfPrc:%>ám # ;

+Ind+Prs+Sg3:%>a #;
+Ind+Prs+Sg3:%>á #;
+Ind+Prs+Du1:%>in #;
+Ind+Prs+Du2:%>ebehtin #;
+Ind+Prs+Du3:%>eba #;
+Ind+Prs+Pl1:%>ep #;
+Ind+Prs+Pl2:%>ebihtit #;
+Ind+Prs+Pl3:%>i #;
+Ind+Prt+Sg1:%>iv #;
+Ind+Prt+Sg2:%>i #;
+Ind+Prt+Pl3:%>in #; 

LEXICON BÅHTET_W
+GerI:%>ietin # ; 
+VAdv:ie # ;
+Sup:%>átjit # ;
+Vabe:%>iehk # ;

+Ind+Prs+Sg1:%>áv #;
+Ind+Prs+Sg1:%>av #;
+Ind+Prs+Sg2:%>á #;
+Ind+Prs+Sg2:%>a #;
+Ind+Prt+Sg3:%>ij #;
+Ind+Prt+Du1:%>ijmen #;
+Ind+Prt+Du2:%>ijden #;
+Ind+Prt+Du3:%>ijga #;
+Ind+Prt+Pl1:%>ijme #;
+Ind+Prt+Pl2:%>ijde #;


LEXICON V_CONTR
+V+Inf:%>t # ; 
+V+PrfPrc:%>m #;

+V+Ind+Prs+Sg1:%>v #;
+V+Ind+Prs+Sg2: #;
+V+Ind+Prs+Sg3: #;
+V+Ind+Prs+Du1:%>jin #;
+V+Ind+Prs+Du2:%>behten #;
+V+Ind+Prs+Du3:%>ba #;
+V+Ind+Prs+Pl1:%>p #;
+V+Ind+Prs+Pl2:%>bihtit #;
+V+Ind+Prs+Pl3:%>ji #;

+V+Ind+Prt+Sg1:%>jiv #;
+V+Ind+Prt+Sg2:%>ji #;
+V+Ind+Prt+Sg3:%>j #;
+V+Ind+Prt+Du1:%>jmen #;
+V+Ind+Prt+Du2:%>jden #;
+V+Ind+Prt+Du3:%>jga #;
+V+Ind+Prt+Pl1:%>jme #;
+V+Ind+Prt+Pl2:%>jde #;
+V+Ind+Prt+Pl3:%>jin #;


LEXICON BALLAT_S
+Inf:%>at # ; 
+GerII:%>amin # ;
+PrfPrc:%>am # ;
+Ind+Prs+Sg3:%>á #;
+Ind+Prs+Du1:%>in #;
+Ind+Prs+Du2:%>abehtin #;
+Ind+Prs+Du3:%>aba #;
+Ind+Prs+Pl1:%>ap #;
+Ind+Prs+Pl2:%>abihtit #;
+Ind+Prs+Pl3:%>i #;
+Ind+Prt+Sg1:%>iv #;
+Ind+Prt+Sg2:%>i #;
+Ind+Prt+Pl3:%>in #; 

LEXICON BALLAT_W
+GerI:%>adin # ; 
+VAdv:%>a # ;
+Sup:%>atjit # ;
+Vabe:%>ahk # ;

    
+Ind+Prs+Sg1:%>av #;
+Ind+Prs+Sg2:%>a #;
+Ind+Prt+Sg3:%>aj #;
+Ind+Prt+Du1:%>ajmen #;
+Ind+Prt+Du2:%>ajden #;
+Ind+Prt+Du3:%>ajga #;
+Ind+Prt+Pl1:%>ajme #;
+Ind+Prt+Pl2:%>ajde #;




LEXICON NAMMADIT
+V+Inf:%>it # ;
+V+Ind+Prs: V_ODD_IND_PRS ;
+V+Ind+Prt: V_ODD_IND_PRT ;

LEXICON V_ODD_IND_PRS
+Sg1:%>av # ; 
+Sg2:%>a # ; 
+Sg3: # ; !oregelbunden
+Sg3:%>a # ; 
+Du1:%>in # ; 
+Du2:%>ihpen # ; 
+Du3:%>ebá # ; 
+Pl1:%>ip # ; 
+Pl2:%>ihpit # ; 
+Pl3:%>i # ; 

LEXICON V_ODD_IND_PRT
+Sg1:%>iv # ; 
+Sg2:%>i # ; 
+Sg3:%>ij # ; 
+Du1:%>ijmen # ; 
+Du2:%>ijden # ; 
+Du3:%>ijga # ; 
+Pl1:%>ijme # ; 
+Pl2:%>ijde # ; 
+Pl3:%>in # ; 
 
 











LEXICON K
# ;
! +Qst:go # ; ! Dummy clitic only!


LEXICON ENDLEX
   @D.NeedNoun.ON@ # ;
	

