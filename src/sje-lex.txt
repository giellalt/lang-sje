! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or divvun@samediggi.no

    Multichar_Symbols

! POS
+N +V +A 
+Adv +CC +CS +Interj +Pron +Num

! Verbal MSP
+Prs +Fut +Prt +Prf 
+Ind +Imp +Cond +Opt
+Sg1 +Sg2 +Sg3 
+Du1 +Du2 +Du3
+Pl1 +Pl2 +Pl3
+Inf
+Pos +Neg +ConNeg
+GerI +GerII +PrfPrc +VAbe +Sup +VAdv

! Nominal MSP
+Sg +Pl
+Nom +Acc +Gen +Ill +Ine +Ela +Com +Ess +Abe
+Ord +Card
+Qst
+Prop +Ani +Mal +Fem +Sur +Plc +Org +Obj ! Semantic properties of names

+PxSg1 +PxSg2 +PxSg3 +PxPl1 +PxPl2 +PxPl3

%^WG    ! weak grade
%^jDEL  ! vowel deletion in front of j
%^MONB4J ! ie to aa (before j), uo to u before j

+Use/Sub ! Not part of standard
+Use/NG  ! Part of standard, but not generated

! Key lexicon

LEXICON Root

Noun ;
Propernoun ;
Verb ;
Adjective ;
Adverb ;
Conjunction ;
Subjunction ;
Interjection ;
Numeral ;
Pronoun ;
Postposition ;
Punctuation ;
Particle ;





! ========== !
! Inflection !
! ========== !

!juollkie+N+Abe             morf =======> upper
!juollkie^WG>htahk   trans
!                                     
!juollkie^WG>Abehtahk   mphon
!juol0kie00htahk         trans ======> lower

! Noun 
! ===============

LEXICON N_EVEN
+N: EVENCASE ;

LEXICON EVENCASE
               N_EVEN_S ;
:%^WG          N_EVEN_W ;
:%^jDEL%>j     N_EVEN_J ;
+Sg+Ill:%^MONB4Jj K ;

LEXICON N_EVEN_S
+Sg+Nom:    K ;
+Ess:%>n K ;

LEXICON N_EVEN_W
+Sg+Gen:%>0  K ;
+Sg+Acc:%>v  K ;
+Sg+Ine:%>n K ;
+Sg+Ela:%>st K ;
+Abe:%>htahk K ; ! Lehtiranta
+Abe:%>dak K ; ! Norm
+Abe:%>htaka K ; ! Informants S and Bj 
+Abe:%>htak K ; ! Informants S and Bj 
+Pl+Nom:%>h  K ;

LEXICON N_EVEN_J
+Sg+Com:n   K ;
+Sg+Com:na  K ; !variant
+Pl+Gen:    K ;
+Pl+Acc:t   K ;
+Pl+Ine:n   K ;
+Pl+Ela:st  K ;
+Pl+Ill:ta  K ; !Lehtiranta
+Pl+Ill:da  K ; !norm
+Pl+Com:    K ;

LEXICON KISTAH
#;

LEXICON ÅÅPPPAA
+N: ÅBBÁCASE ;

LEXICON ÅBBÁCASE
+Sg+Nom:    K ;
+Ess:%>n K ;
+Sg+Gen:%>0  K ;
+Sg+Acc:%>v  K ;
+Sg+Ine:%>n K ;
+Sg+Ela:%>st K ;
+Abe:%>htahk K ; ! Lehtiranta
+Abe:%>dak K ; ! Norm
+Abe:%>htaka K ; ! Informants S and Bj 
+Abe:%>htak K ; ! Informants S and Bj 
+Pl+Nom:%>0  K ;
+Sg+Ill:%^MONB4Jj K ;
+Sg+Com:%>jn   K ;
+Sg+Com:%>jna  K ; !variant
+Pl+Gen:%>j    K ;
+Pl+Acc:%>jt   K ;
+Pl+Ine:%>jn   K ;
+Pl+Ela:%>jst  K ;
+Pl+Ill:%>jta  K ; !Lehtiranta
+Pl+Ill:%>jda  K ; !norm
+Pl+Com:%>j    K ;


LEXICON ALMATJ
+N+Sg+Nom:%^WG PX ;
+N+Sg+Acc:av PX ;
+N+Sg+Gen:a PX ;
+N+Sg+Ill:ij PX ;
+N+Sg+Ine:in PX ;
+N+Sg+Ela:ist PX ;
+N+Sg+Com:ijn PX ;
+N+Sg+Ess:in PX ;
+N+Abe:ahtá PX ;
+N+Pl+Nom:ah PX ;
+N+Pl+Acc:ijt PX ;
+N+Pl+Gen:ij PX ;
+N+Pl+Ill:ijta PX ;
+N+Pl+Ine:ijn PX ;
+N+Pl+Ela:ist PX ;
+N+Pl+Com:ij PX ;



LEXICON ÅLMAAJ
+N+Sg+Nom:%^WGáj PX ;
+N+Sg+Acc:áv PX ;
+N+Sg+Gen:á PX ;
+N+Sg+Ill:áj PX ;
+N+Sg+Ine:án PX ;
+N+Sg+Ela:ást PX ;
+N+Sg+Com:ájn PX ;
+N+Ess:án PX ;
+N+Ess:%^WGán PX ;
+N+Abe:áhtahk PX ;
+N+Pl+Nom:áh PX ;
+N+Pl+Acc:ájt PX ;
+N+Pl+Gen:áj PX ;
+N+Pl+Ill:ájta PX ;
+N+Pl+Ine:ájn PX ;
+N+Pl+Ela:ást PX ;
+N+Pl+Com:áj PX ;


LEXICON BIENA
+N:%^WG BIENASHORT ;
+N: BIENALONG ;

LEXICON BIENALONG
+N+Sg+Acc:av PX ;
+N+Sg+Gen:a PX ;
+N+Sg+Ill:aj PX ;
+N+Sg+Ine:an PX ;
+N+Sg+Ela:ast PX ;
+N+Sg+Com:ajn PX ;
+N+Abe:ahtahk PX ;
+N+Pl+Nom:ah PX ;
+N+Pl+Acc:ajt PX ;
+N+Pl+Gen:aj PX ;
+N+Pl+Ill:ajta PX ;
+N+Pl+Ine:ajn PX ;
+N+Pl+Ela:ast PX ;
+N+Pl+Com:aj PX ;


LEXICON BIENASHORT
+Sg+Nom: PX ;
+Ess:%>n PX ;

LEXICON PX
# ;


! ========== !
! Adjectives !
! ========== !


LEXICON A_EVEN
+A+Attr: # ;
+A: EVENCASE ;








! ===== !
! Verbs !
! ===== !

LEXICON SLADDJIT
BÅHTET ;

LEXICON ÅRROT
+V:     ÅRROT_S ;
+V:%^WG ÅRROT_W ;

LEXICON ÅRROT_S
+Inf:%>ot # ; 
+GerII:%>umin # ;
+PrfPrc:%>um # ;

+Ind+Prs+Sg3:%>u #;
+Ind+Prs+Du1:%>un #;
+Ind+Prs+Du2:%>obehtin #;
+Ind+Prs+Du3:%>oba #;
+Ind+Prs+Pl1:%>op #;
+Ind+Prs+Pl2:%>obihtit #;
+Ind+Prs+Pl3:%>u #;
+Ind+Prt+Sg1:%>uv #;
+Ind+Prt+Sg2:%>u #;
+Ind+Prt+Pl3:%>un #; 

LEXICON ÅRROT_W
+GerI:%>udin # ; 
+VAdv:u # ;
+Sup:%>utjit # ;
+Vabe:%>uhk # ;

+Ind+Prs+Sg1:%>ov #;
+Ind+Prs+Sg2:%>o #;
+Ind+Prt+Sg3:%>oj #;
+Ind+Prt+Du1:%>ojmen #;
+Ind+Prt+Du2:%>ojden #;
+Ind+Prt+Du3:%>ojga #;
+Ind+Prt+Du3:%>ojgan #;
+Ind+Prt+Pl1:%>ojme #;
+Ind+Prt+Pl2:%>ojde #;

LEXICON VESSOT
ÅRROT ;

LEXICON BALLAT
+V:     BALLAT_S ;
+V:%^WG BALLAT_W ;

LEXICON VADTSAAT
+V:     VADTSAAT_S ;
+V:%^WG VADTSAAT_W ;

LEXICON VADTSAAT_S
BALLAT_S ;

LEXICON VADTSAAT_W
BALLAT_W ;


LEXICON LÅHKÅT
+V:     LÅHKÅT_S ;
+V:%^WG LÅHKÅT_W ;

LEXICON LÅHKÅT_S
+Inf:%>åt # ; 
+GerII:%>åmin # ; ! ???
+PrfPrc:%>åm # ;

+Ind+Prs+Sg3:%>a #;
+Ind+Prs+Sg3:%>á #;
+Ind+Prs+Du1:%>in #;
+Ind+Prs+Du2:%>åbehtin #;
+Ind+Prs+Du3:%>åba #;
+Ind+Prs+Pl1:%>åp #;
+Ind+Prs+Pl2:%>åbihtit #;
+Ind+Prs+Pl3:%>i #;
+Ind+Prt+Sg1:%>iv #;
+Ind+Prt+Sg2:%>i #;
+Ind+Prt+Pl3:%>in #; 

LEXICON LÅHKÅT_W
+GerI:%>ietin # ; 
+VAdv:ie # ;
+Sup:%>átjit # ;
+Vabe:%>iehk # ;

+Ind+Prs+Sg1:%>åv #;
+Ind+Prs+Sg2:%>å #;
+Ind+Prt+Sg3:%>åj #;
+Ind+Prt+Du1:%>åjmen #;
+Ind+Prt+Du2:%>åjden #;
+Ind+Prt+Du3:%>åjga #;
+Ind+Prt+Pl1:%>åjme #;
+Ind+Prt+Pl2:%>åjde #;


LEXICON BÅHTET
+V:     BÅHTET_S ;
+V:%^WG BÅHTET_W ;


LEXICON BÅHTET_S
+Inf:%>et # ; 
+GerII:%>iemin # ;
+PrfPrc:%>ám # ;

+Ind+Prs+Sg3:%>a #;
+Ind+Prs+Sg3:%>á #;
+Ind+Prs+Du1:%>in #;
+Ind+Prs+Du2:%>ebehtin #;
+Ind+Prs+Du3:%>eba #;
+Ind+Prs+Pl1:%>ep #;
+Ind+Prs+Pl2:%>ebihtit #;
+Ind+Prs+Pl3:%>i #;
+Ind+Prt+Sg1:%>iv #;
+Ind+Prt+Sg2:%>i #;
+Ind+Prt+Pl3:%>in #; 

LEXICON BÅHTET_W
+GerI:%>ietin # ; 
+VAdv:ie # ;
+Sup:%>átjit # ;
+Vabe:%>iehk # ;

+Ind+Prs+Sg1:%>áv #;
+Ind+Prs+Sg2:%>á #;
+Ind+Prt+Sg3:%>ij #;
+Ind+Prt+Du1:%>ijmen #;
+Ind+Prt+Du2:%>ijden #;
+Ind+Prt+Du3:%>ijga #;
+Ind+Prt+Pl1:%>ijme #;
+Ind+Prt+Pl2:%>ijde #;


LEXICON KONTR
+V+Inf:%>t # ; 
+V+PrfPrc:%>m #;

+V+Ind+Prs+Sg1:%>v #;
+V+Ind+Prs+Sg2: #;
+V+Ind+Prs+Sg3: #;
+V+Ind+Prs+Du1:%>jin #;
+V+Ind+Prs+Du2:%>behten #;
+V+Ind+Prs+Du3:%>ba #;
+V+Ind+Prs+Pl1:%>p #;
+V+Ind+Prs+Pl2:%>bihtit #;
+V+Ind+Prs+Pl3:%>ji #;

+V+Ind+Prt+Sg1:%>jiv #;
+V+Ind+Prt+Sg2:%>ji #;
+V+Ind+Prt+Sg3:%>ji #;
+V+Ind+Prt+Du1:%>jmen #;
+V+Ind+Prt+Du2:%>jden #;
+V+Ind+Prt+Du3:%>jga #;
+V+Ind+Prt+Pl1:%>jma #;
+V+Ind+Prt+Pl2:%>jda #;
+V+Ind+Prt+Pl3:%>jin #;


LEXICON BALLAT_S
+Inf:%>at # ; 
+GerII:%>amin # ;
+PrfPrc:%>am # ;
+Ind+Prs+Sg3:%>á #;
+Ind+Prs+Du1:%>in #;
+Ind+Prs+Du2:%>apehtien #;
+Ind+Prs+Du3:%>apan #;
+Ind+Prs+Du3:%>apah #;
+Ind+Prs+Pl1:%>ap #;
+Ind+Prs+Pl2:%>apihtit #;
+Ind+Prs+Pl3:%>ih #;
+Ind+Prt+Sg1:%>iv #;
+Ind+Prt+Sg2:%>ih #;
+Ind+Prt+Pl3:%>in #; 

LEXICON BALLAT_W
+GerI:%>atin # ; 
+VAdv:%>a # ;
+Sup:%>atjit # ;
+Sup:%>atjit # ;
+Vabe:%>ahk # ;

+Ind+Prs+Sg1:%>av #;
+Ind+Prs+Sg2:%>ah #;
+Ind+Prt+Sg3:%>aj #;
+Ind+Prt+Du1:%>ajmen #;
+Ind+Prt+Du2:%>ajten #;
+Ind+Prt+Du3:%>ajkah #;
+Ind+Prt+Du3:%>ajkan #;
+Ind+Prt+Pl1:%>ajmeh #;
+Ind+Prt+Pl2:%>ajteh #;




LEXICON NAMMADIT
+V+Inf:%>it # ;
+V+Ind+Prs: V_ODD_IND_PRS ;
+V+Ind+Prt: V_ODD_IND_PRT ;

LEXICON V_ODD_IND_PRS
+Sg1:%>av # ; 
+Sg2:%>ah # ; 
+Sg3: # ; 
+Sg3:%>a # ; 
+Du1:%>in # ; 
+Du2:%>iehpien # ; 
+Du3:%>iehpaa # ; 
+Pl1:%>ip # ; 
+Pl2:%>ihpit # ; 
+Pl3:%>ih # ; 

LEXICON V_ODD_IND_PRT
+Sg1:%>iv # ; 
+Sg2:%>ih # ; 
+Sg3:%>ij # ; 
+Du1:%>ijmien # ; 
+Du2:%>iejtien # ; 
+Du3:%>iejkaa # ; 
+Pl1:%>iejmieh # ; 
+Pl2:%>iejtieh # ; 
+Pl3:%>im # ; 
 
 











LEXICON K
# ;
! +Qst:go # ; ! Dummy clitic only!

