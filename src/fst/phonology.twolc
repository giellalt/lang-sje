! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@uit.no or feedback@divvun.no
!
! NB: through June 2019, J. Wilbur's development work made possible by 
! Deutsche Forschungsgemeinschaft (grant 286335341)

!! # Pite Sámi TWOLC file
!
! $Id$
!
!! This file documents the [phonology.twolc file](http://github.com/giellalt/lang-sje/blob/main/src/fst/phonology.twolc) 


Alphabet
  a b c d e f g h i ï j k l m n ŋ o p q r s t u v w x y z æ ä ø ö å
  A B C D E F G H I   J K L M N Ŋ O P Q R S T U V w X Y Z Æ Ä Ø Ö Å
  á é í 
  Å Ä Ö Ø Æ Á
  Á É Ó Ú Í À È Ò Ù Ì Ë Ü Ï Â Ê Ô Û Î Ã Ý þ Ñ Ð
  á é ó ú í à è ò ù ì ë ü ï â ê ô û î ã ý þ ñ ð ª %- ß
  i2:i u2:u ä2:ä
  b2:b d2:d g2:g t2:t j2:j
  %^O:o
  ':0

  %^WG:0     !!≈ * **@CODE@**:  weak grade
  %^G3:0     !!≈ * **@CODE@**:  marks grade three for stems w/o Cgrad
  %^V2E2AA:0 !!≈ * **@CODE@**:  e to á in V2 (e.g. ILL.SG, DIM, 1/2-Sg)
  %^V2O2U:0 !!= @CODE@ * o to u in V2 (e.g. Ill.Sg, Dim, some N_ODD) etc.
  %^CDEL:0   !!≈ * **@CODE@**:  delete final consonant odd (biednag)
  %^VDEL:0   !!≈ * **@CODE@**:  delete final V2 vowel in compounds or gájk
  %^MON:0    !!≈ * **@CODE@**:  Monophthong in contracted stems
  %^UAUML:0    !!≈ * **@CODE@**:  uo to uä juolge / juällge
  %^IEUML:0    !!≈ * **@CODE@**:  ie to ä, gielbar / gällbara
  %^IUML:0   !!= @CODE@ * a to i, gallgat gillgin
  %^IJ:0     !!= @CODE@ * e to i in front of Plural j and Sg Com
  %^MONB4J:0 !!= @CODE@ * what is this?

! Literal quotes and angles must be escaped (cf morpheme boundaries further down):
 »7     ! »
 «7     ! «
 %[%>%] ! >
 %[%<%] ! <

! Morpheme boundaries:
 «    ! Derivational prefix
 »    ! Derivational suffix
 %<:0 ! Inflectional prefx
 %>:0 ! Inflectional suffix
 #    ! Word boundary for both lexicalised and dynamic compounds
 %^   ! (exceptional) soft hyphenation point

! End of alphabet definitions
;

Sets
  Letter = a b c d e f g h i ï j k l m n ŋ o p q r s t u v w x y z æ ä ø ö å á é í ó ú ' ;
  Cns    =   b c d   f g h     j k l m n ŋ   p q r s t   v w x   z                     '
      		 B C D   F G H     J K L M N Ŋ   P Q R S T   V W X   Z
             b2 d2 g2 t2 j2 ;
  Vow    = a       e       i ï             o           u       y   æ ä ø ö å á é í ó ú 
           A       E       I               O           U           Æ Ä Ø Ö Å Á É Í Ó Ú 
                           i2              %^O         u2            ä2 ;
  Dummy  =  %^WG %^G3 %^V2E2AA %^V2O2U %^CDEL %^VDEL %^MON %^UAUML %^IEUML %^IUML %^IJ ; 
  
Definitions

 G3 = [ b b n j |
        b b t [j|s] |
        d d n j |
        h t t [j|s] |
        j j s t |
        j j t s |
        j j t j |
        k k s [j|t] |
        k k t [j|s] |
        l d n j |
        l l t [j|s] |
        m m s [j|t] |
        n n j g |
        n n s [k|t] |
        n n t [j|s] |
        p p t [j|s] |
        r r s j |
        r r t [j|s] |
        s s j k |
        s s j m |
        v d n j |
        v v d n |
        v v s [j|k] |
        v v t [j|s] |

        f ':0 f |
        l ':0 l |
        m ':0 m |
        n ':0 n |
        ŋ ':0 ŋ |
        r ':0 r |
        s ':0 s |
        v ':0 v |
        s ':0 s j |

        b b [d|j|l|m|n|r] |
        b d n |
        d d [g|j|m|n|v] |
        f f [t|n] |
        g g d |
        g g ŋ |
        h [k k|p p|t t] |
        j j [b|d|g|k|l|p|r|s|t|v] |
        j b m |
        j d n | 
        k k [n|s|t] |
        l l [b|f|g|k|p|s|v] |
        l b m |
        l d j |
        l d n |
        l l [d|j|t] |
        m m [b|g|p|s] |
        n n [d|t] |
        ŋ ŋ [g|k|n] |
        p p [k|s|t] |
        r r [b|d|f|g|j|k|s|t|v] |
        r d j |
        s s [k|m|n|ŋ|p|t] |
        t t k |
        v v [d|g|j|k|l|r|s] |
        v d n ] ;

 noCG = [ 
        b b |
        d d |
        d t [j|s] |
        g g |
        r b m |
        r d n |
        r d n j |
        r g ŋ |
        l g ŋ ] ; ! for consonant centers with 3+ characters that do NOT alternate

 VHtrig = [
        e:i |
        i   |
        o:u |
        u
        ] ; !vowels that trigger VH (does NOT include i2 and u2)


 noVHtrig = [
        a:a | 
        á:á | 
        e:á | 
        e:e | 
        o:o | 
        y:y | 
        i2:i | 
        u2:u | 
        ä2:ä |
        %^O:o
        ] ; !vowels that do NOT trigger VH

 V2E2AAtrig = [
        v |
        j |
        t j
        ] ; !consonants (incl. digraphs) which can occur before ^V2E2AA

!! # Rules

Rules
 
 
!! ## Consonant gradation rules

"Consonant Gradation for htt(j|s):ht(j|s)"
!! **@RULENAME@**  
t:0 <=> Vow: h t _ ( j | s ) (Vow:+) (Cns:+) ( Dummy: ) %^WG: ;

"Consonant Gradation for hxx:hx"
!! **@RULENAME@**  
Cx:0 <=> Vow: h Cx _ (Vow:+) (Cns:+) ( Dummy: ) %^WG: ;
     where Cx in (p k) ; ! 't' covered by "Consonant Gradation for htt(j|s):ht(j|s)"

"Consonant Gradation for xdn(j):xn(j)"
!! **@RULENAME@**  
d:0 <=> Vow: j _ n (j) Vow: Cns:* ( Dummy: ) %^WG: ;
		Vow: v _ n (j) Vow: Cns:* ( Dummy: ) %^WG: ;
		Vow: l _ n (j) Vow: Cns:* ( Dummy: ) %^WG: ;
		Vow:   _ n  j  Vow: Cns:* ( Dummy: ) %^WG: ; ! ladnja<->lanja; ganjal<->gadnjala

!		[#|.#.] Cns:+ Vow: _ n j Vow: Cns: %^WG: ;

! !!€ jalgŋe^WGs>0
! !!€ jalgŋe0s00
! !!€ jalgŋá>v
! !!€ jalgŋá0v

! !!€ gållmes^WG>0
! !!€ gå0lmes^WG>0

! !!€ #ádnjas^WG#
! !!€ #á0njas0#
! !!€ #ádnjasa>v#
! !!€ #ádnjasa0v#
! !!€ #gadnjal^WG#
! !!€ #ga0njal0#
! !!€ #vu^Onjes^WG#
! !!€ #vuonjes0#
! !!€ #vu^Onjesa>v#
! !!€ #vuonjasa0v#


"Consonant Gradation for xx:x"
!! **@RULENAME@**  
Cx:0 <=> Vow: Cx _ Vow: Cns:* ( Dummy: ) %^WG: ;
     where Cx in ( t k m n ŋ s r l f j v ) ; !not bb, dd, gg, dtj, dts

     
"Consonant Gradation for xxy:xy"
!! **@RULENAME@**  
Cx:0 <=> Vow:+ Cx _ Cy Vow:+ Cns:* ( Dummy: ) %^WG: ;
     where Cx in ( b b b b b b d d d d d d f f g g g g j j j j j j j j j j k k k l l l l l l l l l m m m m n n n p r r r r r r r r r s s s s s s t t t t ŋ ŋ ŋ v v v v v v v )
           Cy in ( d j l m n r g j k m n v d n d l ŋ v b d g p k l m r s v n s v j b d f g k p s v b g p s d j s s b d f g j k p s v j k m n p v j k m s g k n d j k l r g s )  
          matched ;

! !!€ Ánnda^WG
! !!€ Án0da0
! 
! !!€ ánnde^WG
! !!€ án0de0

! !!€ stállo^WG
! !!€ stál0o0

! !!€ tjállag^WG^CDEL
! !!€ tjál0a000
! !!€ tjállaga%>v
! !!€ tjállaga0v

          
"Consonant Gradation for xxt(j|s):xt(j|s)" ! ájjte, gájjtsa
!! **@RULENAME@**  
Cx:0 <=> Vow:+ Cx _ t (j:*|s:*) Vow:+ Cns:* ( Dummy: ) %^WG: ;
     where Cx in ( b f j k l n p r s v ) ;

"Consonant Gradation for xxsj:xsj" ! ákksjo (kksj llsj mmsj ppsj rrsj vvsj) very similar to "Consonant Gradation for xxt(j|s):xt(j|s)"
!! **@RULENAME@**  
Cx:0 <=> Vow:+ Cx _ s j Vow:+ Cns:* ( Dummy: ) %^WG: ;
     where Cx in ( k l m p r v ) ;

! !!€ ájjte^WG
! !!€ áj0te0
! !!€ gájjtsa^WG
! !!€ gáj0tsa0
! !!€ gu^Olle^WG
! !!€ guol0e0
! !!€ gu^Olle^WG^IJ>jd
! !!€ gu0l0i000jd

"Consonant Gradation for xy:y"
!! **@RULENAME@**  
Cx:0 <=> Vow (Vow:) _ Cy Vow: Cns:* ( Dummy: ) %^WG: ; !Cx:0 <=> Vow: _ Cy (Vow:+) (Cns:+) ( Dummy: ) %^WG: ;
     where Cx in ( b d d g )
           Cy in ( m j n ŋ )  
          matched ;

"Delete h in hx:y" ! first step
!! **@RULENAME@**  
h:0  <=> Vow _ Cx: ( [ j | s ] ) Vow: Cns:* ( Dummy: ) %^WG: ;
    where Cx in (p t k) ;

"Intervocalic voiced plosives in hx:y" ! second step
!! **@RULENAME@**  
Cx:Cy <=> h: _ Vow: Cns:* ( Dummy: ) %^WG: ; ! Cx:Cy <=> h: _ ([Vow:+  (Cns:+)]) ( Dummy: ) %^WG: ;
    where Cx in (p t k)
          Cy in (b d g)
          matched ;

"Consonant Gradation for l/jbm:l/jm" 
!! **@RULENAME@**  
b:0 <=> Vow [l|j] _ m Vow: Cns:* ( Dummy: ) %^WG: ; ! vájbmo, njálbme

"Consonant Gradation for nnjg:njg" 
!! **@RULENAME@**  
n:0 <=> Vow n _ j g Vow: Cns:* ( Dummy: ) %^WG: ; ! skannjga, vannjgat

"Consonant Gradation for vgŋ:vŋ" 
!! **@RULENAME@**  
g:0 <=> Vow v _ n Vow: Cns:* ( Dummy: ) %^WG: ; ! ávgŋot (unsure about this word!)

"Consonant Gradation for rdj:rj" 
!! **@RULENAME@**  
d:0 <=> Vow r _ j Vow: Cns:* ( Dummy: ) %^WG: ; ! girdje<->girje (non-standard variant)


!! ## Other consonant rules 
! =====================

"Final C Deletion"
!! **@RULENAME@**  
Cx:0 <=> Vow: _ %^WG: %^CDEL: ;
	where Cx in ( b d g s h ) ; 

!!need to confirm this, then make sure it is functional first! all ODD-classes affected? what about  almatj? what about derived DIM-forms?
!"Final C Deletion for Essive Variant" !svájnas -> svájdnas- -> svájdnan (ess)
!!! __@RULENAME@__  
!Cx:0 <=> Vow: _ %^CDEL: ;
!	where Cx in ( g j l m n r s ) ; ! -tj!


"Final devoicing"
!! **@RULENAME@**  
Cx:Cy <=> _ (%^WG:) (%>:) # ;
    where Cx in ( b2 d2 g2 )
          Cy in ( p  t 	k )
          matched ; 

! !!€ gål'leg2
! !!€ gål0lek
! !!€ gål'leg2a
! !!€ gål0lega
! !!€ gu^Ossmag2^WG
! !!€ guos0mak0
! !!€ iejgád2
! !!€ iejgát
! !!€ gu^Ossmag2a>v
! !!€ guassmaga0v
! !!€ iejgád2i2>jd
! !!€ iejgád0i0jd
! !!€ mu^Ohttag^WG^CDEL
! !!€ muoht0a000
! !!€ mu^Ohttaga>v
! !!€ muahttaga0v

"Word Final Simplification in -st" !st->s (eg. elat) variation (descriptive, not normative)
!! **@RULENAME@**  
t2:0 <=> s _ .#. ;

! !!€ burist2
! !!€ buris0

"Word-final De-Affricatization for tj " !variation (eg. dim-suffix) (descriptive, not normative) (sj - tj word-finally)
!! **@RULENAME@**  
s:t <=> _ j2: .#. ;




! ===========          
!! ## Vowel rules 
! ===========          

!! ###  metaphony

"Default VH" !regressive vowel height assimilation in V1 with i/u in V2" ! á:ä a:i ä:e å:u ^O:0
!! **@RULENAME@**  
	Vx:Vy <=> [#|.#.] Cns:* _ Cns:+ VHtrig Cns:* Dummy:* Vow:* %>:0 ; 
    where Vx in ( á a ä å )
          Vy in ( ä i e u )
          matched ; 
!This requires every root that undergoes inflection to end with a morpheme boundary, even when no suffix exists; this prevents VH in non-final compound stems (since \# wouldn't work). !guolle<->gulij sábme<->sämij båtsoj<->buhtsu (last one w/o morpheme boundary!)

"Default VH for 4syllables" !(ignores first foot, otherwise same as above)
!! **@RULENAME@**  
	Vx:Vy <=> [#|.#.] Cns:* Vow:+ Cns:+ Vow:+ Cns:+ _ Cns:+ VHtrig Cns:* Dummy:* Vow:* %>:0 ; 
    where Vx in ( á a ä å )
          Vy in ( ä i e u )
          matched ; 
!ságastillin, vuosádillin  <- these have to work, too!!


! !!€ ságasti>t
! !!€ ságasti0t
! !!€ ságasta>v
! !!€ ságasta0v

! !!€ #ållol^WG#
! !!€ #ål0ol0#
! !!€ #ållol^V2O2Ua>v#
! !!€ #ullul0a0v#

! !!€ bárdne^WG>0#
! !!€ bárdne000#
! !!€ bárdne^WG^IJ>j#
! !!€ bärdni000j#

! !!€ bähkkum#ståhpo^UAUML#
! !!€ bähkkum#ståhpo0#
! !!€ bähkkum#ståhpo^WG>v#
! !!€ bähkkum#stå0bo00v#
! !!€ bähkku#ståhpo^UAUML#
! !!€ bähkku#ståhpo0#
! !!€ ståhpo^WG>v#
! !!€ stå0bo00v#

! !!€ bajeldis#gárrvo^UAUML#
! !!€ bajeldis#gárrvo0#
! !!€ bajeldis#gárrvo^WG>v#
! !!€ bajeldis#gár0vo00v#
! !!€ gárrvo^WG>v#
! !!€ gár0vo00v#

! !!€ åvvdå#giehtje#
! !!€ åvvdå#giehtje#
! !!€ åvvdå#giehtje^WG#
! !!€ åvvdå#gie0tje0#

! !!€ jievvja^G3
! !!€ j0ävvja0

! !!€ båhtsoj^WG.#.
! !!€ bå0tsoj0.#.
! !!€ båhtsu.#.
! !!€ buhtsu.#.
! !!€ båhtsu%>v.#.
! !!€ buhtsu0v.#.

! !!€ gu^Oksagis#
! !!€ guoksagis#

! ! acc.pl # to make sure VH still works otherwise
! !!€ sábme^WG^IJ>jd
! !!€ sä0mi000jd



"Default UA in G3" ! u^O:ua (juallgáj, luakkta); always with word-initial C
!! **@RULENAME@**  
%^O:a <=> Cns:* u _ G3 [:a|:o|:y|:ä|:å|:á] \%^WG: ; 
          Cns:* u _ noCG [:a|:o|:y|:ä|:å|:á] Cns:* %^G3:* %^UAUML: ;

! added Cns:* before G3/UAUML for odd verbs with C after V2
!!€ lu^Oddan^UAUMLi%>t
!!€ luaddan0i0t

"Special UÄ (VH) in G3" ! u^O:ua (juällge); always with word-initial C
!! **@RULENAME@**  
%^O:ä <=> Cns:* u _ G3 :e \%^WG: ; 
          Cns:* u _ Cns:+ :e (j:j) %^G3: ; ! duädde<->duodde, guäddej<->guäddeja
          Cns:* u _ noCG :e Cns: %^UAUML: ; ! tjuädtjelit (not tjuodtjelit)

!!€ gu^Odde^G3%>t
!!€ guädde00t

!!€ gu^Odde^G3%>t
!!€ guädde00t


! !!€ nju^Onnje^UAUML
! !!€ njuonnje0
! !!€ nju^Onnje^WG
! !!€ njuon0je0

"Special VH for u^O" ! u^O:u0
!! **@RULENAME@**  
%^O:0 <=> Cns:* u _ Cns:+ VHtrig Cns:* Dummy:* %>:* ; !juällge<->julgij

"Special VH for ie" ! ie:0e
!! **@RULENAME@**  
i:0 <=> Cns:* _ e: Cns:+ VHtrig Cns:* Dummy:* Vow:* %>: ; !iello->0elluj
		Cns:* _ e: G3 Vow: \%^WG: ; !biebbmo->b0äbbmo, häg'ga->hiegga, siebla<->säbblag-
		Cns:* _ e: Cns:+ Vow:+ %^G3: ; !biegga->b0ägga
		Cns:* _ e: noCG Vow: \%^WG: \%^WG: ; !bierdna->b0ärdna !double \%^WG: for gierbma<->gärbmaha  (ie: neither of the two elements after V2 can be ^WG)

!!€ sjievdnje^IJ%>s
!!€ sj0evdnji00s

!!€ hierrge^WG^IJ%>j
!!€ h0er0gi000j

!!€ hierrge%>j^V2E2AA
!!€ h0ärrgá0j0

		
"Ä in G3" ! ie:0ä (bägga, bävvde); not word-initial (thus not capitalized)
!! **@RULENAME@**  
e:ä <=> Cns:* i:0 _ G3 noVHtrig \%^WG: ; !biebbmo->b0äbbmo
        Cns:* i:0 _ Cns:+ noVHtrig %^G3: ; !biegga->b0ägga
		Cns:* i:0 _ noCG noVHtrig \%^WG: ; !bierdna->b0ärdna 
			!problem: lieggadit -> *läggadit (currently solved with quickNdirty manual addition of ^WG to affected lexemes, but this is not a good permanent solution)

"Ä in G3 capitalized" ! Ie:0Ä (Änntse->Ientse), eg. proper nouns with word-initial capitalization
!! **@RULENAME@**  
e:Ä <=> I:0 _ G3 noVHtrig \%^WG: ; !Änntse->Ientse


! !!€ tjalmed%>ibme^WG
! !!€ tjalmed0i0me0

! !!€ dibme^WG
! !!€ di0me0


! !!€ vu^Os'sa^WG
! !!€ vuos0sa0

! !!€ fiede^G3^UAUML>0
! !!€ f0äde00>0
! !!€ fiede^WG>v
! !!€ fiede0>v
! !!€ fiede^WG^IJ>jn
! !!€ f0edi00>jn

! !!€ gierbmah^WG^CDEL>0
! !!€ gierbma00000
! !!€ gierbmaha>v
! !!€ g0ärbmaha>v

! !!€ bierdne^WGs>0
! !!€ bierdne0s>0
! !!€ bierdná>v
! !!€ b0ärdná>v

! !!€ ku^Ordna^UAUML
! !!€ kuardna0
! !!€ ku^Ordna^WG
! !!€ kuordna0

! !!€ hieg'ga^UAUML
! !!€ h0äg0ga0
! !!€ hieg'ga^WG
! !!€ hieg0ga0
! !!€ hieg'ga>j^V2E2AA
! !!€ h0äg0ga0j0
! 
! !!€ biebbmo^UAUML
! !!€ b0äbbmo0
! !!€ biebbmo^WG
! !!€ bieb0mo0
! 
! !!€ bievvde^WG
! !!€ biev0de0
! !!€ bievvde^UAUML
! !!€ b0ävvde0
! !!€ biegga^G3^UAUML
! !!€ b0ägga00
! !!€ biegga^WG
! !!€ biegga0

! !!€ rággas
! !!€ rággas
! !!€ rággasa>v 
! !!€ rággasa0v 


"V2 E to I before j-suffixes" ! guolle -> gulij
!! **@RULENAME@**  
e:i <=> Vow:+ Cns:+ _ (Dummy:) %^IJ:0 ; !guolle<->gulij juällge<->julgij sábme<->sämij

"V2 E to Á" ! juällge->juallgáj, sábme->sábmáj, guolle->guolá>tj, vágge->vággáj, båhtet->bådáv
!! **@RULENAME@**  
e:á <=> Vow:+ Cns:+ _ (%^G3:0) (%^WG:0) %>:+ (V2E2AAtrig) %^V2E2AA:0 ;

!!€ båhte^WG%>v^V2E2AA
!!€ bå0dá00v0
!!€ båhte^WG%>^V2E2AA
!!€ bå0dá000
!!€ máhtte^WG%>v^V2E2AA
!!€ máht0á00v0
!!€ máhtte^WG%>^V2E2AA
!!€ máht0á000


"V2 E to Á before S or R" ! åres->årrás-, tjåskes->tjåsskas-, fieber fiebár-
!! **@RULENAME@**  
e:á <=> Vow:+ Cns:+ _ [s|r] %^V2E2AA:0 ;

"V2 O to U" ! rássjo->rässjuj, iello->elluj (variant), ålol<-ullul-
!! **@RULENAME@**  
o:u <=> Vow:+ Cns:+ _ (Dummy:*) %>:+ Cns:+ %^V2O2U:0 ;
		Vow:+ Cns:+ _ Cns:+ %^V2O2U:0 %>:* Vow:+ ; ! turns SUPERL -ubmos- into -ubmusa

! !!€ mu^Ohttag^WG^CDEL
! !!€ muoht0a000
! ! vielmes:vielbm N_CONTR_ESAA
! ! nom.sg
! !!€ vielbme^WGs
! !!€ viel0me0s
! ! acc.sg
! !!€ vielbmáv
! !!€ v0älbmáv

"Final V Deletion" !gåjjkebärrgo -> gåjjkbärrgo; gájka -> gájk
!! **@RULENAME@**  
Vx:0 <=> [#|.#.] (Cns:) Vow:+ Cns:+ _ (%^WG:) %^VDEL: # ;
	where Vx in ( e a ) ; !add more Vs as needed







! !nom.sg
! !!€ almatj
! !!€ almatj
! !nom.pl
! !!€ almatja
! !!€ almatja
! !acc.sg
! !!€ almatja%>v
! !!€ almatja0v
! !acc.pl
! !!€ almatji2%>j
! !!€ almatj0i0j
! 
! 
! !nom.sg
! !!€ biednag^WG^CDEL
! !!€ bie0na000
! !nom.pl
! !!€ biednaga
! !!€ biednaga
! !acc.sg
! !!€ biednaga%>v
! !!€ biednaga0v
! !acc.pl
! !!€ biednagi2%>j
! !!€ biednag0i0j
! 
! !!€ båhtsoj^WG
! !!€ bå0tsoj0
! !!€ båhtsu
! !!€ buhtsu
! !!€ båhtsu%>v
! !!€ buhtsu0v



! !!€ ålbmaj^WG
! !!€ ål0maj0
! !!€ ålbma>v
! !!€ ålbma0v
! !!€ ålbma>j
! !!€ ålbma0j

! !!€ ållol^WG
! !!€ ål0ol0
! !!€ ållol^V2O2Ua>v
! !!€ ullul0a0v
! !!€ ållol^V2O2Ui2>j
! !!€ ullul00i0j
! !!€ rássjo>j^V2O2U
! !!€ rässju0j0
! !!€ rássjo>j^V2E2AA
! !!€ rássjo0j0

! ! gen.pl
! !!€ ju^Ollge^WG^IJ>j
! !!€ ju0l0gi000j
! ! ill.sg
! !!€ ju^Ollge^UAUML>j^V2E2AA
! !!€ juallgá00j0
! ! nom.sg
! !!€ ju^Ollge^UAUML
! !!€ juällge0
! ! nom.pl
! !!€ ju^Ollge^WG
! !!€ juol0ge0

! !!€ sábme>j^V2E2AA
! !!€ sábmá0j0
! !!€ sábme^WG^IJ>j
! !!€ sä0mi000j
! !!€ iello^WG^IJ>j
! !!€ iel0o000j
! !!€ ållge^WG
! !!€ ål0ge0
! !!€ lu^Okkta^UAUML
! !!€ luakkta0
! !!€ bievvde^UAUML
! !!€ b0ävvde0

! !!€ vadnas^WG
! !!€ va0nas0
! !!€ gu^Ohtom
! !!€ guohtom
! !!€ buvdna^WG
! !!€ buv0na0
! ! fake word!
! !!€ skåldnjeg^WG^CDEL
! !!€ skål0nje000
! !!€ dållå^WG
! !!€ dål0å0
! ! suoŋŋer:suoŋŋer N_ODD ;
! !!€ suoŋŋer^WG
! !!€ suoŋ0er0
! ! tjála:tjállag BIENA "skrift" ;
! !!€ tjállag^WG^CDEL
! !!€ tjál0a000

! !!€ árrad2
! !!€ árrat
! !!€ árrad2e
! !!€ árrade
! !!€ unneb2
! !!€ unnep
! !!€ vájbmo^WG
! !!€ váj0mo0
! !!€ dåhpe^WG
! !!€ då0be0
! !!€ råhtje^WG
! !!€ rå0tje0
! !!€ råhtse^WG
! !!€ rå0tse0
! !!€ dåhpe^WG
! !!€ då0be0
! !!€ råhtje^WG
! !!€ rå0tje0
! !!€ riehpeh^WG^CDEL
! !!€ rie0be000
! !!€ biednag^WG^CDEL
! !!€ bie0na000
! !!€ lu^Okkta^WG
! !!€ luok0ta0
! ! fake word:
! !!€ lu^Okktag^WG^CDEL
! !!€ luok0ta000
! ! fake word:
! !!€ fábmog^WG^CDEL
! !!€ fá0mo000

!!€ a
!!€ b


