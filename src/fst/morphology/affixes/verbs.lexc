! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@uit.no or feedback@divvun.no


! ===== !
! Verbs !
! ===== !


!! # Even syllabled verbs

LEXICON V_EVEN_E !båhtet
+V:     V_EVEN_E_STR ;
+V:e^WG V_EVEN_E_WK ;

LEXICON V_EVEN_E_STR
+Ind+Prs+Sg3:á%> ENDLEX ;
!+Ind+Prs+Sg3+Err/Orth:a%> ENDLEX ; !just as an example, for possible later implementation
+Ind+Prs+Du1:i%>n ENDLEX ; 
+Ind+Prs+Du2:e%>behttin ENDLEX ;
+Ind+Prs+Du2:e%>bähtten ENDLEX ;
+Ind+Prs+Du3:e%> 3DUPRS ;
+Ind+Prs+Pl1:e%>p ENDLEX ;
+Ind+Prs+Pl2:e%>behttit ENDLEX ;
+Ind+Prs+Pl2:e%>bähttet ENDLEX ;
+Ind+Prs+Pl3:i%> ENDLEX ; 
+Ind+Prt+Sg1:i%>v ENDLEX ; 
+Ind+Prt+Sg2:i%> ENDLEX ; 
+Ind+Prt+Pl3:i%>n ENDLEX ; 

+Imprt+Du2:e%>n ENDLEX ; 
+Imprt+Pl2:i%>t ENDLEX ;

+Inf:e%>t ENDLEX ; 
+GerII:e%>min ENDLEX ; !aka: +Actio+Ess ?
+PrfPrc:á%>m ENDLEX ;
+PrsPrc:e%>j ENDLEX ; 
+Vsubst:e%>m V_SUBST_EVEN_s2 ; 

LEXICON V_EVEN_E_WK
+Ind+Prs+Sg1:%>v^V2E2AA ENDLEX ;
+Ind+Prs+Sg2:%>^V2E2AA ENDLEX ;
:^IJ%>j V_EVEN_E_WK_J ; !suffixes with -j-element that trigger e->i in V2

+Imprt+Sg2:%> ENDLEX ; 

+GerI:%>din ENDLEX ;  
+ConNeg:%> ENDLEX ; 
!+VGen:%> ENDLEX ; !keeping as comment for now, but replaced with verb.adv. in next line
+Der/VAdv+Adv:%>n ENDLEX ; !e.g. jåden "while migrating"
!+Der/VAdv+Adv+Use/NG:%> ENDLEX ; !main form according to Lehtiranta 1992 (cf. p. 151)
+Sup:%>^V2E2AAtjit ENDLEX ;
+Pot:%>tj^V2E2AA V_EVEN_POT ; 

LEXICON V_EVEN_E_WK_J
+Ind+Prt+Sg3: ENDLEX ;
+Ind+Prt+Du1:min ENDLEX ;
+Ind+Prt+Du2:din ENDLEX ;
+Ind+Prt+Du3: 3DUPRT ;
+Ind+Prt+Pl1:me ENDLEX ;
+Ind+Prt+Pl2:de ENDLEX ;
+Ind+Prt+Pl1+Use/NG:mä ENDLEX ;
+Ind+Prt+Pl2+Use/NG:dä ENDLEX ;


LEXICON V_EVEN_E_G3 !guäddet, däddet
+V:     V_EVEN_E_STR_G3 ; !special case to force G3 for correct output of ua/uä
+V:e^WG V_EVEN_E_WK ; !same as all V_EVEN_E

LEXICON V_EVEN_E_STR_G3 !
+Ind+Prs+Sg3:á^G3^UAUML%> ENDLEX ;
+Ind+Prs+Du1:i%>n ENDLEX ; 
+Ind+Prs+Du2:e^G3^UAUML%>behttin ENDLEX ;
+Ind+Prs+Du2:e^G3^UAUML%>bähtten ENDLEX ;
+Ind+Prs+Du3:e^G3^UAUML%> 3DUPRS ;
+Ind+Prs+Pl1:e^G3^UAUML%>p ENDLEX ;
+Ind+Prs+Pl2:e^G3^UAUML%>behttit ENDLEX ;
+Ind+Prs+Pl2:e^G3^UAUML%>bähttet ENDLEX ;
+Ind+Prs+Pl3:i%> ENDLEX ; 
+Ind+Prt+Sg1:i%>v ENDLEX ; 
+Ind+Prt+Sg2:i%> ENDLEX ; 
+Ind+Prt+Pl3:i%>n ENDLEX ; 

+Imprt+Du2:e^G3^UAUML%>n ENDLEX ; 
+Imprt+Pl2:i%>t ENDLEX ;

+Inf:e^G3^UAUML%>t ENDLEX ; 
+GerII:e^G3^UAUML%>min ENDLEX ; !aka: +Actio+Ess ?
+PrfPrc:á^G3^UAUML%>m ENDLEX ;
+PrsPrc:e^G3^UAUML%>j ENDLEX ; 
+Vsubst:e^G3^UAUML%>m V_SUBST_EVEN_s2 ; 





LEXICON V_EVEN_A !dahkat
+V:     V_EVEN_A_STR ;
+V:a^WG V_EVEN_A_WK ;

LEXICON V_EVEN_A_STR
:á 3SGPRS ;
:a 2DUPRS ;
:a 3DUPRS ;
:a 1PLPRS ;
:a 2PLPRS ;
:a INF ; 
:a GER_II ;
:a PRFPRC ;
:a V_SUBST ; 
: PRSPRC_A ; !vowel added later
:e 2DUIMP ; 
:i 1DUPRS ; 
:i 3PLPRS ; 
:i 1SGPRT ; 
:i 2SGPRT ; 
:i 3PLPRT ; 
:i 2PLIMP ;

LEXICON V_EVEN_A_WK
: 1SGPRS ;
: 2SGPRS ;
: 3SGPRT ;
: 1DUPRT ;
: 2DUPRT ;
: 3DUPRT ;
: 1PLPRT ;
: 2PLPRT ;

: 2SGIMP ; 

: GER_I ;  
: SUPINUM ;
: CONNEG ; 
: V_GEN ;

+Pot:%>tj V_EVEN_POT ;



LEXICON V_EVEN_A_G3 !snuaggat
+V:     V_EVEN_A_STR_G3 ; !special case to force G3 for correct output of ua/uä
+V:a^WG V_EVEN_A_WK ; !same as all V_EVEN_E

LEXICON V_EVEN_A_STR_G3
:á^G3^UAUML 3SGPRS ;
:a^G3^UAUML 2DUPRS ;
:a^G3^UAUML 3DUPRS ;
:a^G3^UAUML 1PLPRS ;
:a^G3^UAUML 2PLPRS ;
:a^G3^UAUML INF ; 
:a^G3^UAUML GER_II ;
:a^G3^UAUML PRFPRC ;
:a^G3^UAUML V_SUBST ; 
: PRSPRC_A_STR_G3 ; !vowel added later
:e^G3^UAUML 2DUIMP ; 
:i 1DUPRS ; 
:i 3PLPRS ; 
:i 1SGPRT ; 
:i 2SGPRT ; 
:i 3PLPRT ; 
:i 2PLIMP ;


LEXICON V_GILGIJ !special case for deviant gilgij-forms of gallgat
+V: V_GILGIJ_FORMS ;

LEXICON V_GILGIJ_FORMS !see above
+Ind+Prt+Sg3:%>j ENDLEX ;
+Ind+Prt+Du1:%>jmen ENDLEX ;
+Ind+Prt+Du2:%>jden ENDLEX ;
+Ind+Prt+Du3:%>j 3DUPRT ;
+Ind+Prt+Pl1:%>jme ENDLEX ;
+Ind+Prt+Pl2:%>jde ENDLEX ;
!+Ind+Prt+Pl1+Use/NG:%>jmä ENDLEX ;
!+Ind+Prt+Pl2+Use/NG:%>jdä ENDLEX ;


LEXICON V_EVEN_O !viessot, årrot
+V:     V_EVEN_O_STR ;
+V:o^WG V_EVEN_O_WK ;

LEXICON V_EVEN_O_STR
:o 2DUPRS ;
:o 3DUPRS ;
:o 1PLPRS ;
:o 2PLPRS ;
:o INF ; 
:o GER_II ;
:o V_SUBST ; 
:o 2DUIMP ; 
:u 3SGPRS ;
:u PRFPRC ;
:u 1DUPRS ; 
:u 3PLPRS ; 
:u 1SGPRT ; 
:u 2SGPRT ; 
:u 3PLPRT ; 
:u 2PLIMP ;
: PRSPRC_O ; !vowel added later

LEXICON V_EVEN_O_WK
: 1SGPRS ;
: 2SGPRS ;
: 3SGPRT ;
: 1DUPRT ;
: 2DUPRT ;
: 3DUPRT ;
: 1PLPRT ;
: 2PLPRT ;

: 2SGIMP ; 

: GER_I ;  
: SUPINUM ;
: CONNEG ; 
: V_GEN ;

+Pot:%>tj V_EVEN_POT ;



LEXICON V_EVEN_O_G3 !luabbot
+V:     V_EVEN_O_STR_G3 ; !special case to force G3 for correct output of ua/uä
+V:o^WG V_EVEN_O_WK ; !same as all V_EVEN_E

LEXICON V_EVEN_O_STR_G3 !
:u 3SGPRS ;
:u 1DUPRS ; 
:u 3PLPRS ; 
:u 1SGPRT ; 
:u 2SGPRT ; 
:u 3PLPRT ; 
:u 2PLIMP ;
:u^G3^UAUML PRFPRC ;
:o^G3^UAUML 2DUPRS ;
:o^G3^UAUML 3DUPRS ;
:o^G3^UAUML 1PLPRS ;
:o^G3^UAUML 2PLPRS ;
:o^G3^UAUML INF ; 
:o^G3^UAUML GER_II ;
:o^G3^UAUML V_SUBST ; 
:o^G3^UAUML 2DUIMP ; 
: PRSPRC_O_STR_G3 ; !vowel added later



LEXICON V_EVEN_Å !hållåt, bårråt
+V:     V_EVEN_Å_STR ;
+V:å^WG V_EVEN_Å_WK ;

LEXICON V_EVEN_Å_STR
:á 3SGPRS ;
:å 2DUPRS ;
:å 3DUPRS ;
:å 1PLPRS ;
:å 2PLPRS ;
:å 2DUIMP ; 
:å INF ; 
:å GER_II ;
:å PRFPRC ;
:å V_SUBST ; 
: PRSPRC_Å ; !vowel added later
:i 1DUPRS ; 
:i 3PLPRS ; 
:i 1SGPRT ; 
:i 2SGPRT ; 
:i 3PLPRT ; 
:i 2PLIMP ;

LEXICON V_EVEN_Å_WK
: 1SGPRS ;
: 2SGPRS ;
: 3SGPRT ;
: 1DUPRT ;
: 2DUPRT ;
: 3DUPRT ;
: 1PLPRT ;
: 2PLPRT ;

: 2SGIMP ; 

: GER_I ;  
: SUPINUM ;
: CONNEG ; 
: V_GEN ;

+Pot:%>tj V_EVEN_POT ;



! post-thematic-vowel suffixes for V_EVEN_ stems
! finite forms
LEXICON 1SGPRS
+Ind+Prs+Sg1:%>v ENDLEX;
LEXICON 2SGPRS ! just for systematicity
+Ind+Prs+Sg2:%> ENDLEX;
LEXICON 3SGPRS ! just for systematicity
+Ind+Prs+Sg3:%> ENDLEX;
LEXICON 1DUPRS
+Ind+Prs+Du1:%>n ENDLEX;
LEXICON 2DUPRS
+Ind+Prs+Du2:%>bähtten ENDLEX;
LEXICON 3DUPRS
+Ind+Prs+Du3:%>ban ENDLEX ;
+Ind+Prs+Du3+Use/NG:%>ba ENDLEX ;
LEXICON 1PLPRS
+Ind+Prs+Pl1:%>p ENDLEX;
LEXICON 2PLPRS
+Ind+Prs+Pl2:%>bihtit ENDLEX;
LEXICON 3PLPRS ! just for systematicity
+Ind+Prs+Pl3:%> ENDLEX;

LEXICON 1SGPRT
+Ind+Prt+Sg1:%>v ENDLEX;
LEXICON 2SGPRT ! just for systematicity
+Ind+Prt+Sg2:%> ENDLEX;
LEXICON 3SGPRT
+Ind+Prt+Sg3:%>j ENDLEX;
LEXICON 1DUPRT
+Ind+Prt+Du1:%>jmen ENDLEX;
+Ind+Prt+Du1+Use/NG:%>jme ENDLEX ;
LEXICON 2DUPRT
+Ind+Prt+Du2:%>jden ENDLEX;
+Ind+Prt+Du2+Use/NG:%>jde ENDLEX ;
LEXICON 3DUPRT
+Ind+Prt+Du3:%>jgan ENDLEX;
+Ind+Prt+Du3+Use/NG:%>jga ENDLEX ;
LEXICON 1PLPRT
+Ind+Prt+Pl1:%>jme ENDLEX;
LEXICON 2PLPRT
+Ind+Prt+Pl2:%>jde ENDLEX;
LEXICON 3PLPRT
+Ind+Prt+Pl3:%>n ENDLEX;

LEXICON 2SGIMP ! just for systematicity
+Imprt+Sg2:%> ENDLEX ; 
LEXICON 2DUIMP
+Imprt+Du2:%>n ENDLEX ; 
LEXICON 2PLIMP
+Imprt+Pl2:%>t ENDLEX ; 

!POT forms for V_EVEN_
LEXICON V_EVEN_POT !
+Sg1:a%>v ENDLEX ;
+Sg2:a%> ENDLEX ;
+Sg3:a%> ENDLEX ;
+Sg3+Use/NG:%> ENDLEX ;
+Du1:i%>n ENDLEX ; 
+Du2:ä%>hppen ENDLEX ;
+Du3:e%>ba ENDLEX ;
+Pl1:i%>p ENDLEX ;
+Pl2:i%>hppit ENDLEX ;
+Pl3:i%> ENDLEX ; 

! post-thematic-vowel suffixes for V_EVEN_; non-finite forms
LEXICON INF
+Inf:%>t ENDLEX ; 
LEXICON CONNEG ! just for systematicity
+ConNeg:%> ENDLEX ; 
LEXICON PRFPRC
+PrfPrc:%>m ENDLEX ;
LEXICON GER_I
+GerI:%>din ENDLEX ;  
LEXICON GER_II
+GerII:%>min ENDLEX ;
LEXICON SUPINUM
+Sup:%>tjit ENDLEX ;
LEXICON V_GEN
+VGen:%>n ENDLEX ;
+VGen+Use/NG:%> ENDLEX ; ! just for systematicity
!+Der/VAdv+Adv:%>n ENDLEX ; !alternative analysis for +VGen
LEXICON PRSPRC_A
+PrsPrc:a%>j ENDLEX ; 
+PrsPrc+Use/NG:e%> ENDLEX ; 
LEXICON PRSPRC_A_STR_G3
+PrsPrc:a^G3^UAUML%>j ENDLEX ; 
+PrsPrc+Use/NG:e^G3^UAUML%> ENDLEX ; 
LEXICON PRSPRC_O
+PrsPrc:o%>j ENDLEX ; 
+PrsPrc+Use/NG:e%> ENDLEX ; 
LEXICON PRSPRC_O_STR_G3
+PrsPrc:o^G3^UAUML%>j ENDLEX ; 
+PrsPrc+Use/NG:e^G3^UAUML%> ENDLEX ; 
LEXICON PRSPRC_Å
+PrsPrc:å%>j ENDLEX ; 
+PrsPrc+Use/NG:e%> ENDLEX ; 
LEXICON V_SUBST
+Vsubst:%>m V_SUBST_EVEN_s2 ; 






!! # Odd syllabled verbs

LEXICON V_ODD !ságastit
+V+Ind+Prs+Sg1:a%>v ENDLEX ;
+V+Ind+Prs+Sg2:a%> ENDLEX ;
+V+Ind+Prs+Sg3:a%> ENDLEX ;
+V+Ind+Prs+Sg3+Use/NG:%> ENDLEX ; !ságast, usjut
+V+Ind+Prs+Du1:i%>n ENDLEX ; 
+V+Ind+Prs+Du2:ä%>hppen ENDLEX ;
+V+Ind+Prs+Du3:e%>ba ENDLEX ;
+V+Ind+Prs+Pl1:i%>p ENDLEX ;
+V+Ind+Prs+Pl2:e%>hppit ENDLEX ;
+V+Ind+Prs+Pl3:i%> ENDLEX ; 

+V+Ind+Prt+Sg1:i%>v ENDLEX ; 
+V+Ind+Prt+Sg2:i%> ENDLEX ; 
+V+Ind+Prt+Sg3:i%>j ENDLEX ;
+V+Ind+Prt+Du1:i%>jmen ENDLEX ;
+V+Ind+Prt+Du2:i%>jden ENDLEX ;
+V+Ind+Prt+Du3:i%>jga ENDLEX ;
+V+Ind+Prt+Pl1:i%>jme ENDLEX ;
+V+Ind+Prt+Pl2:i%>jde ENDLEX ;
+V+Ind+Prt+Pl3:i%>n ENDLEX ; 

+V+Imprt+Sg2:e%> ENDLEX ; 
+V+Imprt+Sg2+Use/NG:ä%> ENDLEX ; 
+V+Imprt+Sg2+Use/NG:i%> ENDLEX ; 
+V+Imprt+Du2:e%>hten ENDLEX ; 
+V+Imprt+Pl2:i%>httit ENDLEX ;

+V+Inf:i%>t ENDLEX ; 
+V+GerII:i%>men ENDLEX ;
+V+PrfPrc:a%>m ENDLEX ;
+V+ConNeg:e%> ENDLEX ; 
+V+PrsPrc:ä%>ddje ENDLEX ; 
+V+Vsubst:i%> V_SUBST_ODD_s2 ; 
!+V+GerI:a%>din ENDLEX ;  
!+V+Sup:a%>tjit ENDLEX ; ! even Lehtiranta's paradigm 1992:153 doesn't have this
!+VGen:%> ENDLEX ; !keeping as comment for now, but replaced with verb.adv. in next line
!+Der/VAdv+Adv:%>n ENDLEX ; !

+V+Pot:a%>tj V_ODD_POT ;


LEXICON V_ODD_UA ! luaddanit (not luoddanit! class for ua/uä + noCG CCenter)
:^UAUML V_ODD ;


LEXICON V_ODD_UT ! vierábut, giehpebut (Adj+Comp+Inf) !most of these suffixes should be checked!
+V+Ind+Prs+Sg1:u%>v ENDLEX ;
+V+Ind+Prs+Sg2:u%> ENDLEX ;
+V+Ind+Prs+Sg3:u%> ENDLEX ;
+V+Ind+Prs+Sg3+Use/NG:%> ENDLEX ;
+V+Ind+Prs+Du1:i%>n ENDLEX ; 
+V+Ind+Prs+Du2:ä%>hppen ENDLEX ;
+V+Ind+Prs+Du3:e%>ba ENDLEX ;
+V+Ind+Prs+Pl1:i%>p ENDLEX ;
+V+Ind+Prs+Pl2:i%>hppit ENDLEX ;
+V+Ind+Prs+Pl3:i%> ENDLEX ; 

+V+Ind+Prt+Sg1:i%>v ENDLEX ; 
+V+Ind+Prt+Sg2:i%> ENDLEX ; 
+V+Ind+Prt+Sg3:i%>j ENDLEX ;
+V+Ind+Prt+Du1:i%>jmen ENDLEX ;
+V+Ind+Prt+Du2:i%>jden ENDLEX ;
+V+Ind+Prt+Du3:i%>jga ENDLEX ;
+V+Ind+Prt+Pl1:i%>jme ENDLEX ;
+V+Ind+Prt+Pl2:i%>jde ENDLEX ;
+V+Ind+Prt+Pl3:i%>n ENDLEX ; 

+V+Imprt+Sg2:e%> ENDLEX ; 
+V+Imprt+Du2:e%>hten ENDLEX ; 
+V+Imprt+Pl2:i%>httit ENDLEX ;

+V+Inf:u%>t ENDLEX ; 
+V+GerII:i%>men ENDLEX ;
+V+PrfPrc:u%>m ENDLEX ;
+V+ConNeg:e%> ENDLEX ; 
+V+PrsPrc:ä%>ddje ENDLEX ; 
+V+Vsubst:u%> V_SUBST_ODD_s2 ; 
!+V+GerI:a%>din ENDLEX ;  
!+V+Sup:a%>tjit ENDLEX ;
!+VGen:%> ENDLEX ; !keeping as comment for now, but replaced with verb.adv. in next line
!+Der/VAdv+Adv:%>n ENDLEX ; !



!V_ODD POT forms -- check!
+V+Pot:a%>tj V_ODD_POT ;

!POT forms for V_ODD
LEXICON V_ODD_POT !
!+Sg1:a%>v ENDLEX ;
!+Sg2:a%> ENDLEX ;
+Sg3:a%> ENDLEX ;
+Sg3+Use/NG:%> ENDLEX ;
!+Du1:i%>n ENDLEX ; 
!+Du2:ä%>hppen ENDLEX ;
!+Du3:e%>ba ENDLEX ;
!+Pl1:i%>p ENDLEX ;
!+Pl2:i%>hppit ENDLEX ;
!+Pl3:i%> ENDLEX ; 





!! # contracted verbs

LEXICON V_CONTR !välldut, tjerrut
+V+Ind+Prs+Sg1:%>v ENDLEX ;
+V+Ind+Prs+Sg2:%> ENDLEX ;
+V+Ind+Prs+Sg3:%>ja ENDLEX ;
+V+Ind+Prs+Du1:%>jin ENDLEX ; 
+V+Ind+Prs+Du2:%>bähttin ENDLEX ;
+V+Ind+Prs+Du3:%>ba ENDLEX ;
+V+Ind+Prs+Pl1:%>p ENDLEX ;
+V+Ind+Prs+Pl2:%>bihttit ENDLEX ;
+V+Ind+Prs+Pl3:%>ji ENDLEX ; 

+V+Ind+Prt+Sg1:%>jiv ENDLEX ; 
+V+Ind+Prt+Sg2:%>ji ENDLEX ; 
+V+Ind+Prt+Sg3:%>j ENDLEX ;
+V+Ind+Prt+Sg3+Use/NG:%>jij ENDLEX ;
+V+Ind+Prt+Du1:%>jmin ENDLEX ;
+V+Ind+Prt+Du2:%>jdin ENDLEX ;
+V+Ind+Prt+Du3:%>j 3DUPRT ;
+V+Ind+Prt+Pl1:%>jme ENDLEX ;
+V+Ind+Prt+Pl2:%>jde ENDLEX ;
+V+Ind+Prt+Pl1+Use/NG:%>jmä ENDLEX ;
+V+Ind+Prt+Pl2+Use/NG:%>jdä ENDLEX ;
+V+Ind+Prt+Pl3:%>jin ENDLEX ; 

+V+Imprt+Sg2:%> ENDLEX ; 
!+V+Imprt+Du2:%>hten ENDLEX ; 
!+V+Imprt+Pl2:%>httit ENDLEX ;

+V+Inf:%>t ENDLEX ; 
+V+GerII:%>min ENDLEX ;
+V+PrfPrc:%>m ENDLEX ;
+V+ConNeg:%> ENDLEX ; 
+V+PrsPrc:%>jäddje ENDLEX ; 
+V+Vsubst:%>m V_SUBST_EVEN_s2 ; !cf. sujjtim (sje19360100a.001)
!+V+GerI:%>din ENDLEX ;  
+V+Sup:%>tjit ENDLEX ;
!+VGen:%> ENDLEX ; !keeping as comment for now, but replaced with verb.adv. in next line
!+Der/VAdv+Adv:%>n ENDLEX ; !

!V_CONTR POT forms -- check!
+V+Pot:%>tj V_CONTR_POT ;

!POT forms for V_CONTR
LEXICON V_CONTR_POT !
+Sg1:a%>v ENDLEX ;
+Sg2:a%> ENDLEX ;
+Sg3:a%> ENDLEX ;
+Sg3+Use/NG:%> ENDLEX ;
+Du1:i%>n ENDLEX ; 
+Du2:ä%>hppen ENDLEX ;
+Du3:e%>ba ENDLEX ;
+Pl1:i%>p ENDLEX ;
+Pl2:i%>hppit ENDLEX ;
+Pl3:i%> ENDLEX ; 


LEXICON V_CONTR_UVVUT ! variation in derived (passive) stems -uvºvut (~uvvat)
:u V_CONTR ; !standard inflectional class: tjalmeduvºvut -j-
+Use/NG: V_EVEN_A ; !variant inflectional class: tjalmeduvvat



!! # Auxiliaries

LEXICON COPULA !
lä: LE "copula/auxiliary verb" ; !!= * @CODE@ 

LEXICON LE !!= * @CODE@ 
+V+Ind+Prs: LEPRS ; 
+V+Ind+Prt: LEPRT ; 
+V+Ind+Pot: LEPOT ;
+V+PrfPrc:lam K ; ! lä (conneg) + *am (prfprc allomorph of *lä*)
+V+Inf:årrot K ; !infinitive
+V+ConNeg:lä K ; !connegative

LEXICON LEPRS !!= * @CODE@ 
+Sg1:läv ENDLEX ; 
+Sg1+Use/NG:lev ENDLEX ; 
+Sg2:lä ENDLEX ;  
+Sg2+Use/NG:le ENDLEX ;  
+Sg2:'l ENDLEX ; !enclitic (only attaches to vowel-final preceding word) !apostrophe tokenized separately, which is not ideal
+Sg3:lä ENDLEX ; 
+Sg3:'l ENDLEX ; !enclitic (only attaches to vowel-final preceding word) !apostrophe tokenized separately, which is not ideal
!+Sg3:le ENDLEX ; 
+Du1:lin ENDLEX ; 
!+Du1:len ENDLEX ; 
+Du2:lihpen ENDLEX ; 
+Du2:lähppen ENDLEX ;  
+Du3:läba ENDLEX ; 
+Du3+Use/NG:läbá ENDLEX ; 
+Pl1:läp ENDLEX ; 
+Pl1:lip ENDLEX ; 
!+Pl1:lep ENDLEX ; 
+Pl2:lihpit ENDLEX ; 
+Pl2:lehpit ENDLEX ; 
+Pl3:lä ENDLEX ;
+Pl3:l ENDLEX ; !enclitic (only attaches to vowel-final preceding word)

LEXICON LEPRT !!= * @CODE@ 
+Sg1:lijjiv    ENDLEX ; 
+Sg1:lidjiv    ENDLEX ;  
+Sg1:liv    ENDLEX ;  
+Sg2:lijji    ENDLEX ; 
+Sg2:lidji    ENDLEX ;  
+Sg3:lij    ENDLEX ; 
+Du1:lijmen ENDLEX ; 
+Du1:liejme ENDLEX ; 
+Du2:lijden ENDLEX ;
+Du2:liejde ENDLEX ; 
+Du3:lij 3DUPRT ;
+Du3:liej 3DUPRT ; 
+Pl1:lijme ENDLEX ; 
+Pl1:liejme ENDLEX ; 
+Pl2:lijde ENDLEX ;
+Pl2:liejde ENDLEX ; 
+Pl3:lidjin    ENDLEX ;  
+Pl3:lijjin    ENDLEX ; 
+Pl3:lin    ENDLEX ; 

LEXICON LEPOT
+Sg1:lidtjav ENDLEX ; 
+Sg2:lidtja ENDLEX ; 
+Sg3:litj ENDLEX ; 
+Du1:lidtjin ENDLEX ; 
+Du2:lidtjabehttin ENDLEX ; 
+Du3:lidtjaba ENDLEX ; 
+Du3:lidtjaban ENDLEX ; 
+Pl1:lidtjap ENDLEX ;  
+Pl2:lidtjabihttit ENDLEX ; 
+Pl3:lidtji ENDLEX ; 


!LEXICON ATJA !!= * @CODE@ !!! originally in ACSj:  ATJA "skola" ;  !!= * @CODE@  ???
!atja+V+Ind+Prs: ATJAPRS ; 
!atja+V+Ind+Prt: ATJAPRT ; 
!
!
!LEXICON ATJAPRS !!= * @CODE@ 
!+Sg1:atjav      ENDLEX ;  !!= * @CODE@ 
!+Sg2:atjah      ENDLEX ; 
!+Sg3:atja       ENDLEX ; 
!+Du1:itjin      ENDLEX ; 
!+Du2:atjabihten ENDLEX ; 
!+Du3:atjaba     ENDLEX ; 
!+Pl1:atjap      ENDLEX ; 
!+Pl2:atjabihtit ENDLEX ; 
!+Pl3:itjih      ENDLEX ; 
!
!LEXICON ATJAPRT !!= * @CODE@ 
!+Sg1:itjiv    ENDLEX ;  !!= * @CODE@ 
!+Sg2:itjih    ENDLEX ; 
!+Sg3:atjaj    ENDLEX ; 
!+Du1:atjajmen ENDLEX ; 
!+Du2:atjajden ENDLEX ; 
!+Du3:atjajgan ENDLEX ; 
!+Pl1:atjajmeh ENDLEX ; 
!+Pl2:atjajdeh ENDLEX ; 
!+Pl3:itjin    ENDLEX ; 

LEXICON IJ !!= * @CODE@ 
!+V+Ind+Prs: IJPRS ;
!+V+Ind+Prt: IJPRT ;
!+V+Imprt: IJIMP ;
+V+Neg+Prs: IJPRS ;
+V+Neg+Prt: IJPRT ;
+V+Neg+Imprt: IJIMP ;

!cf. +V+PrfPrc:lam ENDLEX ; ! lä (BE.conneg) + *am (prfprc allomorph of *lä*)
!check tags!
+V+Neg+Prs+Pl3#lä+ConNeg#lä+PrfPrc:iellam K ; ! = e+lam
+V+Neg+Prs+Pl3#lä+ConNeg#lä+PrfPrc:ällam K ; ! = e+lam
+V+Neg+Prs+Sg3#lä+ConNeg#lä+PrfPrc:idtjam K ; ! = idtjij+lam

LEXICON IJPRS !!= * @CODE@ 
+Sg1:iv    K ;  !!= * @CODE@ 
+Sg2:i    K ; 
+Sg3:ij    K ; 
+Sg3+Use/NG:ijja    K ; ! long form for short answers, cf. sje20150329b.138, pit080924.487/.489
+Du1:ien K ; 
+Du2:iehpen K ;   
+Du3:iebá K ; 
+Pl1:iep K ; 
+Pl1+Use/NG:iehpe K ; 
+Pl2:ihpit K ;  
+Pl3:ie   K ;
+Pl3+Use/NG:e   K ;
+Pl3#lä+V+ConNeg:ällä K ; ! = e+lä

LEXICON IJPRT !!= * @CODE@ 
+Sg1:idtjiv    K ;  !!= * @CODE@ 
+Sg2:idtji    K ; 
+Sg3:idtjij    K ; 
+Du1+Use/NG:iejmen K ; 
+Du1:idtjijmen K ; 
+Du2+Use/NG:iejden  K ; 
+Du2:idtjijden K ; 
+Du3+Use/NG:iejgan 3DUPRT ; 
+Du3:idtjijga 3DUPRT ; 
+Pl1+Use/NG:iejme K ; 
+Pl1:idtjijme K ; 
+Pl2+Use/NG:iejde K ; 
+Pl2:idtjijde K ; 
+Pl3:idtjin    K ;


LEXICON IJIMP !!= * @CODE@ 
+Sg2:iele  K ; 
+Sg2+Use/NG:ilu  K ; 
+Du2:iellen K ; 
+Du2+Use/NG:illun K ; 
+Pl2:iellet  K ;
+Pl2+Use/NG:illut  K ;



! +Vsubst / actio initially (based somewhat on saL for now)
LEXICON V_SUBST_EVEN_s2 !båhtem, båhtema, båhtemin, båhtemijn
+Nom: ENDLEX ;
+Cmp/SgNom: R ;
+Gen:a ENDLEX ;
!+Ess:in ENDLEX ;
+Com:ijn ENDLEX ; !cf. pit090609a.164 gullimijn "with the fishing"

LEXICON V_SUBST_ODD_s2 !málestibme, málestime, málestimen, málestimijn
+Nom:bme ENDLEX ; !cf. no. 5234 ságastibme "conversation"
+Nom+Use/NG:m ENDLEX ; !cf. sje20231218a.079 fielastim "travel/journey"
!+Cmp/SgNom: R ;
+Gen:me ENDLEX ;
!+Ess:men ENDLEX ;
!+Com:mijn ENDLEX ;
