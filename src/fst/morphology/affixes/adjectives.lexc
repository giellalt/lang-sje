! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@uit.no or feedback@divvun.no


! ========== !
!! # Adjectives !
! ========== !

!
! Josh's testing:
!
! positive form rules (based on PRED.SG as basic form)
LEXICON A_BLAAVVAT ! blävvis/blávvat/blávvada/blávvadup/blávvadumos/-bmus-
+A+Attr:i%>s # ;
+A:ad2 A_BLAAVVAT_s2 ; 


LEXICON A_JAORRBAOT ! jurrbut/jårrbåt/jårrbåda/??
+A+Attr:i%>s # ;
+A:åd2 A_BLAAVVAT_s2 ; 

LEXICON A_BLAAVVAT_s2 
+Sg+Nom:%> K ; 
+Pl+Nom:%>a K ; 
+Comp: A_EVEN_COMP ;
+Superl: A_EVEN_SUP ;


LEXICON A_AMAS ! amas/amas/abmasa/abmasup
+A+Attr:^WG%> # ;
+A: A_AMAS_s2 ; 

LEXICON A_AMAS_s2 
N_ODD_WG_s2 ;
+Comp: A_EVEN_COMP ;
+Superl: A_EVEN_SUP ;


LEXICON A_AARVAS ! árvas/árvas/árrvasa/árvasup/árvasumos/árvasubmusa
+A+Attr:^WG%> # ;
+A: A_AARVAS_s2 ; 

LEXICON A_AARVAS_s2 
+Sg+Nom:^WG%> K ; 
+Pl+Nom:%>a K ; 
+Comp:^WG A_EVEN_COMP ;
+Superl:^WG A_EVEN_SUP ;


LEXICON A_BIVVAL ! bivvalis/bivval/bivvala/bivvalup/bivvalumos/-bmus-
+A+Attr:%>is # ;
+A: A_BIVVAL_s2 ; 

LEXICON A_BIVVAL_s2 
+Sg+Nom:%> K ; 
+Pl+Nom:%>a K ; 
+Comp: A_EVEN_COMP ;
+Superl: A_EVEN_SUP ;


LEXICON A_TJAOSKES ! tjåskes/tjåskes/tjåsskása/tjåsskásup/tjåsskásumos/-bmus-
+A+Attr:^WG%> # ;
+A: A_TJAOSKES_s2 ; 

LEXICON A_TJAOSKES_s2 
+Sg+Nom:^WG%> K ; 
+Pl+Nom:^V2E2AA%>a K ; 
+Comp:^V2E2AA A_EVEN_COMP ;
+Superl:^V2E2AA A_EVEN_SUP ;


LEXICON A_VUOJNOS ! vuojnos/vuojnos/vujdnusa/...?
+A+Attr:^WG%> # ;
+A: A_VUOJNOS_s2 ; 

LEXICON A_VUOJNOS_s2 ! first ADJ lexicon to link to case/number endings, for a start...
+Sg+Nom:^WG%> K ;
:^V2O2U N_ODD_X ;
+Comp:^V2O2U A_EVEN_COMP ;
+Superl:^V2O2U A_EVEN_SUP ;


LEXICON A_ASEDIS ! asedis / asedis / asedasa / asedup / asedumos/-bmus-
+A+Attr:is%> # ;
+A: A_ASEDIS_s2 ; 

LEXICON A_ASEDIS_s2 
+Sg+Nom:is%> K ; 
+Pl+Nom:as%>a K ; 
+Comp: A_EVEN_COMP ;
+Superl: A_EVEN_SUP ;


!check difference between A_BUOSSJE and A_TJAEHPPE (in SUPERL-j)
LEXICON A_TJAEHPPE ! tjiehpes/tjähppe/tjiehpe/tjiehpep/tjiehpemus-
+A+Attr:^WG%>s # ;
+A: A_TJAEHPPE_s2 ; 

LEXICON A_TJAEHPPE_s2 
: EVENCASE ;  ! Sg Nom, Ess
: N_EVEN_ILL ;  ! Sg Ill
:^WG N_EVEN_WK ; ! CG here
!+Sg+Nom:%> K ; !old
!+Pl+Nom:^WG%> K ; !old
+Comp:^WG A_ODD_COMP ;
+Superl:^WG A_ODD_SUP ;


LEXICON A_ÅNEGIS ! ånegis/ådne/åne/ånep/ånemus-
+A+Attr:^WG%>gis # ;
+A: A_ÅNEGIS_s2 ; 

LEXICON A_ÅNEGIS_s2 
+Sg+Nom:%> K ; 
+Pl+Nom:^WG%> K ; 
+Comp:^WG A_ODD_COMP ;
+Superl:^WG A_ODD_SUP ;


LEXICON A_AALLKE ! állkes/állke/állke/állkep/állkemus- (COMP-variant állkáp!)
+A+Attr:%>s # ;
+A: A_AALLKE_s2 ; 

LEXICON A_AALLKE_s2 
+Sg+Nom:%> K ; 
+Pl+Nom:%> K ; 
+Comp: A_ODD_COMP ;
+Comp+Use/NG: A_ODD_COMP_V2E2AA ;
+Superl: A_ODD_SUP ;


LEXICON A_BAONNDAA ! båndá/bånndá/båndá/båndáp/båndájmus- (same as tjähppe except for -j- in SUPERL)
+A+Attr:^WG%>s # ;
+A: A_BAONNDAA_s2 ; 

LEXICON A_BAONNDAA_s2 
+Sg+Nom:%> K ; 
+Pl+Nom:^WG%> K ; 
+Comp:^WG A_ODD_COMP ;
+Superl:^WG A_ODD_jSUP ;


LEXICON A_BAAHKAS ! báhka/báhkas/báhkkasa/báhkkasup?/báhkkasumos?/-bmus-?
+A+Attr:^WG%> # ;
+A: A_BAAHKAS_s2 ; 

LEXICON A_BAAHKAS_s2 
+Sg+Nom:s^WG%> K ; 
!+Pl+Nom:s%>a K ; 
:s%> N_ODD_X ;
+Comp:s A_EVEN_COMP ;
+Superl:s A_EVEN_SUP ;


LEXICON A_TJAABBE ! tjábba/tjábbe/tjábbáp-a/u/tjäbbijmus-
+A+Attr:a%> # ;
+A: A_TJAABBE_s2 ; 

LEXICON A_TJAABBE_s2 
+Sg+Nom:e%> K ; 
+Pl+Nom:e%> K ; 
+Comp:á A_ODD_COMP ;
+Comp+Use/NG:á A_ODD_COMP_a ;
+Superl:á A_ODD_jSUP ;
+Superl+Use/NG:i A_ODD_jSUP ;

LEXICON A_UNNE ! unna/unne/unnep/unnemus-
+A+Attr:a%> # ;
+A: A_UNNE_s2 ; 

LEXICON A_UNNE_s2 
+Sg+Nom:e%> K ; 
+Pl+Nom:e%> K ; 
+Comp:e A_ODD_COMP ;
+Superl:e A_ODD_SUP ;
!+Superl+Use/NG:i A_ODD_jSUP ;

LEXICON A_SMAAVVE ! smáva/smávve/smáve/smávep-a/smávájmus-
+A+Attr:a^WG%> # ;
+A: A_SMAAVVE_s2 ; 

LEXICON A_SMAAVVE_s2 
+Sg+Nom:e%> K ; 
+Pl+Nom:e^WG%> K ; 
+Comp:e^WG A_ODD_COMP ;
+Superl:á A_ODD_jSUP ;




LEXICON A_AAJDNA ! ájdna/ájdna/?/?-
+A+Attr:%> # ;
+A: A_AAJDNA_s2 ; 

LEXICON A_AAJDNA_s2 
+Sg+Nom:%> K ; 
+Pl+Nom:%> K ; 
!+Comp:e A_ODD_COMP ;
!+Superl:e A_ODD_SUP ;


!check difference between A_BUOSSJE and A_TJAEHPPE (in SUPERL-j)
LEXICON A_BUOSSJE ! buosjes/buossje/buosje/buosjep/busjijmus-
+A+Attr:^WG%>s # ;
+A: A_BUOSSJE_s2 ; 

LEXICON A_BUOSSJE_s2 
: EVENCASE ;
: N_EVEN_ILL ;
:^WG N_EVEN_WK ;
!+Sg+Nom:%> K ; 
!+Pl+Nom:^WG%> K ; 
+Comp:^WG A_ODD_COMP ;
+Superl:^WG^IJ A_ODD_jSUP ;


!main difference to A_GALMAS is -jSUPERL
LEXICON A_DIMES ! dibma/dimes/dibmása/dibmáp/dibmájmus-
+A+Attr:a%> # ;
+A:e A_DIMES_s2 ; 

LEXICON A_DIMES_s2 
+Sg+Nom:^WG%>s K ; 
:%>^V2E2AAs N_ODD_X ;
!+Pl+Nom:%>^V2E2AAsa K ; 
+Comp: A_ODD_COMP_V2E2AA ;
+Superl: A_ODD_jSUP_V2E2AA ;


!main difference to A_DIMES is no -jSUPERL
LEXICON A_GALMAS ! galbma/galmas/galbmasa/galbmasup/galbmasumos-
+A+Attr:%> # ;
+A: A_GALMAS_s2 ; 

LEXICON A_GALMAS_s2 
+Sg+Nom:^WG%>s K ; 
+Pl+Nom:%>sa K ; 
+Comp:s A_EVEN_COMP ;
+Superl:s A_EVEN_SUP ;



LEXICON A_xDIBME ! tjalmedis/tjalmedibme/tjalmedime/?/?-
+A+Attr:%>is # ;
+A:%>ibme A_xDIBME_s2 ; 

LEXICON A_xDIBME_s2 
! copy of N_EVEN without +N and DIM-derivation:
: EVENCASE ;  ! Sg Nom, Ess
: N_EVEN_ILL ;
:^WG N_EVEN_WK ; ! CG here
!+Comp: A_ODD_COMP ;
!+Superl: A_ODD_SUP ;


LEXICON A_BAOSSTOT ! båsto/båsstot/båsstoda/?p/?mos
+A+Attr:^WG%> # ;
+A:d2 A_BAOSSTOT_s2 ; 

LEXICON A_BAOSSTOT_s2 
+Sg+Nom:%> K ; 
+Pl+Nom:%>a K ; 
+Comp: A_EVEN_COMP ;
+Superl: A_EVEN_SUP ;


LEXICON A_NUORRA ! nuorra/nuorra/nuora/nuorap-a/nuoramus (?nuorasup-a/?nuorasumos)
+A+Attr:%> # ;
+A: A_NUORRA_s2 ; 

LEXICON A_NUORRA_s2 
: EVENCASE ;
: N_EVEN_ILL ;
:^WG N_EVEN_WK ;

+Comp:^WG A_ODD_COMP ;
+Comp+Use/NG:^WG A_ODD_COMP_a ;
+Superl:^WG A_ODD_SUP ;


LEXICON A_STUORES ! stuorra (stuor)/stuores/stuorrá/stuoráp/stuoremus
+A+Attr:a%> # ;
+A: A_STUORES_s2 ; 

LEXICON A_STUORx ! stuor (variant of stuorra (ATTR))
+A+Attr:%> # ;

LEXICON A_STUORES_s2 
+Sg+Nom:e^WG%>s K ; 
+Pl+Nom:%>á K ; 
+Comp:á^WG A_ODD_COMP ;
+Superl:e^WG A_ODD_SUP ;


LEXICON A_FIEROK ! fierok/fierok/fieroga/fierogup/fierogumus/-bmusa
+A+Attr:%> # ;
+A: A_FIEROK_s2 ; 

LEXICON A_FIEROK_s2 
+Sg+Nom:%> K ; 
:%> N_ODD_X ;
+Comp:^WG A_EVEN_COMP ;
+Superl:^WG A_EVEN_SUP ;


LEXICON A_IETJAA ! ietjá/ietjá/iehtjáda/..?
+A+Attr:^WG%> # ;
+A: A_IETJAA_s2 ; 

LEXICON A_IETJAA_s2 
+Sg+Nom:^WG%> K ; 
!+Pl+Nom:d%>a K ; 
:d%> N_ODD_X ;
+Comp:^WG A_EVEN_COMP ;
+Superl:^WG A_EVEN_SUP ;


LEXICON A_DUALLBAT ! duallba/duallbat/duallbada/?duallbadup/?duallbadumus/?-bmusa
+A+Attr:%> # ;
+A: A_DUALLBAT_s2 ; 

LEXICON A_DUALLBAT_s2 
+Sg+Nom:d2%> K ; 
+Pl+Nom:d2%>a K ; 
+Comp:d2 A_EVEN_COMP ;
+Superl:d2 A_EVEN_SUP ;


LEXICON A_ELULATJ ! elulatj/elulatj/elulatja/??elulatjap/??elulatjamus-a
+A+Attr:tj%> # ;
+A: A_ELULATJ_s2 ; 

LEXICON A_ELULATJ_s2 
+Sg+Nom:tj K ; 
:tja N_EVEN4_DEFAULT ; 
+Use/NG:dtja N_EVEN4_DEFAULT ; 
+Comp:tja A_ODD_COMP ;
+Comp+Use/NG:dtja A_ODD_COMP ;
+Superl:tja A_ODD_SUP ;
+Superl+Use/NG:dtja A_ODD_SUP ;


LEXICON A_IEDNAK ! iedna/iednak/iednaga/ienap/ienamus-a
+A+Attr:%> # ;
+A: A_IEDNAK_s2 ; 

LEXICON A_IEDNAK_s2 
+Sg+Nom:g2%> K ; 
:g2%> N_ODD_X ; 
+Comp:^WG A_ODD_COMP ; ! normally ienabu (but Rensund: ienaba)
+Comp+Use/NG:^WG A_ODD_COMP_a ;
+Superl:^WG A_ODD_SUP ;
+Superl+Use/NG:^WG A_ODD_jSUP ;


LEXICON A_NAGAN ! nagan/nagan/nagana etc. (no COMP/SUPERL)
+A+Attr:%> # ;
+A+Sg+Nom:%> K ;
+A: N_ODD_X ; 


LEXICON A_GIENES ! gienes/gienes/gädnása/?/?
+A+Attr:s^WG%> # ;
+A: A_GIENES_s2 ; 

LEXICON A_GIENES_s2 
+Sg+Nom:s^WG%> K ; 
+Pl+Nom:s^V2E2AA N_ODD_X ; 
!+Comp:^V2E2AA A_EVEN_COMP ;
!+Superl:^V2E2AA A_EVEN_SUP ;


LEXICON A_GÅJKES ! gåjjke/gåjkes/gåjjkása/gåjjkáp/gåjjkájmus-
+A+Attr:%> # ;
+A: A_GÅJKES_s2 ; 

LEXICON A_GÅJKES_s2 
+Sg+Nom:^WG%>s K ; 
+Pl+Nom:%>^V2E2AAsa K ; 
+Comp: A_ODD_COMP_V2E2AA ;
+Superl: A_ODD_jSUP_V2E2AA ;

LEXICON A_KUORA ! kuora 'left over, kvar' NO ATTR FORM!
+A: A_KUORA_s2 ; 

LEXICON A_KUORA_s2 
+Sg+Nom:%> K ; 
+Pl+Nom:%> K ; 
+Comp:^WG A_ODD_COMP_V2E2AA ;
+Superl:^WG A_ODD_jSUP_V2E2AA ;

LEXICON A_VAASSE ! vásse, färrta, jåhteje (only ATTR, no PRED-forms!)
+A+Attr:%> # ;
!not sure about Comp/Superl !árra -> árajmus


LEXICON A_UMASSE ! umas/umas'se/umasse 'all kinds of' 
+A+Attr:%> # ;
+A:'s A_UMASSE_s2 ; 

LEXICON A_UMASSE_s2 
+Sg+Nom:%>e K ; 
+Pl+Nom:^WG%>e K ; 
+Sg+Acc:^WG%>ev K ; 
!+Pl+Acc:^WG%>ijd K ; not sure about this, plus VH won't work b/c 3 syllables!
!+Comp: A_ODD_COMP_V2E2AA ;
!+Superl: A_ODD_jSUP_V2E2AA ;

LEXICON A_JUHKE !juges/juhke/juhke ??
+A+Attr:^WG%>s # ;
+A: A_JUHKE_s2 ; 

LEXICON A_JUHKE_s2 
+Sg+Nom:%> K ; 
+Pl+Nom:%> K ; 
!+Comp: A_ODD_COMP_V2E2AA ;
!+Superl: A_ODD_jSUP_V2E2AA ;


LEXICON A_SJAEVDNJET ! sjevdnjis/sjävdnjet/sjävdnjeda/sjävdnjedup/sjävdnjemus- !unexpected forms, but evidence from wordlist
+A+Attr:^IJ%>s # ;
+A: A_SJAEVDNJET_s2 ; 

LEXICON A_SJAEVDNJET_s2 
+Sg+Nom:d2%> K ; 
+Pl+Nom:d2%>a K ; 
+Comp:^WG A_ODD_COMP ;
+Superl:^WG A_ODD_SUP ;




! COMP/SUPERL rules:
!COMP:
!if even-syllabled COMP-ATTR wordform and V2 ≠ e, then V2-p in ATTR, SG.NOM, -V2-ba in PL.NOM
LEXICON A_ODD_COMP
:%>b2 A_ODD_COMP_s2 ;

!if even-syllabled COMP-ATTR wordform and V2 = a, then V2-p in ATTR, SG.NOM, -V2-bu in PL.NOM
LEXICON A_ODD_COMP_a
:%>b2 A_ODD_COMP_a_s2 ;

!if even-syllabled wordform COMP-ATTR and V2 = e, then -á-p in ATTR, SG.NOM, -á-ba in PL.NOM
LEXICON A_ODD_COMP_V2E2AA 
:%>^V2E2AAb2 A_EVEN_COMP_s2 ;

LEXICON A_ODD_COMP_s2 
+Attr: # ;
+Sg+Nom: # ;
!+Pl+Nom:%>a # ;
:%> N_ODD_X ;

LEXICON A_ODD_COMP_a_s2 
+Attr: # ;
+Sg+Nom: # ;
!+Pl+Nom:%>u # ;
:%> N_ODD_X_adjCompU ;

!if odd-syllabled wordform COMP-ATTR, then -up in ATTR, SG.NOM, -uba/-ubo in PL.NOM (seemingly random for PL.NOM)
LEXICON A_EVEN_COMP 
:u%>b2 A_EVEN_COMP_s2 ;

LEXICON A_EVEN_COMP_s2 
+Attr: # ;
+Sg+Nom: # ;
+Pl+Nom:%> N_ODD_X ;
+Pl+Nom+Use/NG:%> N_ODD_X_adjCompU ;


!SUPERL:
!if even-syllabled SUPERL-ATTR wordform and V2 = e,å,o sometimes a, then -mus
LEXICON A_ODD_SUP
:%>mus A_ODD_SUP_s2 ;

!if even-syllabled SUPERL-ATTR wordform and V2 = á,i, sometimes a, then -jmus
LEXICON A_ODD_jSUP 
:%>jmus A_ODD_SUP_s2 ;

!if even-syllabled SUPERL-ATTR wordform and V2 = e, then -ájmus
LEXICON A_ODD_jSUP_V2E2AA 
:%>^V2E2AAjmus A_ODD_SUP_s2 ;

LEXICON A_ODD_SUP_s2  
+Attr: # ;
+Sg+Nom: # ;
+Sg+Ela:%>t # ; !ienamust (sje19210000a-lagercrantz1957a-427.016)
+Pl+Nom:%>u # ;


!if odd-syllabled SUPERL-ATTR wordform, then -umos in ATTR, SG.NOM, -ubmusa in PL.NOM
LEXICON A_EVEN_SUP 
:%>ubmos A_EVEN_SUP_s2 ;

LEXICON A_EVEN_SUP_s2 
+Attr:^WG^V2O2U # ;
+Sg+Nom:^WG^V2O2U # ;
+Pl+Nom:^V2O2U%>a # ;



!highly unpredictable, suppletive, irregular lemmata
LEXICON A_VIERRE !vieráp, vierájmus (N-Germn loan, including lack of positive form)
+A+Comp:^WG A_ODD_COMP_V2E2AA ;
+A+Superl:^WG A_ODD_jSUP_V2E2AA ;





