! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@uit.no or feedback@divvun.no
!
! Main development by Joshua Wilbur (joshua.wilbur@ut.ee)



! ========== !
! Inflection !
! ========== !

!juollkie+N+Abe             morf =======%> upper
!juollkie^WG%>htahk   trans
!                                     
!juollkie^WG%>Abehtahk   mphon
!juol0kie00htahk         trans ======%> lower

! Noun 
! ===============


! even-syllabic
! -------------

LEXICON N_EVEN ! giella, guolle, bissti
+N: EVENCASE ;  ! Sg Nom, Ess
+N: N_EVEN_ILL ;
+N:^WG N_EVEN_WK ; ! CG here
+N: N_EVEN_DIM ; !DIM derivation
!+N: PX ; !possessive suffixes

LEXICON N_EVEN_O ! rássjo-variant rässjuj
+N: EVENCASE ;  ! Sg Nom, Ess
+N: N_EVEN_ILL ; 
+N: N_EVEN_ILL_O ; ! allows rássjo-variant: rässjuj
+N:^WG N_EVEN_WK ; ! CG here
+N: N_EVEN_O_DIM ; !DIM derivation

LEXICON N_EVEN_NOCG ! without consonant gradation, e.g. bägga<->biegga
+N:^G3 EVENCASE ;
+N:^G3 N_EVEN_ILL ;
+N:^WG N_EVEN_WK ; !
+N: N_EVEN_DIM ; !DIM derivation

LEXICON N_EVEN_NOALT ! without any stem alternation (no C-grad, no umlaut); loan words with -iehka
+N: EVENCASE ;
+N: N_EVEN_ILL ;
+N: N_EVEN_WK ; !
+N: N_EVEN_DIM ; !DIM derivation



LEXICON EVENCASE
+Sg+Nom:^UAUML%> K ;
N_EVEN_ESS ; ! Essive
+Cmp/SgNom:^UAUML R ; !compound element in strong-grade
+Cmp/SgGen:^WG R ; !compound element in weak-grade

LEXICON N_EVEN_ILL
+Sg+Ill:%>j^V2E2AA K ; !V2 e->á in ill.sg, dim

LEXICON N_EVEN_ILL_O
+Sg+Ill+Use/NG:%>j^V2O2U # ; !V2 o->u in ill.sg

LEXICON N_EVEN_ESS
+Ess:^UAUML%>n # ;

LEXICON N_EVEN_WK
+Sg+Gen: K ;
+Sg+Acc:%>v # ;
+Sg+Ine:%>n # ;
+Sg+Ela:%>st # ;
+Sg+Ela+Use/NG:%>s # ; !dialectal variant, but not standard
+Abe:%>dak # ; ! Norm
!+Abe:%>htaka K ; ! Informants S and Bj 
!+Abe:%>htak # ; ! Informants S and Bj 
+Pl+Nom:  K ;
:^IJ%>j N_EVEN_J ;

LEXICON N_EVEN_J
+Pl+Gen:    # ;
+Pl+Acc:t   # ;
+Pl+Acc+Err/Orth:d   # ;
+Pl+Ine:n   # ;
+Pl+Ela:st  # ; !norm
+Pl+Ela+Use/NG:s  # ;
+Pl+Ill:da  K ; 
+Pl+Com:    # ;
+Pl+Com+Use/NG:gum # ;
+Sg+Com:n # ;         !norm
+Sg+Com+Use/NG:na K ; !variant


LEXICON N_EVEN4 !mánnodak<->mánnodag-
+N+Sg+Nom:%> # ;
+N:a N_EVEN4_DEFAULT ;
+N+Cmp/SgNom:%> R ; !compound element
!+N+Cmp/SgGen:%>a R ; !compound element !not sure this is real


LEXICON N_EVEN4_ISA !guoksagis<->guoksagas-
+N+Sg+Nom:i2s%> # ;
+N:asa N_EVEN4_DEFAULT ;

LEXICON N_EVEN4_ASA !mujjtulas<->mujjtulas-
+N+Sg+Nom:as%> # ;
+N:asa N_EVEN4_DEFAULT ;

LEXICON N_EVEN4_TJ !såbmelatj<->såbmelatja- (variant såbmeladtja-)
+N+Sg+Nom:tj%> # ;
+N:tja N_EVEN4_DEFAULT ;
+N+Use/NG:dtja N_EVEN4_DEFAULT ;

LEXICON N_EVEN4_DEFAULT ! from previous step, plus allows ADJs to dock here
+Sg+Acc:%>v # ;
+Sg+Gen:%> K ;
+Sg+Ill:%>j # ;
+Sg+Ine:%>n # ;
+Sg+Ela:%>st # ;
+Sg+Ela+Use/NG:%>s # ;
+Sg+Com:%>jn # ;
+Sg+Com+Use/NG:%>jna K ;
+Ess:%>n # ;
!+Abe:%>dak # ; ! XXX
+Pl+Nom:%> K ;
+Pl+Acc:%>jt # ;
+Pl+Acc+Err/Orth:%>jd # ;
+Pl+Gen:%>j # ;
+Pl+Ill:%>jda K ;
+Pl+Ine:%>jn # ;
+Pl+Ela:%>jst # ;
+Pl+Ela+Use/NG:%>js # ;
+Pl+Com:%>j # ;
+Pl+Com+Use/NGminip:%>jgum # ;
: N_EVEN_DIM ; !DIM derivation


LEXICON N_EVEN_DIM ! guolla->guolatj; guolle->guolátj; mánnodak->mánnodagatj
+Der/Dimin+N:^WG%>tj^V2E2AA N_DIM_s2 ;

LEXICON N_EVEN_O_DIM !gárrvo->gärvutj
+Der/Dimin+N:^WG%>tj^V2O2U N_DIM_s2 ;

LEXICON N_JUHAAN ! Juhán, Juhána- (special N-class for loan (proper-)nouns that have unusual morphophonology (always -a- as V3))
+N+Sg+Nom:^UAUML%>    # ;
+N:a N_EVEN_ESS ; ! Essive
+N:a N_EVEN_ILL ;
+N:a^WG N_EVEN_WK ; ! CG here
+N:a N_EVEN_DIM ; !DIM derivation


!LEXICON N_EVEN4_VSA !guoksagis<->guoksagas-, mujjtulas<->mujjtulas-
!+Sg+Acc:asa%>v # ;
!+Sg+Gen:asa%> K ;
!+Sg+Ill:asa%>j # ;
!+Sg+Ine:asa%>n # ;
!+Sg+Ela:asa%>st # ;
!+Sg+Ela+Use/NG:asa%>s # ;
!+Sg+Com:asa%>jn # ;
!+Sg+Com+Use/NG:asa%>jna K ;
!+Ess:asa%>n # ; 
!!+Abe:asa%>dak # ; ! XXX
!+Pl+Nom:asa%> K ;
!+Pl+Acc:asa%>jd # ;
!+Pl+Gen:asa%>j # ;
!+Pl+Ill:asa%>jda K ;
!+Pl+Ine:asa%>jn # ;
!+Pl+Ela:asa%>jst # ;
!+Pl+Ela+Use/NG:asa%>js # ;
!+Pl+Com:asa%>j # ;
!+Pl+Com+Use/NGminip:asa%>jgum # ;





! odd-syllabic
! ------------

LEXICON N_ODD !standard odd-syllable noun, eg. guohtom<->guohtom-
+N+Sg+Nom:%> # ; 
+N+Cmp/SgNom: R ; !compound element
!+N+Cmp/SgGen:a R ; !compound element; not sure how it works: biednagbärrgo???
+N: N_ODD_X ;
+N: N_ODD_DIM ; !link to DIM

LEXICON N_ODD_WG !standard odd-syllable noun with CGrad, eg. vanas<->vadnas-
+N: N_ODD_WG_s2 ;
+N+Cmp/SgNom:^WG R ; !compound element
!+N+Cmp/SgGen:a R ; !compound element !not sure!

LEXICON N_ODD_WG_s2 !allows ADJ to dock here
+Sg+Nom:^WG%> # ;
N_ODD_X ;
N_ODD_DIM ; !link to DIM

LEXICON N_ODD_OPEN !open-syllable, eg. biena<->biednag-, låvdå<->låvvdåk-
+N+Sg+Nom:^WG^CDEL%> K ;
+N+Cmp/SgNom:^WG^CDEL R ; !compound element in weak-grade, no final C
!+N+Cmp/SgGen: R ; !compound element in weak-grade, no final C ! not sure about this
+N: N_ODD_X ;
+N: N_ODD_DIM ; !link to DIM

LEXICON N_ODD_VH !odd-syllable with VH, eg. ålol->ullul- 
+N+Sg+Nom:^WG%> # ;
+N:^V2O2U N_ODD_X ;
+N:^V2O2U N_ODD_DIM ; !link to DIM

LEXICON N_ODD_V2E2AA !odd-syllable with change in V2 from E to Á, eg. åres->årrás-; ALWAYS followed by -s- or -r-
+N+Sg+Nom:^WG%> # ;
+N:^V2E2AA N_ODD_X ;
+N:^V2E2AA N_ODD_DIM ; !link to DIM

LEXICON N_ODD_V2E2AA_G3 !odd-syllable with change in V2 from E to Á without C-gradation, eg. vähttjer->vähttjára-; ALWAYS followed by -s- or -r-
+N+Sg+Nom:%> # ;
+N:^V2E2AA N_ODD_X ;
+N:^V2E2AA N_ODD_DIM ; !link to DIM

LEXICON N_ODD_noCG_G3 !odd-syllable noun III-II but without CGrad, eg. guobbar<->guabbar-
+N+Sg+Nom:%> # ;
+N:^G3^UAUML N_ODD_X ;
!+N+Cmp/SgNom: R ; !compound element !not sure!
!+N+Cmp/SgGen:a R ; !compound element !not sure!

LEXICON N_ODD_noCG_G3_OPEN !open odd-syllable noun III-II but without CGrad, eg. vuodda<->vuaddag-
+N+Sg+Nom:^WG^CDEL%> K ;
+N:^G3^UAUML N_ODD_X ;
!+N+Cmp/SgNom: R ; !compound element !not sure!
!+N+Cmp/SgGen:a R ; !compound element !not sure!

!LEXICON N_ODD_WG_aH !odd-syllable noun with CGrad, nom.sg ends in -a, extension in -ah-, eg. ruma<->rubmah-
!+N+Sg+Nom:a^WG%> K ;
!+N:ah N_ODD_X ;
!!+N: N_ODD_DIM ; !link to DIM !CHECK!

LEXICON N_ODD_WG_eH !odd-syllable noun with CGrad, nom.sg ends in -e, extension in -ih-, eg. gane<->gidnih-
+N+Sg+Nom:e^WG%> K ;
+N:ih N_ODD_X ;
!+N: N_ODD_DIM ; !link to DIM !CHECK!

LEXICON N_ODD_WG_oH !odd-syllable noun with CGrad, nom.sg ends in -o, extension in -uh-, eg. njuono<->njunnuh-
+N+Sg+Nom:o^WG%> K ;
+N:uh N_ODD_X ;
!+N: N_ODD_DIM ; !link to DIM !CHECK!

LEXICON N_ODD_X !N_ODD suffixes (X as in suffiX)
+Sg+Acc:a%>v # ;
+Sg+Gen:a%> K ;
+Sg+Ill:i2%>j # ;
+Sg+Ine:i2%>n # ;
+Sg+Ela:i2%>st # ;
+Sg+Ela+Use/NG:i2%>s # ;
+Sg+Com:i2%>jn # ;
+Ess:i2%>n # ;

!+Ess+Use/NG:^CDEL%>n # ; 
!!needs to delete the final C of the stem! 
!!but shouldn't apply to DIM-forms (currently just adds -n, without V before!)

+Abe:a%>htá K ;
+Pl+Nom:a%> K ;
+Pl+Acc:i2%>jt # ;
+Pl+Acc+Err/Orth:i2%>jd # ;
+Pl+Gen:i2%>j # ;
+Pl+Ill:i2%>jda K ;
+Pl+Ine:i2%>jn # ;
+Pl+Ela:i2%>jst # ;
+Pl+Ela+Use/NG:i2%>js # ;
+Pl+Com:i2%>j # ;
+Pl+Com+Use/NGminip:i2%>jgum # ;

LEXICON N_ODD_X_adjCompU !special case for comparative form of certain bisyllabic adjectives
+Sg+Acc:u%>v # ;
+Sg+Gen:u%> K ;
+Sg+Ill:u%>j # ;
+Sg+Ine:u2%>n # ;
+Sg+Ela:u2%>st # ;
+Sg+Ela+Use/NG:u2%>s # ;
+Sg+Com:u2%>jn # ;
+Ess:u2%>n # ;
+Abe:u%>htá K ;
+Pl+Nom:u%> K ;
+Pl+Acc:u2%>jt # ;
+Pl+Acc+Err/Orth:u2%>jd # ;
+Pl+Gen:u2%>j # ;
+Pl+Ill:u2%>jda K ;
+Pl+Ine:u2%>jn # ;
+Pl+Ela:u2%>jst # ;
+Pl+Ela+Use/NG:u2%>js # ;
+Pl+Com:u2%>j # ;
+Pl+Com+Use/NGminip:u2%>jgum # ;
+Der/Dimin+N:u2%> N_ODD_DIM_s2 ;


LEXICON N_ODD_DIM !N_ODD diminutive root add v3
+Der/Dimin+N:a%> N_ODD_DIM_s2 ;

LEXICON N_ODD_DIM_s2 !N_ODD diminutive root with variant 
:tj N_DIM_s2 ;
+Use/NG:dtj NO_VH_X_s2 ; !add everything but nom.sg


! contracted
! ----------

LEXICON N_CONTR_OJU !båtsoj<->buhtsu
+N+Sg+Nom:oj^WG%> # ;
+N+Cmp/SgNom:oj^WG%> R ; !compound element in weak-grade with final C, e.g. båtsojsujtár
+N+Cmp/SgGen:u%> R ; !compound element in strong-grade without final C, e.g. buhtsubärrgo
N_CONTR_O ;

LEXICON N_CONTR_OU !suolo<->sullu
+N+Sg+Nom:o^WG%> K ;
+N+Cmp/SgNom:o^WG%> R ; !compound element in weak-grade
+N+Cmp/SgGen:o%> R ; !compound element in strong-grade without final C, e.g. buhtsubärrgo
N_CONTR_O ;

LEXICON N_CONTR_O !båtsoj<->buhtsu, suolo<->sullu (except NOM.SG)
+N+Sg+Acc:u%>v # ;
+N+Sg+Gen:u%> K ;
+N+Sg+Ill:u%>j # ;
+N+Sg+Ine:u%>n # ;
+N+Sg+Ela:u%>st # ;
+N+Sg+Ela+Use/NG:u%>s # ;
+N+Sg+Com:u%>jn # ;
+N+Ess:u%>n # ;
+N+Ess:u%>n # ;
+N+Abe:u%>dak # ; ! XXX
+N+Pl+Nom:u%> K ;
+N+Pl+Acc:u%>jt # ;
+N+Pl+Acc+Err/Orth:u%>jd # ;
+N+Pl+Gen:u%>j # ;
+N+Pl+Ill:u%>jda K ;
+N+Pl+Ine:u%>jn # ;
+N+Pl+Ela:u%>jst # ;
+N+Pl+Ela+Use/NG:u%>js # ;
+N+Pl+Com:u%>j # ;
+N+Pl+Com+Use/NGminip:u%>jgum # ;
+N: N_CONTR_O_DIM ; !buhtsutj

LEXICON N_CONTR_AJA !ålmáj<->ålmá
+N+Sg+Nom:á^WGj%> # ;
+N+Cmp/SgNom:á^WGj%> R ; !compound element in weak-grade with final-C
+N+Cmp/SgGen:á%> R ; !compound element in strong-grade with not final-C; cf. ålbmábarrgo
+N+Sg+Acc:á%>v # ;
+N+Sg+Gen:á%> K ;
+N+Sg+Ill:á%>j # ;
+N+Sg+Ine:á%>n # ;
+N+Sg+Ela:á%>st # ;
+N+Sg+Ela+Use/NG:á%>s # ;
+N+Sg+Com:á%>jn # ;
+N+Ess:á%>n # ; !check ^WG - is it required by ESS? is it implemented by twolc?
!+N+Ess:á^WGn # ; !check ^WG - is it required by ESS? is it implemented by twolc?
+N+Abe:á%>dak # ; ! XXX
+N+Pl+Nom:á%> K ;
+N+Pl+Acc:á%>jt # ;
+N+Pl+Acc+Err/Orth:á%>jd # ;
+N+Pl+Gen:á%>j # ;
+N+Pl+Ill:á%>jda K ;
+N+Pl+Ine:á%>jn # ;
+N+Pl+Ela:á%>jst # ;
+N+Pl+Ela+Use/NG:á%>js # ;
+N+Pl+Com:á%>j # ;
+N+Pl+Com+Use/NGminip:á%>jgum # ;
+N: N_CONTR_DIM ; !ålmátj


LEXICON N_CONTR_ESAA !sarves<->sarvvá
+N+Sg+Nom:e^WGs%> # ;
+N+Cmp/SgNom:e^WGs%> R ; !compound element in weak-grade with final-C
+N+Cmp/SgGen:á%> R ; !compound element in weak-grade with final-C ; not sure! cf. fállábärrgo
+N+Sg+Acc:á%>v # ;
+N+Sg+Gen:á%> K ;
+N+Sg+Ill:á%>j # ;
+N+Sg+Ine:á%>n # ;
+N+Sg+Ela:á%>st # ;
+N+Sg+Ela+Use/NG:á%>s # ;
+N+Sg+Com:á%>jn # ;
+N+Ess:á%>n # ; !check ^WG - is it required by ESS? is it implemented by twolc?
!+N+Ess:á^WGn # ; !check ^WG - is it required by ESS? is it implemented by twolc?
+N+Abe:á%>dak # ; ! XXX
+N+Pl+Nom:á%> K ;
+N+Pl+Acc:á%>jt # ;
+N+Pl+Acc+Err/Orth:á%>jd # ;
+N+Pl+Gen:á%>j # ;
+N+Pl+Ill:á%>jda # ;
+N+Pl+Ine:á%>jn # ;
+N+Pl+Ela:á%>jst # ;
+N+Pl+Ela+Use/NG:á%>js # ;
+N+Pl+Com:á%>j # ;
+N+Pl+Com+Use/NGminip:á%>jgum # ;
+N: N_CONTR_DIM ; !sarrvátj


LEXICON N_CONTR_O_DIM ! buhtsutj
+Der/Dimin+N:u%>tj N_DIM_s2 ;

LEXICON N_CONTR_DIM ! ålmátj, sarrvátj
+Der/Dimin+N:á%>tj N_DIM_s2 ;



!general addition of DIM case/number endings (avoids recursive DIM generation)
LEXICON N_DIM_s2 !nom.sg, then link to other forms
!+Sg+Nom: # ;
!: N_ODD_X ;
: NO_VH_X ;

LEXICON PX ! possessive suffixes (not clearly productive; initial SgNom set from Lehtiranta 1992)
+Sg+Nom+PxSg1:m # ; !cf. Lagercrantz 1957 text 426
+Sg+Nom+PxSg2:t # ; 
+Sg+Nom+PxSg3:s # ; 
+Sg+Nom+PxDu1:ma # ; 
+Sg+Nom+PxDu2:da # ; 
+Sg+Nom+PxDu3:sa # ; 
+Sg+Nom+PxPl1:men # ; 
+Sg+Nom+PxPl2:den # ; 
+Sg+Nom+PxPl3:ska # ; 



LEXICON NO_VH_X  !tetra-syllabic forms such as ODD+DIM, monosyllabic proper Ns (no -i- in suffixes)
+Sg+Nom: # ;
: NO_VH_X_s2 ;

LEXICON NO_VH_X_s2
+Sg+Acc:av # ;
+Sg+Gen:a K ;
+Sg+Ill:ij # ;
+Sg+Ine:in # ;
+Sg+Ela:ist # ; 
+Sg+Ela+Use/NG:is # ; 
+Sg+Com:ijn # ;
+Ess:in # ;
+Pl+Nom:a K ;
+Pl+Acc:ijt # ;
+Pl+Acc+Err/Orth:ijd # ;
+Pl+Gen:ij # ;
+Pl+Ill:ijda K ;
+Pl+Ine:ijn # ;
+Pl+Ela:ijst # ;
+Pl+Ela+Use/NG:ijs # ;
+Pl+Com:ij # ;
+Pl+Com+Use/NGminip:ijgum # ;


! proper noun lexica below:

LEXICON N_MONOSYL_PROPER ! monosyllabic proper nouns (mostly person names)
+N: NO_VH_X ; !add PoS, then link to morphology

!for proper names for which the inflectional paradigms are not clear (at least until more is known about how they inflect – essentially only non-Saami names)
LEXICON N_NAME_noInfl_C !C-final
+N+Sg+Nom:%> # ; 
+N: N_ODD_X ;
!diminutive for C-final names:
+N+Der/Dimin+N:a%>tj N_DIM_s2 ;

LEXICON N_NAME_noInfl_V !
+N: N_NAME_noInfl_V_s2 ;

LEXICON N_NAME_noInfl_V_s2 !V-final
+Sg+Nom: K ;
+Sg+Acc:v # ;
+Sg+Gen: K ;
+Sg+Ill:j # ;
+Sg+Ine:n # ;
+Sg+Ela:st # ; 
+Sg+Ela+Use/NG:s # ; 
+Sg+Com:jn # ;
+Ess:an # ;
+Pl+Nom: K ;
+Pl+Acc:jt # ;
+Pl+Acc+Err/Orth:jd # ;
+Pl+Gen:j # ;
+Pl+Ill:jda K ;
+Pl+Ine:jn # ;
+Pl+Ela:jst # ;
+Pl+Ela+Use/NG:js # ;
+Pl+Com:j # ;
+Pl+Com+Use/NGminip:jgum # ;
!diminutive for V-final names:
+Der/Dimin+N:%>tj^V2E2AA N_DIM_s2 ;


