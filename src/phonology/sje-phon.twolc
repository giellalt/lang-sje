! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or feedback@divvun.no

!! !!!Pite Sámi TWOLC file
!
! $Id$
!
!

Alphabet
  a b c d e f g h i ï j k l m n ŋ o p q r s t u v w x y z æ ä ø ö å
  á é í 
  Å Ä Ö Ø Æ Á
  Á É Ó Ú Í À È Ò Ù Ì Ë Ü Ï Â Ê Ô Û Î Ã Ý þ Ñ Ð
  á é ó ú í à è ò ù ì ë ü ï â ê ô û î ã ý þ ñ ð ß ª ß %-
  b2:b d2:d t2:t j2:j h2:h g2:g
  %^O:o
  ':0

  %^WG:0     !!≈ ; @CODE@ : weak grade
  %^jDEL:0   !!≈ ; @CODE@ : vowel deletion in front of j
  %^MONB4J:0 !!≈ ; @CODE@ : e to á and o to u before j
  %^CDEL:0   !!≈ ; @CODE@ : final consonant c (biednag)
  %^MON:0    !!≈ ; @CODE@ : Monophthong in contract stems
  %^LOW:0    !!≈ ; @CODE@ : Lowering u to å and o (ålol)
  %^UAUML:0    !!≈ ; @CODE@ : uo to uä juolge / juällge
  %^IEUML:0    !!≈ ; @CODE@ : ie to ä etc. gielbar / gællbara
  %^IUML:0   !!= @CODE@ * a to i, gallgat gillgin
  %^IJ:0     !!= @CODE@ * e to i in front of Plural j and Sg Com

! Morpheme boundaries:
 «    ! Derivational prefix
 »    ! Derivational suffix
 %<:0 ! Inflectional prefx
 %>:0 ! Inflectional suffix
 #    ! Word boundary for both lexicalised and dynamic compounds
 %^   ! (exceptional) soft hyphenation point

! End of alphabet definitions
;

Sets
  Letter = a b c d e f g h i ï j k l m n ŋ o p q r s t u v w x y z æ ä ø ö å á é í ó ú '    ;
  Cns    =   b c d   f g h     j k l m n ŋ   p q r s t   v w x   z                     ' ;
  Vow    = a       e       i ï             o           u       y   æ ä ø ö å á é í ó ú 
           %^O ;
  WeG    =  %^WG %^jDEL ;
  Dummy  =  %^WG %^jDEL %^MONB4J %^CDEL %^MON %^UAUML %^IUML %^IJ ;
  
Definitions


 G3 = [ Cns Cns Cns Cns |
      b ':0 b|
      d ':0 d|
      g ':0 g|
      l ':0 l|
      s ':0 s|
      r ':0 r|
      v ':0 v|
      b b m |
      d d [n|j] |
      d n j |
      f f t |
      g g ŋ |
      l l [d|j|t] |
      m m [b|p] |
      n n [d|j|t] |
      ŋ ŋ [g|k] |
      p p [s|t] |
      s s [k|m|n|p|t] |
      b b [j|d|l|r|n] |
      b d n |
      g g d |
      j j [b|d|g|k|l|p|r|s|t|v] |
      j d n | 
      j b m |
      l l [b|g|k|f|p|s|v] |
      l b m |
      m m [g|s] |
      r r [b|d|f|g|j|k|s|t|v] |
      d d g |
      t t k |
      r b m |
      r d n |
      v v [d|g|k|l|r|s] |
      v d n |
      h [k k|p p|t t] |
      d t [j|s] |
      k k [s|t] ] ;



!! !!!Rules

Rules
 
 
!! !!Consonant gradation rules

"Consonant Gradation for htt(j|s):ht(j|s)"
!! __@RULENAME@__  
t:0 <=> Vow: h t _ ( j | s ) (Vow:+) (Cns:+) ( Dummy: ) WeG: ;

! !!€ áhttje^WG
! !!€ áht0je0

"Consonant Gradation for hxx:hx"
!! __@RULENAME@__  
Cx:0 <=> Vow: h Cx _ (Vow:+) (Cns:+) ( Dummy: ) WeG: ;
     where Cx in (p k) ; ! 't' covered by "Consonant Gradation for htt(j|s):ht(j|s)"

! !!€ náhppe^WG
! !!€ náhp0e0

"Consonant Gradation for xdn(j):xn(j)"
!! __@RULENAME@__  
d:0 <=> Vow: Cx _ n (j) Vow: Cns:* ( Dummy: ) WeG: ;
     where Cx in ( j v l ) ;

! !!€ buvdna^WG
! !!€ buv0na0
! 
! ! fake word!
! !!€ skåldnjeg^WG^CDEL
! !!€ skål0nje000

         
"Consonant Gradation for xx:x"
!! __@RULENAME@__  
Cx:0 <=> Vow: Cx _ Vow: Cns:* ( Dummy: ) WeG: ;
     where Cx in ( t k m n ŋ s r l f j v ) ;
     
! !!€ dållå^WG
! !!€ dål0å0
! 
! ! suoŋŋer:suoŋŋer N_ODD ;
! !!€ suoŋŋer^WG
! !!€ suoŋ0er0
! 
! ! tjála:tjállag BIENA "skrift" ;
! !!€ tjállag^WG^CDEL
! !!€ tjál0a000


"Consonant Gradation for xxy:xy"
!! __@RULENAME@__  
 Cx:0 <=> Vow: Cx _ Cy ([g|j|k|m|n|s|t]) Vow: Cns:* ( Dummy: ) WeG: ; !Cx:0 <=> Vow: Cx _ Cy                   (Vow:+) (Cns:+) ( Dummy: ) WeG: ;
 where Cx in ( b b b b b b b d d d d f f f g g g g j j j j j j j j j j j j k k k l l l l l l l l l l m m m m n n n n p p r r r r r r r r r r s s s s s s t t t t ŋ ŋ ŋ v v v v v v v )
       Cy in ( d j l m n r t j k n v d n t d l ŋ v b d g p t k l r s v m t s t v j b d f g k p s t v b g p s d j s t s t b d f g j k p s t v j k n p t v j k m s g k n d k l r g s t )  
          matched ;

! !!€ lu^Okkta^WG
! !!€ luok0ta0
! 
! ! fake word:
! !!€ lu^Okktag^WG^CDEL
! !!€ luok0ta000


"Consonant Gradation for xy:y"
!! __@RULENAME@__  
Cx:0 <=> Vow (Vow:) _ Cy Vow: Cns:* ( Dummy: ) WeG: ; !Cx:0 <=> Vow: _ Cy (Vow:+) (Cns:+) ( Dummy: ) WeG: ;
     where Cx in ( b d d )
           Cy in ( m j n )  
          matched ;

! ! fake word:
! !!€ fábmog^WG^CDEL
! !!€ fá0mo000


!! !!Consonant Gradation for hx:x

"Delete h in hx:y" ! first step
!! __@RULENAME@__  
h:0  <=> Vow _ Cx: ( [ j | s ] ) Vow: Cns:* ( Dummy: ) WeG: ;
    where Cx in (p t k) ;

! !!€ dåhpe^WG
! !!€ då0be0
! 
! !!€ råhtje^WG
! !!€ rå0tje0
! 
! !!€ råhtse^WG
! !!€ rå0tse0


! ! "Delete h before ts in hts:ts" ! old - delete?
! ! !! __@RULENAME@__  
! ! h:0 <=> Vow+ _ t s Vow: Cns:* ( Dummy: ) WeG: ;
! !    
! ! !!€ buhts^WG^LOWoj
! ! !!€ bå0ts00oj


"complete change x unvoiced to y voiced intervocalically in hx:y" ! second step
!! __@RULENAME@__  
Cx:Cy <=> h: _ Vow: Cns:* ( Dummy: ) WeG: ; ! Cx:Cy <=> h: _ ([Vow:+  (Cns:+)]) ( Dummy: ) WeG: ;
    where Cx in (p t k)
          Cy in (b d g)
          matched ;

! !!€ dåhpe^WG
! !!€ då0be0
! 
! !!€ råhtje^WG
! !!€ rå0tje0



"Consonant Gradation for xN:N"
!! __@RULENAME@__  
Cx:0 <=> Vow [l|r|v|j] _ Cy Vow: Cns:* ( Dummy: ) WeG: ; ! <=> Vow _ Cy (Vow:+) (Cns:+) ( Dummy: ) WeG: ;
    where Cx in (b g g )
          Cy in (m n ŋ )
          matched ;

! !!€ vájbmo^WG
! !!€ váj0mo0


! "Steggo or not xxy vs xyy"  ! even Steggo stopping using this
! !! __@RULENAME@__  
! Cx:Cy <=> Vow: Cx _ Cy (Vow:+) (Cns:+) ( Dummy: ) \WeG: ;
!  where Cx in ( b b b b b b b d d d d f f f g g g g j j j j j j j j j j j j k k k l l l l l l l l l l m m m m n n n n p p r r r r r r r r r r s s s s s s t t t t ŋ ŋ ŋ v v v v v v )
!        Cy in ( d j l m n r t j k n v d n t d l ŋ v b d g p t k l r s v m t s t v j b d f g k p s t v b g p s d j s t s t b d f g j k p s t v j k n p t v j k m s g k n d l r g s t )  
!           matched ;
! This does not work as intended:
! The idea was to make a tolerant version of the analyser, 
! accepting both xxy and xyy.
! no longer necessary, even Steggo writes xxy now.



!! !!Other consonant rules 
! =====================

"Final Consonant Deletion"
!! __@RULENAME@__  
Cx:0 <=> _ (Dummy:) %^CDEL: ;
	where Cx in ( b d g s h ) ; ! JW: added h (riebe/riehpeha); what about j (båtsoj/buhtsu)??

! !!€ riehpeh^WG^CDEL
! !!€ rie0be000
! 
! !!€ biednag^WG^CDEL
! !!€ bie0na000


"Final devoicing"
!! __@RULENAME@__  
Cx:Cy <=> _ .#. ;
    where Cx in ( b2 d2 g2 )
          Cy in ( p  t  k  )
          matched ; ! JW: removed pair h2:j (not sure what it refers to)

! !!€ árrad2
! !!€ árrat
! !!€ árrad2e
! !!€ árrade
! 
! !!€ unneb2
! !!€ unnep


"Word Final Simplification in -st" !st->s (eg. elat) variation (descriptive, not normative)
!! __@RULENAME@__  
t2:0 <=> s _ .#. ;

! !!€ burist2
! !!€ buris0


"Fricative Auslautverhärtung" ! variation! (eg. dim-suffix) (descriptive, not normative) (sj -> tj word-finally)
!! __@RULENAME@__  
s:t <=> _ j2: .#. ;

! !!€ almasj2a
! !!€ almasja
! !!€ almasj2
! !!€ almatj

          
! ===========          
!! !!Vowel rules 
! ===========          

!! ! Stem vowels
        
"Vowel Shortening e to i in Front of j-initial suffixes"
!! __@RULENAME@__  
 e:i <=> Cns: _  %^WG:0 (%^IJ:0) %>:0 j ;
 	
!!€ riehpe^WG^IJ>j^MONB4Jt
!!€ r0e0bi000j0t


"e to á in front of j"
!! __@RULENAME@__  
e:á <=>  _ %>: j [ %^MONB4J:0 | %^MON: ] ;
!giedde>gäddáj, gájjse>gájjsáj

"o to u in front of j"
!! __@RULENAME@__  
o:u <=>  Cns (Cns:)  _  ([Dummy: - %^jDEL:0]) %>: (j) %^MONB4J:0 ;
! This rule works both for Ill (without the jDEL) and for Com Sg (with it)
!fällo>felujn
  !!€ mánno>j^MONB4J
  !!€ männu0j0

"u to o in odd stems"
!! __@RULENAME@__  
u:o <=>  Cns:* _ Cns (Dummy:)  %^LOW:0 ;

 !!€ ullul^WG^MONB4J
 !!€ ål0ol

"Delete stem final a before e initial suffix"
!! __@RULENAME@__  
a:0 <=> _  (Dummy:) %>: e ;
        _ %^WG:0 %>: i ; 
! Examples ?? 4 hits in make check
!!€ blabla>en
!!€ blabl00en


!! ! Diphthong rules

"Diphthong simplification in front of j"
!! __@RULENAME@__  
Vy:0 <=> Vx _ Cns:+ Vow: [ %^WG:0 %^IJ:0 | %^WG:0 %>: j %^MONB4J:0 n ] ;
     where Vx in (   u  ) ! i u
           Vy in ( %^O )   ! e o
           matched ;
! ie-i, uo-u, u^O-u           
 !!€ ju^Ollge^WG>j^MONB4Jn
 !!€ ju0l0ge0j0n
           

"o deletion in uo in front of j"
!! __@RULENAME@__  
o:0 <=> u _ Cns:* [Vow:* - a: ] %>: j %^MONB4J:0 ;
! uo > u, not a-stems, muorraj

"i deletion in ie in front of j and in ie:ä"
!! __@RULENAME@__  
i:0 <=> _ e: Cns:* Vow:*         (%^WG:0)(%^IJ:0) %>: (j) %^MONB4J:0 ;
        _ e: G3 [o:|e:|á:|a:|å:] (Cns) %^UAUML:0 ;
! ie > e iello>elluj

"e to ä in ie"
!! __@RULENAME@__  
e:ä <=> i: _ Cns:* Vow:*         (%^WG:0)(%^IJ:0) %>: (j) %^MONB4J:0 ; 
           _ G3 [o:|e:|á:|a:|å:] (Cns:) %^UAUML:0 ;

!!€ gied'de^WG^IJ>jd
!!€ g0äd0di000jd



"ä deletion in uä in front of final i/j"
ä:0 <=> u _ Cns:* :i (Dummy:) %>: [i|j] ;
!!€ juällge^WG>jd
!!€ ju0l0gi00jd

"uO to ua in front of j"
!! __@RULENAME@__  

!%^O:a <=> u _ Cns:+ [Vow:* - a: ] %>: j %^MONB4J:0 ;
%^O:a <=> u _ Cns:+ Vow:* %>: j %^MONb4J:0 ; 
!^O becomes a when preceded by u, followed by 1+C and V plus one morpheme-border plus j + ^MONB4J
          u _ G3 a %^UAUML:0 ;
!tsuabbtsa:tsu^Obbtsa N_EVEN "bts" ;
!!€ tsu^Obbtsa>j^MONb4J
!!€ tsuabbtsa0j0

"uo - uä Umlaut"
!! __@RULENAME@__  
%^O:ä <=> _ G3 e %^UAUML:0 ;
 !!€ ju^Ollge^UAUML
 !!€ juäll0ge0

!!€ a
!!€ b

!! ! Monophthong rules 

"á to ä and å to u and a to i in front u and i"
Vx:Vy <=> [Cns|#:] _ Cns:*  [:u|:i] (%^WG:0) %>: j %^MONB4J:0 ;
          [Cns|#:] _ Cns:*  i %^IUML:0   ;
	where Vx in (a á å)
	      Vy in (i ä u)
	      matched ;
	      ! båtsoj>buhttsu, rássjo>rässjuj,árrbe>ärbujn, gallgat>gilgiv, åbba>ubbijda 	      

!Vx:Vy <=> [Cns|#:] _ Cns:*  [a:|e:|o:] (%^WG:0) %>: %^MONB4J:0 j ;


"á to ä in ij forms"
 á:ä <=> _ Cns:+ Vow: %^WG:0 %^IJ:0 %>:0 j ;

!!€ ájjge^WG^IJ>j
!!€ äj0gi000j

"u to å in odd stems"
!! __@RULENAME@__  
u:å <=> _ Cns:* Vow: Cns:* (Dummy:)  %^LOW:0 ;
! ålol>ullula


! Gamla reglar 
! Gamla reglar 
! Gamla reglar 
! Gamla reglar 
! Gamla reglar 
! Gamla reglar 
! Gamla reglar 
! Gamla reglar 


! "ä to ie in weak grade 1"
! !! __@RULENAME@__  
! ä:e <=> [Cns|#] :i _ Cns:+ Vow:+ (Dummy:) WeG: ;
! !hittar inget exempel
! 
! "ae to ie in weak grade 2"
! !! __@RULENAME@__  
! 0:i <=> [Cns|#] _ ä: Cns:+ Vow:+ ( Dummy: ) WeG: ;





