! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or feedback@divvun.no

!! !!!Pite Sámi TWOLC file
!
! $Id$
!
!

Alphabet
  a b c d e f g h i ï j k l m n ŋ o p q r s t u v w x y z æ ä ø ö å
  á é í 
  Å Ä Ö Ø Æ Á
  Á É Ó Ú Í À È Ò Ù Ì Ë Ü Ï Â Ê Ô Û Î Ã Ý þ Ñ Ð
  á é ó ú í à è ò ù ì ë ü ï â ê ô û î ã ý þ ñ ð ß ª ß %-
  b2:b d2:d g2:g t2:t j2:j h2:h
  %^O:o
  ':0

  %^WG:0     !!≈ ; @CODE@ : weak grade
  %^ILLE2AA:0 !!≈ ; @CODE@ : e to á before j
  %^CDEL:0   !!≈ ; @CODE@ : delete final consonant odd (biednag)
  %^MON:0    !!≈ ; @CODE@ : Monophthong in contract stems
  %^UAUML:0    !!≈ ; @CODE@ : uo to uä juolge / juällge
  %^IEUML:0    !!≈ ; @CODE@ : ie to ä etc. gielbar / gællbara
  %^IUML:0   !!= @CODE@ * a to i, gallgat gillgin
  %^IJ:0     !!= @CODE@ * e to i in front of Plural j and Sg Com
  %^V2O2U:0 !!= @CODE@ * o to u in V2 (e.g. Ill.Sg, Dim, some N_ODD) etc.
  %^VHBAR:0 !!= @CODE@ * block VH from happening

! Morpheme boundaries:
 «    ! Derivational prefix
 »    ! Derivational suffix
 %<:0 ! Inflectional prefx
 %>:0 ! Inflectional suffix
 #    ! Word boundary for both lexicalised and dynamic compounds
 %^   ! (exceptional) soft hyphenation point

! End of alphabet definitions
;

Sets
  Letter = a b c d e f g h i ï j k l m n ŋ o p q r s t u v w x y z æ ä ø ö å á é í ó ú '    ;
  Cns    =   b c d   f g h     j k l m n ŋ   p q r s t   v w x   z                     '
             b2 d2 g2 t2 j2 h2 ;
  Vow    = a       e       i ï             o           u       y   æ ä ø ö å á é í ó ú 
           %^O ;
  Dummy  =  %^WG %^ILLE2AA %^CDEL %^MON %^UAUML %^IUML %^IJ %^V2O2U %^VHBAR ;
  
Definitions


 G3 = [ Cns Cns Cns Cns |
      b ':0 b|
      d ':0 d|
      g ':0 g|
      l ':0 l|
      s ':0 s|
      r ':0 r|
      v ':0 v|
      b b m |
      d d [n|j] |
      d n j |
      f f t |
      g g ŋ |
      l l [d|j|t] |
      m m [b|p] |
      n n [d|j|t] |
      ŋ ŋ [g|k] |
      p p [s|t] |
      s s [k|m|n|p|t] |
      b b [j|d|l|r|n] |
      b d n |
      g g d |
      j j [b|d|g|k|l|p|r|s|t|v] |
      j d n | 
      j b m |
      l l [b|g|k|f|p|s|v] |
      l b m |
      m m [g|s] |
      r r [b|d|f|g|j|k|s|t|v] |
      d d g |
      t t k |
      r b m |
      r d n |
      v v [d|g|k|l|r|s] |
      v d n |
      h [k k|p p|t t] |
      d t [j|s] |
      k k [s|t] ] ;



!! !!!Rules

Rules
 
 
!! !!Consonant gradation rules

"Consonant Gradation for htt(j|s):ht(j|s)"
!! __@RULENAME@__  
t:0 <=> Vow: h t _ ( j | s ) (Vow:+) (Cns:+) ( Dummy: ) %^WG: ;

! !!€ áhttje^WG
! !!€ áht0je0

"Consonant Gradation for hxx:hx"
!! __@RULENAME@__  
Cx:0 <=> Vow: h Cx _ (Vow:+) (Cns:+) ( Dummy: ) %^WG: ;
     where Cx in (p k) ; ! 't' covered by "Consonant Gradation for htt(j|s):ht(j|s)"

! !!€ náhppe^WG
! !!€ náhp0e0

"Consonant Gradation for xdn(j):xn(j)"
!! __@RULENAME@__  
d:0 <=> Vow: Cx _ n (j) Vow: Cns:* ( Dummy: ) %^WG: ;
     where Cx in ( j v l ) ;

! !!€ vadnas^WG
! !!€ va0nas0
! !!€ gu^Ohtom
! !!€ guohtom

! !!€ buvdna^WG
! !!€ buv0na0
! ! fake word!
! !!€ skåldnjeg^WG^CDEL
! !!€ skål0nje000

         
"Consonant Gradation for xx:x"
!! __@RULENAME@__  
Cx:0 <=> Vow: Cx _ Vow:+ Cns:* ( Dummy: ) %^WG: ;
     where Cx in ( t k m n ŋ s r l f j v ) ; !not bb, dd, gg (auch nicht dtj, dts)
     
! !!€ dållå^WG
! !!€ dål0å0
! ! suoŋŋer:suoŋŋer N_ODD ;
! !!€ suoŋŋer^WG
! !!€ suoŋ0er0
! ! tjála:tjállag BIENA "skrift" ;
! !!€ tjállag^WG^CDEL
! !!€ tjál0a000


"Consonant Gradation for xxy:xy"
!! __@RULENAME@__  
Cx:0 <=> Vow: Cx _ Cy Vow: Cns:* ( Dummy: ) %^WG: ;
! Cx:0 <=> Vow: Cx _ Cy ([g|j|k|m|n|s|t]) Vow: Cns:* ( Dummy: ) %^WG: ;
 where Cx in ( b b b b b b b d d d d f f f g g g g j j j j j j j j j j j k k k l l l l l l l l l l m m m m n n n n p p r r r r r r r r r r s s s s s s s t t t t ŋ ŋ ŋ v v v v v v v )
       Cy in ( d j l m n r t j k n v d n t d l ŋ v b d g p t k l r s v m s t v j b d f g k p s t v b g p s d j s t s t b d f g j k p s t v j k m n p t v j k m s g k n d k l r g s t )  
          matched ;

! !!€ lu^Okkta^WG
! !!€ luok0ta0
! ! fake word:
! !!€ lu^Okktag^WG^CDEL
! !!€ luok0ta000


"Consonant Gradation for xy:y"
!! __@RULENAME@__  
Cx:0 <=> Vow (Vow:) _ Cy Vow: Cns:* ( Dummy: ) %^WG: ; !Cx:0 <=> Vow: _ Cy (Vow:+) (Cns:+) ( Dummy: ) %^WG: ;
     where Cx in ( b d d )
           Cy in ( m j n )  
          matched ;

! ! fake word:
! !!€ fábmog^WG^CDEL
! !!€ fá0mo000


!! !!Consonant Gradation for hx:x

"Delete h in hx:y" ! first step
!! __@RULENAME@__  
h:0  <=> Vow _ Cx: ( [ j | s ] ) Vow: Cns:* ( Dummy: ) %^WG: ;
    where Cx in (p t k) ;

! !!€ dåhpe^WG
! !!€ då0be0
! !!€ råhtje^WG
! !!€ rå0tje0
! !!€ råhtse^WG
! !!€ rå0tse0


"Intervocalic voiced plosives in hx:y" ! second step
!! __@RULENAME@__  
Cx:Cy <=> h: _ Vow: Cns:* ( Dummy: ) %^WG: ; ! Cx:Cy <=> h: _ ([Vow:+  (Cns:+)]) ( Dummy: ) %^WG: ;
    where Cx in (p t k)
          Cy in (b d g)
          matched ;

! !!€ dåhpe^WG
! !!€ då0be0
! !!€ råhtje^WG
! !!€ rå0tje0



"Consonant Gradation for xN:N" ! vájbmo
!! __@RULENAME@__  
Cx:0 <=> Vow [l|r|v|j] _ Cy Vow: Cns:* ( Dummy: ) %^WG: ; ! <=> Vow _ Cy (Vow:+) (Cns:+) ( Dummy: ) %^WG: ;
    where Cx in (b g g )
          Cy in (m n ŋ )
          matched ;

! !!€ vájbmo^WG
! !!€ váj0mo0


!! !!Other consonant rules 
! =====================

"Final C Deletion"
!! __@RULENAME@__  
Cx:0 <=> Vow: _ %^WG:  %^CDEL: ;
	where Cx in ( b d g s h ) ; 

! !!€ riehpeh^WG^CDEL
! !!€ rie0be000
! !!€ biednag^WG^CDEL
! !!€ bie0na000


"Final devoicing"
!! __@RULENAME@__  
Cx:Cy <=> _ (%^WG:) .#. ;
    where Cx in ( b2 d2 g2 )
          Cy in ( p  t  k  )
          matched ; ! JW: removed pair h2:j (not sure what it refers to)

!!€ gu^Ossmag2^WG
!!€ guos0mak0
!!€ iejgád2
!!€ iejgát
!!€ gu^Ossmag2a>v
!!€ guassmaga0v
!!€ iejgád2^VHBARi>jd
!!€ iejgád0i0jd
!!€ mu^Ohttag^WG^CDEL
!!€ muoht0a000
!!€ mu^Ohttaga>v
!!€ muahttaga0v
! !!€ árrad2
! !!€ árrat
! !!€ árrad2e
! !!€ árrade
! !!€ unneb2
! !!€ unnep


"Word Final Simplification in -st" !st->s (eg. elat) variation (descriptive, not normative)
!! __@RULENAME@__  
t2:0 <=> s _ .#. ;

! !!€ burist2
! !!€ buris0


"Fricative Auslautverhärtung" !variation (eg. dim-suffix) (descriptive, not normative) (sj - tj word-finally)
!! __@RULENAME@__  
s:t <=> _ j2: .#. ;




! ===========          
!! !!Vowel rules 
! ===========          

!! ! raising second syllable's vowel

"E to I in V2 before j-suffixes" ! guolle -> gulij
!! __@RULENAME@__  
e:i <=> Vow:+ Cns:+ _ (Dummy:) %^IJ:0 ; !guolle<->gulij juällge<->julgij sábme<->sämij

!! ! metaphony

"Default VH" !regressive vowel height assimilation in V1 with i/u in V2" ! á:ä a:i ä:e å:u ^O:0
!! __@RULENAME@__  
Vx:Vy <=> _ \%^VHBAR:* [:i|:u] ; !Cns:* Dummy:* %>:* ; !guolle<->gulij juällge<->julgij sábme<->sämij båtsoj<->buhtsu (last one w/o morpheme boundary!)
    where Vx in ( á a ä å )
          Vy in ( ä i e u )
          matched ; 
! Vx:Vy <=> [.#.|Cns|#|i] _ Cns:+ \%^VHBAR:* [:i|:u] ; !Cns:* Dummy:* %>:* ; !guolle<->gulij juällge<->julgij sábme<->sämij båtsoj<->buhtsu (last one w/o morpheme boundary!)

! !nom.sg
! !!€ almatj
! !!€ almatj
! !nom.pl
! !!€ almatja
! !!€ almatja
! !acc.sg
! !!€ almatja%>v
! !!€ almatja0v
! !acc.pl
! !!€ almatj^VHBARi%>j
! !!€ almatj0i0j
! 
! 
! !nom.sg
! !!€ biednag^WG^CDEL
! !!€ bie0na000
! !nom.pl
! !!€ biednaga
! !!€ biednaga
! !acc.sg
! !!€ biednaga%>v
! !!€ biednaga0v
! !acc.pl
! !!€ biednag^VHBARi%>j
! !!€ biednag0i0j
! 
! !!€ båhtsoj^WG
! !!€ bå0tsoj0
! !!€ båhtsu
! !!€ buhtsu
! !!€ båhtsu%>v
! !!€ buhtsu0v


"Special VH for u^O" ! u^O:u0
!! __@RULENAME@__  
%^O:0 <=> Cns:* u _ Cns:+ [:i|:u] Cns:* Dummy:* %>:* ; !juällge<->julgij

"Special VH for ie" ! ie:0e
!! __@RULENAME@__  
i:0 <=> Cns:* _ e: Cns:+ [:i|:u] Cns:* Dummy:* %>: ; !iello->0elluj
		Cns:* _ e: G3 ; !biegga->b0ägga

"Ä in G3" ! ie:0ä (bägge, bävvde); can be word-initial
!! __@RULENAME@__  
e:ä <=>  Cns:* i:0 _ G3 %^UAUML:* ;

! ! vielmes:vielbm N_CONTR_ESA
! ! nom.sg
! !!€ vielbme^WGs
! !!€ viel0me0s
! ! acc.sg
! !!€ vielbmáv
! !!€ v0älbmáv
! ! acc.sg !why doesn't this fail?!? "Special VH for ie" and "Ä in G3" are both fulfilled!
! !!€ vielbmáv
! !!€ vielbmáv

"Default UA in G3" ! u^O:ua (juallgáj, luakkta); always with word-initial C
!! __@RULENAME@__  
%^O:a <=>  Cns:* u _ G3 ; ! use ^G3?? ^UAUML?? ?better way to write at least 2 Cns?

! !!€ mu^Ohttag^WG^CDEL
! !!€ muoht0a000

"Special UÄ (VH) in G3" ! u^O:ua (juällge); always with word-initial C
!! __@RULENAME@__  
%^O:ä <=>  Cns:* u _ G3 :e ; ! use ^G3?? ^UAUML?? ?better way to write at least 2 Cns?

"V2 E to Á" ! juällge->juallgáj, sábme->sábmáj, guolle->guolátj
!! __@RULENAME@__  
e:á <=> Vow:+ Cns:+ _ (Dummy:*) %>:+ Cns:+ %^ILLE2AA:0 ;

"V2 O to U" ! rássjo->rässjuj, iello->elluj (variant), ålol<-ullul-
!! __@RULENAME@__  
o:u <=> Vow:+ Cns:+ _ (Dummy:*) %>:+ Cns:+ %^V2O2U:0 ;
		Vow:+ Cns:+ _ Cns:+ %^V2O2U:0 %^VHBAR:* Vow:+ ;

! !!€ ålbmaj^WG
! !!€ ål0maj0
! !!€ ålbma>v
! !!€ ålbma0v
! !!€ ålbma>j
! !!€ ålbma0j

! !!€ ållol^WG
! !!€ ål0ol0
! !!€ ållol^V2O2Ua>v
! !!€ ullul0a0v
! !!€ ållol^V2O2U^VHBARi>j
! !!€ ullul00i0j
! !!€ rássjo>j^V2O2U
! !!€ rässju0j0
! !!€ rássjo>j^ILLE2AA
! !!€ rássjo0j0

! ! gen.pl
! !!€ ju^Ollge^WG^IJ>j
! !!€ ju0l0gi000j
! ! ill.sg
! !!€ ju^Ollge^UAUML>j^ILLE2AA
! !!€ juallgá00j0
! ! nom.sg
! !!€ ju^Ollge^UAUML
! !!€ juällge0
! ! nom.pl
! !!€ ju^Ollge^WG
! !!€ juol0ge0

!!€ sábme>j^ILLE2AA
!!€ sábmá0j0
!!€ sábme^WG^IJ>j
!!€ sä0mi000j
!!€ iello^WG^IJ>j
!!€ iel0o000j
!!€ ållge^WG
!!€ ål0ge0
!!€ lu^Okkta^UAUML
!!€ luakkta0
!!€ bievvde^UAUML
!!€ b0ävvde0


!!€ a
!!€ b


