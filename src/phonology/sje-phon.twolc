! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or feedback@divvun.no

!! !!!Pite Sámi TWOLC file
!
! $Id$
!
!

Alphabet
  a b c d e f g h i ï j k l m n ŋ o p q r s t u v w x y z æ ä ø ö å
  á é í 
  Å Ä Ö Ø Æ Á
  Á É Ó Ú Í À È Ò Ù Ì Ë Ü Ï Â Ê Ô Û Î Ã Ý þ Ñ Ð
  á é ó ú í à è ò ù ì ë ü ï â ê ô û î ã ý þ ñ ð ª %- ß
  i2:i u2:u ä2:ä
  b2:b d2:d g2:g t2:t j2:j d9:d
  %^O:o
  ':0

  %^WG:0     !!≈ ; @CODE@ : weak grade
  %^G3:0     !!≈ ; @CODE@ : marks grade three for stems w/o Cgrad
  %^V2E2AA:0 !!≈ ; @CODE@ : e to á in V2 (e.g. ILL.SG, DIM, 1/2-Sg)
  %^CDEL:0   !!≈ ; @CODE@ : delete final consonant odd (biednag)
  %^MON:0    !!≈ ; @CODE@ : Monophthong in contracted stems
  %^UAUML:0    !!≈ ; @CODE@ : uo to uä juolge / juällge
  %^IEUML:0    !!≈ ; @CODE@ : ie to ä, gielbar / gällbara
  %^IUML:0   !!= @CODE@ * a to i, gallgat gillgin
  %^IJ:0     !!= @CODE@ * e to i in front of Plural j and Sg Com
  %^V2O2U:0 !!= @CODE@ * o to u in V2 (e.g. Ill.Sg, Dim, some N_ODD) etc.

! Literal quotes and angles must be escaped (cf morpheme boundaries further down):
 »7     ! »
 «7     ! «
 %[%>%] ! >
 %[%<%] ! <

! Morpheme boundaries:
 «    ! Derivational prefix
 »    ! Derivational suffix
 %<:0 ! Inflectional prefx
 %>:0 ! Inflectional suffix
 #    ! Word boundary for both lexicalised and dynamic compounds
 %^   ! (exceptional) soft hyphenation point

! End of alphabet definitions
;

Sets
  Letter = a b c d e f g h i ï j k l m n ŋ o p q r s t u v w x y z æ ä ø ö å á é í ó ú ' ;
  Cns    =   b c d   f g h     j k l m n ŋ   p q r s t   v w x   z                     '
             b2 d2 g2 t2 j2 ;
  Vow    = a       e       i ï             o           u       y   æ ä ø ö å á é í ó ú 
                           i2              %^O         u2            ä2 ;
  Dummy  =  %^WG %^G3 %^V2E2AA %^CDEL %^MON %^UAUML %^IEUML %^IUML %^IJ %^V2O2U ; 
  
Definitions

 G3 = [ b b n j |
        b b t [j|s] |
        d d n j |
        h t t [j|s] |
        j j s t |
        j j t s |
        j j t j |
        k k s [j|t] |
        k k t [j|s] |
        l d n j |
        l l t [j|s] |
        m m s [j|t] |
        n n j g |
        n n s [k|t] |
        n n t [j|s] |
        p p t [j|s] |
        r r s j |
        r r t [j|s] |
        s s j k |
        s s j m |
        v d n j |
        v v d n |
        v v s [j|k] |
        v v t [j|s] |

        f ':0 f |
        l ':0 l |
        m ':0 m |
        n ':0 n |
        r ':0 r |
        s ':0 s |
        v ':0 v |

        b b [d|j|l|m|n|r] |
        b d n |
        d d [g|j|m|n] |
        d t [j|s] |
        f f [t|n] |
        g g d |
        g g ŋ |
        h [k k|p p|t t] |
        j j [b|d|g|k|l|p|r|s|t|v] |
        j b m |
        j d n | 
        k k [n|s|t] |
        l l [b|f|g|k|p|s|v] |
        l b m |
        l l [d|j|t] |
        m m [b|g|p|s] |
        n n [d|t] |
        ŋ ŋ [g|k|n] |
        p p [s|t] |
        r r [b|d|f|g|j|k|s|t|v] |
        r d j |
        s s [k|m|n|p|t] |
        t t k |
        v v [d|g|j|k|l|r|s] |
        v d n ] ;

 noCG = [ 
        b b |
        d d |
        g g |
        r b m |
        r d n |
        r d n j |
        r g ŋ |
        l g ŋ ] ; ! for consonant centers with 3+ characters that do NOT alternate

 VHtrig = [
        e:i |
        i   |
        o:u |
        u
        ] ; !vowels that can trigger VH (does NOT include i2 and u2)

 V2E2AAtrig = [
        0 |
        v |
        j |
        t j
        ] ; !consonant (incl. digraphs) which can occur before ^V2E2AA

!! !!!Rules

Rules
 
 
!! !!Consonant gradation rules

"Consonant Gradation for htt(j|s):ht(j|s)"
!! __@RULENAME@__  
t:0 <=> Vow: h t _ ( j | s ) (Vow:+) (Cns:+) ( Dummy: ) %^WG: ;

"Consonant Gradation for hxx:hx"
!! __@RULENAME@__  
Cx:0 <=> Vow: h Cx _ (Vow:+) (Cns:+) ( Dummy: ) %^WG: ;
     where Cx in (p k) ; ! 't' covered by "Consonant Gradation for htt(j|s):ht(j|s)"

"Consonant Gradation for xdn(j):xn(j)"
!! __@RULENAME@__  
d:0 <=> Vow: Cx _ n (j) Vow: Cns:* ( Dummy: ) %^WG: ;
		Vow: _ n j Vow: Cns: %^WG: ;
     where Cx in ( j v l ) ;
!		[#|.#.] Cns:+ Vow: _ n j Vow: Cns: %^WG: ;

! !!€ jalgŋe^WGs>0
! !!€ jalgŋe0s00
! !!€ jalgŋá>v
! !!€ jalgŋá0v

! !!€ gållmes^WG>0
! !!€ gå0lmes^WG>0

! !!€ #ádnjas^WG#
! !!€ #á0njas0#
! !!€ #ádnjasa>v#
! !!€ #ádnjasa0v#
! !!€ #gadnjal^WG#
! !!€ #ga0njal0#
! !!€ #vu^Onjes^WG#
! !!€ #vuonjes0#
! !!€ #vu^Onjesa>v#
! !!€ #vuonjasa0v#


"Consonant Gradation for xx:x"
!! __@RULENAME@__  
Cx:0 <=> Vow: Cx _ Vow: Cns:* ( Dummy: ) %^WG: ;
     where Cx in ( t k m n ŋ s r l f j v ) ; !not bb, dd, gg, dtj, dts

     
"Consonant Gradation for xxy:xy"
!! __@RULENAME@__  
Cx:0 <=> Vow:+ Cx _ Cy Vow:+ Cns:* ( Dummy: ) %^WG: ;
     where Cx in ( b b b b b b d d d d d f f g g g g j j j j j j j j j j k k k l l l l l l l l l m m m m n n n p r r r r r r r r r s s s s s s t t t t ŋ ŋ ŋ v v v v v v v )
           Cy in ( d j l m n r j k m n v d n d l ŋ v b d g p k l m r s v n s v j b d f g k p s v b g p s d j s s b d f g j k p s v j k m n p v j k m s g k n d j k l r g s )  
          matched ;
! !!€ tjállag^WG^CDEL
! !!€ tjál0a000
! !!€ tjállaga%>v
! !!€ tjállaga0v

          
"Consonant Gradation for xxt(j|s):xt(j|s)" ! ájjte, gájjtsa
!! __@RULENAME@__  
Cx:0 <=> Vow:+ Cx _ t (j:*|s:*) Vow:+ Cns:* ( Dummy: ) %^WG: ;
     where Cx in ( b f j k l n p r s v ) ;

"Consonant Gradation for xxsj:xsj" ! ákksjo (kksj llsj mmsj ppsj rrsj vvsj) very similar to "Consonant Gradation for xxt(j|s):xt(j|s)"
!! __@RULENAME@__  
Cx:0 <=> Vow:+ Cx _ s j Vow:+ Cns:* ( Dummy: ) %^WG: ;
     where Cx in ( k l m p r v ) ;

! !!€ ájjte^WG
! !!€ áj0te0
! !!€ gájjtsa^WG
! !!€ gáj0tsa0
! !!€ gu^Olle^WG
! !!€ guol0e0
! !!€ gu^Olle^WG^IJ>jd
! !!€ gu0l0i000jd

"Consonant Gradation for xy:y"
!! __@RULENAME@__  
Cx:0 <=> Vow (Vow:) _ Cy Vow: Cns:* ( Dummy: ) %^WG: ; !Cx:0 <=> Vow: _ Cy (Vow:+) (Cns:+) ( Dummy: ) %^WG: ;
     where Cx in ( b d d )
           Cy in ( m j n )  
          matched ;

"Delete h in hx:y" ! first step
!! __@RULENAME@__  
h:0  <=> Vow _ Cx: ( [ j | s ] ) Vow: Cns:* ( Dummy: ) %^WG: ;
    where Cx in (p t k) ;

"Intervocalic voiced plosives in hx:y" ! second step
!! __@RULENAME@__  
Cx:Cy <=> h: _ Vow: Cns:* ( Dummy: ) %^WG: ; ! Cx:Cy <=> h: _ ([Vow:+  (Cns:+)]) ( Dummy: ) %^WG: ;
    where Cx in (p t k)
          Cy in (b d g)
          matched ;

"Consonant Gradation for l/jbm:l/jm" 
!! __@RULENAME@__  
b:0 <=> Vow [l|j] _ m Vow: Cns:* ( Dummy: ) %^WG: ; ! vájbmo, njálbme

"Consonant Gradation for vgŋ:vŋ" 
!! __@RULENAME@__  
g:0 <=> Vow v _ n Vow: Cns:* ( Dummy: ) %^WG: ; ! ávgŋot (unsure about this word!)

"Consonant Gradation for rdj:rj" 
!! __@RULENAME@__  
d:0 <=> Vow r _ j Vow: Cns:* ( Dummy: ) %^WG: ; ! girdje<->girje (non-standard variant)


!! !!Other consonant rules 
! =====================

"Final C Deletion"
!! __@RULENAME@__  
Cx:0 <=> Vow: _ %^WG: %^CDEL: ;
	where Cx in ( b d g s h ) ; 

"Final devoicing"
!! __@RULENAME@__  
Cx:Cy <=> _ (%^WG:) (%>:) # ;
    where Cx in ( b2 d2 g2 )
          Cy in ( p  t 	k )
          matched ; 

! !!€ gål'leg2
! !!€ gål0lek
! !!€ gål'leg2a
! !!€ gål0lega
! !!€ gu^Ossmag2^WG
! !!€ guos0mak0
! !!€ iejgád2
! !!€ iejgát
! !!€ gu^Ossmag2a>v
! !!€ guassmaga0v
! !!€ iejgád2i2>jd
! !!€ iejgád0i0jd
! !!€ mu^Ohttag^WG^CDEL
! !!€ muoht0a000
! !!€ mu^Ohttaga>v
! !!€ muahttaga0v

"Word Final Simplification in -st" !st->s (eg. elat) variation (descriptive, not normative)
!! __@RULENAME@__  
t2:0 <=> s _ .#. ;

! !!€ burist2
! !!€ buris0

"Word-final De-Affricatization for tj " !variation (eg. dim-suffix) (descriptive, not normative) (sj - tj word-finally)
!! __@RULENAME@__  
s:t <=> _ j2: .#. ;




! ===========          
!! !!Vowel rules 
! ===========          

!! ! metaphony

"Default VH" !regressive vowel height assimilation in V1 with i/u in V2" ! á:ä a:i ä:e å:u ^O:0
!! __@RULENAME@__  
	Vx:Vy <=> [#|.#.] Cns:* _ Cns:+ VHtrig Cns:* Dummy:* Vow:* %>:0 ; 
    where Vx in ( á a ä å )
          Vy in ( ä i e u )
          matched ; 
!This requires every root that undergoes inflection to end with a morpheme boundary, even when no suffix exists; this prevents VH in non-final compound stems (since \# wouldn't work). !guolle<->gulij sábme<->sämij båtsoj<->buhtsu (last one w/o morpheme boundary!)

! !!€ ságasti>t
! !!€ ságasti0t
! !!€ ságasta>v
! !!€ ságasta0v

! !!€ #ållol^WG#
! !!€ #ål0ol0#
! !!€ #ållol^V2O2Ua>v#
! !!€ #ullul0a0v#

! !!€ bárdne^WG>0#
! !!€ bárdne000#
! !!€ bárdne^WG^IJ>j#
! !!€ bärdni000j#

! !!€ bähkkum#ståhpo^UAUML#
! !!€ bähkkum#ståhpo0#
! !!€ bähkkum#ståhpo^WG>v#
! !!€ bähkkum#stå0bo00v#
! !!€ bähkku#ståhpo^UAUML#
! !!€ bähkku#ståhpo0#
! !!€ ståhpo^WG>v#
! !!€ stå0bo00v#

! !!€ bajeldis#gárrvo^UAUML#
! !!€ bajeldis#gárrvo0#
! !!€ bajeldis#gárrvo^WG>v#
! !!€ bajeldis#gár0vo00v#
! !!€ gárrvo^WG>v#
! !!€ gár0vo00v#

! !!€ åvvdå#giehtje#
! !!€ åvvdå#giehtje#
! !!€ åvvdå#giehtje^WG#
! !!€ åvvdå#gie0tje0#

! !!€ jievvja^G3
! !!€ j0ävvja0

! !!€ båhtsoj^WG.#.
! !!€ bå0tsoj0.#.
! !!€ båhtsu.#.
! !!€ buhtsu.#.
! !!€ båhtsu%>v.#.
! !!€ buhtsu0v.#.

! !!€ gu^Oksagis#
! !!€ guoksagis#

! ! acc.pl # to make sure VH still works otherwise
! !!€ sábme^WG^IJ>jd
! !!€ sä0mi000jd



"Default UA in G3" ! u^O:ua (juallgáj, luakkta); always with word-initial C
!! __@RULENAME@__  
%^O:a <=> Cns:* u _ G3 [:a|:o|:y|:ä|:å|:á] \%^WG: ; 
          Cns:* u _ noCG [:a|:o|:y|:ä|:å|:á] %^G3:* %^UAUML: ;

"Special UÄ (VH) in G3" ! u^O:ua (juällge); always with word-initial C
!! __@RULENAME@__  
%^O:ä <=> Cns:* u _ G3 :e \%^WG: ; 
          Cns:* u _ Cns:+ :e %^G3: ; ! duädde<->duodde

!!€ nju^Onnje^UAUML
!!€ njuonnje0
!!€ nju^Onnje^WG
!!€ njuon0je0

"Special VH for u^O" ! u^O:u0
!! __@RULENAME@__  
%^O:0 <=> Cns:* u _ Cns:+ VHtrig Cns:* Dummy:* %>:* ; !juällge<->julgij

"Special VH for ie" ! ie:0e
!! __@RULENAME@__  
i:0 <=> Cns:* _ e: Cns:+ VHtrig Cns:* Dummy:* Vow:* %>: ; !iello->0elluj
		Cns:* _ e: G3 Vow: \%^WG: ; !biebbmo->b0äbbmo, häg'ga->hiegga, siebla<->säbblag-
		Cns:* _ e: Cns:+ Vow:+ %^G3: ; !biegga->b0ägga
		Cns:* _ e: noCG Vow: \%^WG: \%^WG: ; !bierdna->b0ärdna !double \%^WG: for gierbma<->gärbmaha  (ie: neither of the two elements after V2 can be ^WG)
		
"Ä in G3" ! ie:0ä (bägga, bävvde); can be word-initial
!! __@RULENAME@__  
e:ä <=> Cns:* i:0 _ G3 Vow: \%^WG: ; !biebbmo->b0äbbmo
        Cns:* i:0 _ Cns:+ Vow:+ %^G3: ; !biegga->b0ägga
		Cns:* i:0 _ noCG Vow: \%^WG: ; !bierdna->b0ärdna


!!€ tjalmed%>ibme^WG
!!€ tjalmed0i0me0

!!€ dibme^WG
!!€ di0me0


!!€ vu^Os'sa^WG
!!€ vuos0sa0

!!€ fiede^G3^UAUML>0
!!€ f0äde0000
!!€ fiede^WG>v
!!€ fiede00v
!!€ fiede^WG^IJ>jn
!!€ f0edi000jn

!!€ gierbmah^WG^CDEL>0
!!€ gierbma00000
!!€ gierbmaha>v
!!€ g0ärbmaha0v

!!€ bierdne^WGs>0
!!€ bierdne0s00
!!€ bierdná>v
!!€ b0ärdná0v

! !!€ ku^Ordna^UAUML
! !!€ kuardna0
! !!€ ku^Ordna^WG
! !!€ kuordna0

! !!€ hieg'ga^UAUML
! !!€ h0äg0ga0
! !!€ hieg'ga^WG
! !!€ hieg0ga0
! !!€ hieg'ga>j^V2E2AA
! !!€ h0äg0ga0j0
! 
! !!€ biebbmo^UAUML
! !!€ b0äbbmo0
! !!€ biebbmo^WG
! !!€ bieb0mo0
! 
! !!€ bievvde^WG
! !!€ biev0de0
! !!€ bievvde^UAUML
! !!€ b0ävvde0
! !!€ biegga^G3^UAUML
! !!€ b0ägga00
! !!€ biegga^WG
! !!€ biegga0

! !!€ rággas
! !!€ rággas
! !!€ rággasa>v 
! !!€ rággasa0v 


"V2 E to I before j-suffixes" ! guolle -> gulij
!! __@RULENAME@__  
e:i <=> Vow:+ Cns:+ _ (Dummy:) %^IJ:0 ; !guolle<->gulij juällge<->julgij sábme<->sämij

"V2 E to Á" ! juällge->juallgáj, sábme->sábmáj, guolle->guolá>tj, vágge->vággáj, båhtet->bådáv
!! __@RULENAME@__  
e:á <=> Vow:+ Cns:+ _ (%^G3:0) (%^WG:0) %>:+ V2E2AAtrig %^V2E2AA:0 ;

"V2 E to Á before S" ! åres->årrás-, tjåskes->tjåsskas-
!! __@RULENAME@__  
e:á <=> Vow:+ Cns:+ _ s %^V2E2AA:0 ;

"V2 O to U" ! rássjo->rässjuj, iello->elluj (variant), ålol<-ullul-
!! __@RULENAME@__  
o:u <=> Vow:+ Cns:+ _ (Dummy:*) %>:+ Cns:+ %^V2O2U:0 ;
		Vow:+ Cns:+ _ Cns:+ %^V2O2U:0 %>:* Vow:+ ; ! turns SUPERL -ubmos- into -ubmusa

! !!€ mu^Ohttag^WG^CDEL
! !!€ muoht0a000
! ! vielmes:vielbm N_CONTR_ESA
! ! nom.sg
! !!€ vielbme^WGs
! !!€ viel0me0s
! ! acc.sg
! !!€ vielbmáv
! !!€ v0älbmáv
!
! !nom.sg
! !!€ almatj
! !!€ almatj
! !nom.pl
! !!€ almatja
! !!€ almatja
! !acc.sg
! !!€ almatja%>v
! !!€ almatja0v
! !acc.pl
! !!€ almatji2%>j
! !!€ almatj0i0j
! 
! 
! !nom.sg
! !!€ biednag^WG^CDEL
! !!€ bie0na000
! !nom.pl
! !!€ biednaga
! !!€ biednaga
! !acc.sg
! !!€ biednaga%>v
! !!€ biednaga0v
! !acc.pl
! !!€ biednagi2%>j
! !!€ biednag0i0j
! 
! !!€ båhtsoj^WG
! !!€ bå0tsoj0
! !!€ båhtsu
! !!€ buhtsu
! !!€ båhtsu%>v
! !!€ buhtsu0v



! !!€ ålbmaj^WG
! !!€ ål0maj0
! !!€ ålbma>v
! !!€ ålbma0v
! !!€ ålbma>j
! !!€ ålbma0j

! !!€ ållol^WG
! !!€ ål0ol0
! !!€ ållol^V2O2Ua>v
! !!€ ullul0a0v
! !!€ ållol^V2O2Ui2>j
! !!€ ullul00i0j
! !!€ rássjo>j^V2O2U
! !!€ rässju0j0
! !!€ rássjo>j^V2E2AA
! !!€ rássjo0j0

! ! gen.pl
! !!€ ju^Ollge^WG^IJ>j
! !!€ ju0l0gi000j
! ! ill.sg
! !!€ ju^Ollge^UAUML>j^V2E2AA
! !!€ juallgá00j0
! ! nom.sg
! !!€ ju^Ollge^UAUML
! !!€ juällge0
! ! nom.pl
! !!€ ju^Ollge^WG
! !!€ juol0ge0

! !!€ sábme>j^V2E2AA
! !!€ sábmá0j0
! !!€ sábme^WG^IJ>j
! !!€ sä0mi000j
! !!€ iello^WG^IJ>j
! !!€ iel0o000j
! !!€ ållge^WG
! !!€ ål0ge0
! !!€ lu^Okkta^UAUML
! !!€ luakkta0
! !!€ bievvde^UAUML
! !!€ b0ävvde0

! !!€ vadnas^WG
! !!€ va0nas0
! !!€ gu^Ohtom
! !!€ guohtom
! !!€ buvdna^WG
! !!€ buv0na0
! ! fake word!
! !!€ skåldnjeg^WG^CDEL
! !!€ skål0nje000
! !!€ dållå^WG
! !!€ dål0å0
! ! suoŋŋer:suoŋŋer N_ODD ;
! !!€ suoŋŋer^WG
! !!€ suoŋ0er0
! ! tjála:tjállag BIENA "skrift" ;
! !!€ tjállag^WG^CDEL
! !!€ tjál0a000

! !!€ árrad2
! !!€ árrat
! !!€ árrad2e
! !!€ árrade
! !!€ unneb2
! !!€ unnep
! !!€ vájbmo^WG
! !!€ váj0mo0
! !!€ dåhpe^WG
! !!€ då0be0
! !!€ råhtje^WG
! !!€ rå0tje0
! !!€ råhtse^WG
! !!€ rå0tse0
! !!€ dåhpe^WG
! !!€ då0be0
! !!€ råhtje^WG
! !!€ rå0tje0
! !!€ riehpeh^WG^CDEL
! !!€ rie0be000
! !!€ biednag^WG^CDEL
! !!€ bie0na000
! !!€ lu^Okkta^WG
! !!€ luok0ta0
! ! fake word:
! !!€ lu^Okktag^WG^CDEL
! !!€ luok0ta000
! ! fake word:
! !!€ fábmog^WG^CDEL
! !!€ fá0mo000

!!€ a
!!€ b


